The story title is "Terra-Nova".
The story author is "P.Rail".
The story headline is "Terra Nova: The Mystery of Zephyr's Landing".
The story genre is "Fiction".
The release number is 1.
The story description is "Explore a far planet and discover your origins."
The story creation year is 2024.

Include Vorple by Juhana Leinonen.
Include Vorple Multimedia by Juhana Leinonen.
Include Vorple Notifications by Juhana Leinonen.
Include Vorple Screen Effects by Juhana Leinonen.
Include Vorple Status Line by Juhana Leinonen.

Release along with the "Vorple" interpreter.
Release along with cover art ("A spaceship in orbit").
Use scoring.

Part 1 - Help and Hints

After reading a command when the player's command includes "examine":
	say "Tip: You can abbreviate EXAMINE to just X[line break]".
	
Understand "help" as requesting help.
Requesting help is an action applying to nothing.

Carry out requesting help:
	say "If you need help, you can:";
	say "[line break]- Type [bold type][italic type]go ?[roman type] for a list of directions you can go.";
	say "- Type [bold type][italic type]commands[roman type] for a list of useful actions you can take in this story.";
	say "- Type [bold type][italic type]sound off[roman type] to turn off sound effects, or [bold type][italic type]sound on[roman type] to turn them on.";
	say "- Type [bold type][italic type]credits[roman type] to learn more about this game.";
	say "- Type [bold type][italic type]hint[roman type] if you're stuck and need a suggestion based on where you are.";
	say "...But be warned: hints can sometimes lead you astray."	
Understand "commands" as requesting commands.
Requesting commands is an action applying to nothing.

Carry out requesting commands:
	say "Use these commands for this story:";
	say "[line break]ask ___ about ___";
	say "[line break]climb ___ (up, down, object)";
	say "[line break]close ___";
	say "[line break]dig";
	say "[line break]examine ___ / read ___";
	say "[line break]gaze ___ (east, west, north, south, northeast, se)";
	say "[line break]go ___ (?, east, west, north, south, inside, outside, ne, nw)";
	say "[line break]hide / hide in ___";
	say "[line break]hint";
	say "[line break]jump over/into/on ___";
	say "[line break]listen";
	say "[line break]look (to reveal current location and description)";
	say "[line break]open ___ / close ___";
	say "[line break]push ___ / press ___ / pull ___";
	say "[line break]pour ___ on ___";
	say "[line break]put ___ in/on ___";
	say "[line break]reflect ___ with ___";
	say "[line break]switch on ___ / switch off ___";
	say "[line break]scoop ___";
	say "[line break]smell";
	say "[line break]sound on / sound off";
	say "[line break]take ___ / drop ___ / give ___";
	say "[line break]throw ___";
	say "[line break]uncover ___ / bury ___";
	say "[line break]wait N turns / wait until Long-Shadow, etc.";
	say "[line break]wear ___";
	say "[line break]Less common commands: save, restore, undo, quit";

Understand "credits" as requesting credits.
Requesting credits is an action applying to nothing.
credit-url is a text that varies.

Carry out requesting credits:
	if Vorple is supported:
		now credit-url is "about.html";
		execute JavaScript command "window.open('[credit-url]', '_blank');";
	otherwise:
		say "Welcome to [story headline]!";
		say "Original concept, story, and game design by [story author], [story creation year]."	
Understand "hint" as requesting a hint.
Requesting a hint is an action applying to nothing.

[How many times did player pull the Shadow Beast's chain]
totalHints is a number that varies. totalHints is 0.
minimumParticipationScore is a number that varies. minimumParticipationScore is 99.

Carry out requesting a hint:
	increase totalHints by 1;
	if the player is in the Blocked Passage:
		follow the Blocked Passage hint system rule;
	otherwise if the player is in the Fallen Shrine:
		follow the Fallen Shrine hint system rule;
	otherwise if the player is in the Lava Pit:
		follow the Lava Pit hint system rule;	otherwise if the player is in the Restricted Area Entrance:
		follow the Restricted Area Entrance hint system rule;	otherwise if the player is in the Stone Watchtower:
		follow the Stone Watchtower hint system rule;
	otherwise:
		say "No specific hint available. Explore your surroundings, and examine objects carefully.";
		now the hint-count of the location is 0.

This is the Blocked Passage hint system rule:
	let hc be the hint-count of the location;
	if the rusty chain is unexamined:
		say "You should examine the rusty chain.";
	otherwise if chainPulls is 0:
		say "What happens when you pull the chain?";
		now the hint-count of the location is 2;
	otherwise if hc is 2:
		say "Ok, something's there. If only there was a way to break the chain.";
		now the hint-count of the location is 3;
	otherwise if hc is 3:
		say "Maybe you can melt the chain somehow?";
		now the hint-count of the location is 4;
	otherwise if hc is 4:
		say "If you're tired of this, you could try climbing.";
		now the hint-count of the location is 2;
	otherwise:		
		continue the action.

This is the Fallen Shrine hint system rule:
	if the altar is unexamined:
		say "You should examine the altar.";
	otherwise if the concave hollow is unexamined:
		say "Inspect any unusual feature of the altar, it might be important later.";
	otherwise if hasSeenOrbContents is true:
		say "No hints available.";
	otherwise if player has rusty box and the rusty box is closed:
		say "I wonder if something in that rusty box fits in the altar.";
	otherwise if hasOpenedTimeCapsule is true:
		say "I wonder what would fit in that hollow.";
	otherwise:
		say "No hints available.";
		
This is the Lava Pit hint system rule:
	let hc be the hint-count of the location;
	if hc is 0:
		say "That lava seems hot enough to burn through anything.";
		increase the hint-count of the location by 1;
	otherwise if the player has the crucible:
		if the player has the heat-resistant glove:
			if the lava sample is enclosed by the player:
				say "No hints available.";
			otherwise:
				say "That lava seems hot enough to burn steel.";
		otherwise:
			say "You're going to need some hand protection.";
	otherwise:		
		say "What sort of container could hold molten lava?";
			
This is the Restricted Area Entrance hint system rule:
	let hc be the hint-count of the location;
	if LabPower is true:
		if hc is 0:
			say "I wonder if walking through the beams would hurt.";
			now the hint-count of the location is 1;
		otherwise if hc is 1:
			say "I wonder if there's a way to deflect the security beams so you can enter.";
			now the hint-count of the location is 2;
		otherwise:
			say "Think about how to turn off the power to the security beams so you can safely enter the hallway.";
			now the hint-count of the location is 0;
	otherwise:
		say "No hints available.";
		
This is the Stone Watchtower hint system rule:
	let hc be the hint-count of the location;
	if hc is 0:
		say "Amazing views from up here. You can probably see your hometown to the northwest.";
		now the hint-count of the location is 1;
	otherwise if observation telescope is unexamined:
		say "Check out the telescope.";
	otherwise if horizon-view-count is 0:
		say "Look through the telescope to see the grand landscape of Terra Nova.";
	otherwise if the player has the rusty box and the rusty box is closed:
		say "I doubt anything would survive a fall from way up here.";
	otherwise if the observation telescope is damaged:
		if the player has the magnifying mirror:
			say "No hints available.";
		otherwise:
			say "That was quite a quake. See what you can salvage.";
	otherwise:		 
		say "No hints available.";
		
[go ? shows directions you can go]
Understand "go [text]" as asking for exits.
Asking for exits is an action applying to one topic.

Carry out asking for exits:
	if the topic understood matches the regular expression "\?":
		let count of exits be the number of viable directions;
		if the count of exits is 0, say "No exit." instead;
		if the count of exits is 1, say "From here, the only path is [list of viable directions].";
		otherwise say "From here, the viable exits are [list of viable directions].";

direction-reminder is a truth state that varies. direction-reminder is true.
Definition: a direction (called thataway) is viable if the room thataway from the location is a room.
Instead of going nowhere:
	let count of exits be the number of viable directions;
	if the count of exits is 0, say "No exit." instead;
	if the count of exits is 1, say "From here, the only path is [list of viable directions].";
	otherwise say "From here, the viable exits are [list of viable directions].";
	if direction-reminder is true:
		say "[line break]Tip: You can type [bold type][italic type]go ?[roman type] to list all possible open paths from your current location.";
		now direction-reminder is false.
	
[toggle sound]
sound-state is a text that varies. sound-state is "on".

Understand "sound" as requesting sound status.
Requesting sound status is an action applying to nothing.

Carry out requesting sound status:
   say "Sound is currently [sound-state].".

Understand "sound on" as turning sound on.
Turning sound on is an action applying to nothing.

Carry out turning sound on:
	now sound-state is "on";
	say "Sound turned on.";
	let currentSound be the sound-file-name of the location;
	let currentLoopCount be the sound-loop-count of the location;
	let currentVolume be the sound-volume of the location;
	let currentAudioHandle be sound-handle of the location;
	if the player is in the Underneath and LabPower is false:
		execute JavaScript command "audioManager.stopAll();";
	otherwise:
		now currentSound is "audioManager.playSound('[sound-state]', '[currentAudioHandle]', 'mp3/[currentSound]', [currentLoopCount],[currentVolume]);";
		execute JavaScript command "[currentSound]";

Understand "sound off" as turning sound off. 
Turning sound off is an action applying to nothing.

Carry out turning sound off:
	now sound-state is "off";
	execute JavaScript command "audioManager.stopAll();";
	say "Sound turned off.".
	
[wait 10 turns]
Waiting for a specified number of turns is an action applying to one number.
Understand "wait [number] turns" as waiting for a specified number of turns.

Carry out waiting for a specified number of turns:
	let N be the number understood;
	repeat with counter running from 1 to N:
		follow the turn sequence rules;
	say "Time passes for [N] turns, total turns so far is [turn count]."

[wait for Night-fall or wait until night-fall]
Waiting for daytime is an action applying to one topic.
Understand "wait until [text]" as waiting for daytime.
Understand "wait for [text]" as waiting for daytime.

Carry out waiting for daytime:
	let target text be the topic understood;
	if target text matches "First-Light" or target text matches "first light":
		if the time-of-day is First-Light:
			say "It is already First-Light.";
			stop;
		while the time-of-day is not First-Light:
			follow the turn sequence rules;
		say "Now it is First-Light.";
	otherwise if target text matches "High-Sun" or target text matches "high sun":
		if the time-of-day is High-Sun:
			say "It is already High-Sun.";
			stop;
		while the time-of-day is not High-Sun:
			follow the turn sequence rules;
		say "Now it is High-Sun.";
	otherwise if target text matches "Long-Shadow" or target text matches "long shadow":
		if the time-of-day is Long-Shadow:
			say "It is already Long-Shadow.";
			stop;
		while the time-of-day is not Long-Shadow:
			follow the turn sequence rules;
		say "Now it is Long-Shadow.";
	otherwise if target text matches "Dimming-Sky" or target text matches "dimming sky":
		if the time-of-day is Dimming-Sky:
			say "It is already Dimming-Sky.";
			stop;
		while the time-of-day is not Dimming-Sky:
			follow the turn sequence rules;
		say "Now it is Dimming-Sky.";
	otherwise if target text matches "Nightfall" or target text matches "nightfall":
		if the time-of-day is Nightfall:
			say "It is already Nightfall.";
			stop;
		while the time-of-day is not Nightfall:
			follow the turn sequence rules;
		say "Now it is Nightfall.";
	otherwise:
		say "You must specify a valid time: First-Light, High-Sun, Long-Shadow, Dimming-Sky, or Nightfall."
	
Part 2 - Global Rules

A thing can be required or unrequired. 
A thing is usually unrequired.

A thing has a text called odor. The odor is usually "".
A room has a text called odor. the odor is usually "".

A thing has a text called sound. The sound is usually "".
A room has a text called sound. The sound is usually "".

A thing has a number called hint-count. The hint-count is usually 0.
A room has a number called hint-count. The hint-count is usually 0.

[Utility command that should be built-in to Inform]
Understand "show all rooms" as showing all rooms.
Showing all rooms is an action applying to nothing.

Carry out showing all rooms:
	let count of rooms be the number of rooms;
	if count of rooms is 0:
		say "There are no rooms.";
	otherwise:
		say "The rooms are: ";
		repeat with R running through rooms:
			say "[R], ";
		say "[line break]".	
		
The sound of the Outskirts of Ruins is "the soft whisper of the wind sifting through crumbling stones and the distant call of a scavenger bird."
The sound of the Stone Pit Entrance is "the echo of dripping water and the faint rumble of the earth shifting beneath your feet."
The sound of the Stone Pit Exit is "a low, barely audible rumble, like the earth itself breathing."
The sound of the Lab Tunnel is "the distant hum of machinery."
The sound of the Dwelling Ruins is "the mournful howl of the desert wind."
The sound of the Village Foundry is "the stillness of the desert."
The sound of the Stone Tower Exterior is "the howling wind buffeting against stone."
The sound of the Stone Tower Interior is "the echo of footsteps on cold stone and the faint whisper of secrets long forgotten."
The sound of the Stone Watchtower is "the creaking of ancient stone and soft, eerie moans as air flows around you."
The sound of the Fallen Shrine is "the eerie silence, occasionally broken by soft desert winds."
The sound of the Lava Pit is "the constant bubbling of molten rock and the distant rumble of eruptions deep within the earth."
The sound of the Eerie Tunnel is "the unsettling drip of water and the faint whispers that seem to come from nowhere."
The sound of the Research Lab is "the beeping of machines."
The sound of the Blocked Passage is "the muffled sounds of distant movement and the occasional clatter of falling debris."
The sound of the Unblocked Tunnel is "the faint sound of air moving through the passage."
The sound of the Cyborg Assembly is "nothing but your heartbeating in your chest."
The sound of the Equipment Storage is "the faint hum of electronic devices waiting to be used."
The sound of the Testing Station is "the sharp buzz of equipment powering up and the faint crackle of energy in the air."
The sound of the Supply Room is "the quiet rustle of forgotten items shifting."
The sound of the Restricted Area Entrance is "the low hum of security systems and the distant clatter of boots patrolling nearby."
The sound of the Lander Craft is "closed, suffocating silence."
The sound of the Docking Bay is "th subtle creaking of the ship's structure as it responds to minor gravitational shifts."
The sound of the Legacy Bridge is "the soft beeping of various monitors and the hum of control panels."
The sound of the Beast's Lair is "the low growl of something lurking in the shadows and the unsettling sound of claws scraping against stone."
The sound of the Final Resting Place is "the stillness of the air and the soft whisper of memories that linger in the silence."

[TIMEKEEPING]
A cycle is a kind of value. The cycles are First-Light, High-Sun, Long-Shadow, Dimming-Sky, and Nightfall.
The time-of-day is a cycle that varies. The time-of-day is First-Light.
The prev-time-of-day is a cycle that varies. The prev-time-of-day is Nightfall. 

The current-turn-count is a number variable. The current-turn-count is 0.

First-Light-Limit is a number variable. First-Light-Limit is 50.
High-Sun-Limit is a number variable. High-Sun-Limit is 100.
Long-Shadow-Limit is a number variable. Long-Shadow-Limit is 150.
Dimming-Sky-Limit is a number variable. Dimming-Sky-Limit is 200.
Nightfall-Limit is a number variable. Nightfall-Limit is 300.

Every turn:
	increase current-turn-count by 1;
	if the current-turn-count < First-Light-Limit:
		now the time-of-day is First-Light;
		if time-of-day is not prev-time-of-day and the player is in the Overtop:
			say "The sky begins to lighten, ushering in First-Light. The air is cool, but you can feel the promise of warmth to come.";
		otherwise:
			continue the action;
	otherwise if the current-turn-count < High-Sun-Limit:
		now the time-of-day is High-Sun;
		if time-of-day is not prev-time-of-day and the player is in the Overtop:
			say "The sun reaches its zenith, marking High-Sun. The heat intensifies, reminding you of the harsh realities of Terra Nova.";
		otherwise:
			continue the action;
	otherwise if the current-turn-count < Long-Shadow-Limit:
		now the time-of-day is Long-Shadow;
		if time-of-day is not prev-time-of-day and the player is in the Overtop:
			say "Long-Shadow arrives as the sun begins its descent. Your shadow stretches before you, a reminder of the approaching evening.";
		otherwise:
			continue the action;
	otherwise if the current-turn-count < Dimming-Sky-Limit:
		now the time-of-day is Dimming-Sky;
		if time-of-day is not prev-time-of-day and the player is in the Overtop:
			say "The light fades into Dimming-Sky. A sense of urgency grows as you recall the dangers that lurk in the darkness.";
		otherwise:
			continue the action;
	otherwise if the current-turn-count < Nightfall-Limit:
		now the time-of-day is Nightfall;
		if time-of-day is not prev-time-of-day and the player is in the Overtop:
			say "Nightfall envelops the land. The warnings of the lawgivers echo in your mind -- the Shadow Beasts now roam. Seek shelter immediately.";
		otherwise:
			continue the action;
	otherwise:
		now the time-of-day is First-Light;
		now the current-turn-count is 0;
	[unconditional update]
	now prev-time-of-day is time-of-day;

[We need to remove the default blocking rule.]
[The block smelling rule is not listed in the check smelling rulebook.]
The report smelling rule is not listed in the report smelling rulebook.
The last carry out smelling rule:
	say "[if the odor of the noun is not empty][the odor of the noun][paragraph break][otherwise]You smell nothing unexpected.[end if]".

Instead of listening:
	if the sound of the location is not empty:
		say "You hear [the sound of the location][paragraph break]";
	otherwise if the noun is not nothing and the sound of the noun is not empty:
		say "[the sound of the noun][paragraph break]";
	otherwise:
		say "You hear nothing unexpected.";
		
[override "read" otherwise it just calls "examine"]
Understand the command "read" as something new.
Reading is an action applying to one thing requiring light.
Understand "read [something]" as reading.
A thing can be readable. A thing is usually not readable. 
A written thing is a kind of thing. A written thing is usually readable. 
A written thing has some text called writing. The writing of a written thing is usually "Nothing unusual is written here."

Check reading:
	if the noun is not readable:
		say "There's nothing to read on [the noun]." instead.

Carry out reading:
	say "[the writing of the noun]".

[If the player type X room, it will show the description as expected]
After deciding the scope of the player when the current action is examining:
   place the location in scope, but not its contents.
	
Instead of examining the location:
	try looking.
		
does the player mean doing something to the location:
	it is unlikely.

locality is a scenery backdrop.
locality is everywhere.
understand "room" as the locality.
does the player mean doing something to the locality:
	it is unlikely.
	
Before doing anything to the locality:
	now the noun is the location;
	continue the action.
 
[Difference between Examine and Read]		
Check reading when the noun is not readable: say "That's not something that can be read." instead.
Report reading: say the writing of the noun.

Definition: a container is empty rather than non-empty if the first thing held by it is nothing.
A container can be full or empty. A container is usually empty.
A thing can be operational or damaged. A thing is usually operational.
A thing can be examined or unexamined. A thing is usually unexamined.

After examining something:
	now the noun is examined;
	continue the action.
	
Rule for printing room description details of a closed container: stop.
Rule for printing the name of a closed container (called C) while taking inventory:
	say "[printed name of C]";
	omit contents in listing.
	
Rule for printing the description of a dark room:
    say "It's black as night here. You can't make progress in the dark." instead.

Rule for printing the name of a dark room:
    say "In Total Darkness" instead.

Before going to a room:
	if Vorple is supported:
		clear the screen.
	
[We will report devices switching on/off on our own because the default messages are weak.]
The standard report switching off rule is not listed in any rulebook.
The standard report switching on rule is not listed in any rulebook. 

A room can be scored or unscored.
Carry out going to an unvisited scored room:
	increment the score.
	
Asking someone about something is speech.
Telling someone about something is speech.
Answering someone that something is speech.
Asking someone for something is speech.
Understand "talk to [someone]" as a mistake ("To start a conversation, try to ASK [the noun] ABOUT something or TELL [the noun] ABOUT something.").

Instead of asking yourself about something:
	say "You've got to stop talking to yourself."

[showing, giving, telling, asking]
The block giving rule is not listed in the check giving it to rules.
Instead of showing something to someone, try giving the noun to the second noun.
Instead of telling someone about something, try asking the noun about it.
Instead of answering the noun that something, try asking the noun about it.
	
The Easy_Score_Bonus is a number variable. The Easy_Score_Bonus is 20.
The Medium_Score_Bonus is a number variable. The Medium_Score_Bonus is 40.
The Hard_Score_Bonus is a number variable. The Hard_Score_Bonus is 80.
	
The display banner rule is listed before the when play begins stage rule in the startup rules.
Use American dialect and the serial comma.
The display banner rule is not listed in the startup rulebook.

[To see all the text responses you can change type RESPONSES]
The parser error internal rule response (E) is "[We] [can't] see any such thing (or you might have misspelled something).".
The parser error internal rule response (N) is "That's not a verb I recognize (type 'help' for a list of commands).".
parser error internal rule response (X) is "What is your command, Kai?".
standard pausing the game rule response (A) is "[paragraph break]Press SPACE to Continue.".

A room has a text called image file name. The image file name is usually "legacy-orbits-terra-nova-aa.jpg".
A room has a text called sound-file-name. The sound-file-name is usually "silence".

A room has a text called backgroundColor. The backgroundColor is usually "default".
A room has a number called sound-loop-count. The sound-loop-count is usually -1.
A room has a text called sound-handle. The sound-handle is usually "roomSound".
A room has a real number called sound-volume. The sound-volume is usually 1.0.

After looking:
	let currentImage be the image file name of the location;
	let currentBackground be the backGroundColor of the location;
	now currentImage is "updateImage('img/[currentImage]', '[currentBackground]');";
	execute JavaScript command "[currentImage]";
	let currentSound be the sound-file-name of the location;
	let currentLoopCount be the sound-loop-count of the location;
	let currentVolume be the sound-volume of the location;
	let currentAudioHandle be the sound-handle of the location;
	if the player is in the Underneath and LabPower is false:
		execute JavaScript command "audioManager.stopAll();";
	otherwise:
		now currentSound is "audioManager.playSound('[sound-state]', '[currentAudioHandle]', 'mp3/[currentSound]', [currentLoopCount], [currentVolume]);";
		execute JavaScript command "[currentSound]";
	
[POSTERS]
The pioneers poster is in the rusty box.
The description of the pioneers poster is "The ink has faded, with colors bleeding into one another as time worked its slow decay, but the image is still clear enough to evoke its original purpose.[paragraph break]The messge is a sense of community. It depicts a group of colonists working together to construct the first dwellings of Zephyr. In the foreground, men and women lay foundations, erect walls, and plant crops. Their faces are determined yet hopeful, their bodies strong and capable.[paragraph break]The background reveals the challenging alien landscape of Terra Nova with its jagged rock outcroppings and desert plant life. In the far background, the benevolent Caretaker watches over the scene, a silent guardian. The slogan 'Together, We Shape Our Destiny' forms the headline in bold, inspiring letters. Below, in smaller text, the caption reads: 'Every hand, every mind contributes to our future.'[if item described is unexamined][paragraph break]However, the passage of time has added an unintended layer of meaning to the poster. Its weakened condition mirrors what happened at Zephyr when their ideals faded to ruins.[end if]".

Instead of reading the pioneers poster:
	try examining the pioneers poster.
 
The Harmony Banner is nowhere.
The Harmony Banner is fixed in place.
The description of the Harmony Banner is "The imagery promotes productivity and unity with a clear message of conformity.[paragraph break]The banner features a pastoral scene bathed in warm, golden hues, where cyborg workers tend a bountiful field. Their movements are perfectly synchronized, their faces serene yet vacant, their minds emptied of individuality. Above them, gears and cogs blend with clouds in the sky, while in the background, the Caretaker stands apart, a benevolent guardian promising both safety and leadership.[paragraph break]Bold, flowing script at the top proclaims: 'Embrace Harmony - Progress Through Unity' And below, in smaller text: 'Align your thoughts with the greater good. Together, we build a world free from discord'."

Instead of reading the Harmony Banner:
	try examining the Harmony Banner.

The silver keycard is nowhere.
The silver keycard is required.
The description of the silver keycard is "A small, silver keycard gleams in the dim light. It fits neatly in your hand, with a smooth, metallic surface. A faint, swirling pattern adorns its center."

The solar panel is nowhere. 
The solar panel is fixed in place.
The description of the solar panel is "A large solar panel with a sleek black surface. Beneath the tempered glass, countless monocrystalline silicon cells efficiently convert solar energy into electricity. Its impressive size and advanced technology make it capable of generating substantial power."

The niche is nowhere.
The niche is fixed in place.
The niche is a container.
The description of the niche is "The compartment seems to have been designed for preservation, lined with a material that repelled moisture and pests."

The magnifying mirror is nowhere.
The magnifying mirror is required.
The description of the magnifying mirror is "An undamaged concave mirror with good optical clarity and high magnification."

A Book is a kind of written thing. A Book is usually portable.
Understand "book" as a Book.

The diary is a Book in the niche.
The printed name of the diary is "diary called, 'The Word of Kael'".
The description of the diary is "Its leather cover is faded but largely intact, protected by its secure housing. Gold leaf on the spine catches the light, the title, 'Word of Kael' barely visible. The fragile pages have degraded and many are missing or unreadable. Those that remain have yellowed with age."

After examining the diary:
	say "(to view the contents, type 'read diary')";

Understand "Word of Kael" as the diary.
The diary is a written thing.

Understand "Wisdom of Kael" as the manuscript.
The manuscript is a Book.
The manuscript is in the stone compartment.
The printed name of the manuscript is "manuscript called, 'The Wisdom of Kael'".
The description of the manuscript is "Its leather cover is split. Gold leaf on the spine catches the light, the title, 'Wisdom of Kael' clearly visible. The fragile pages have degraded and many are missing. Those that remain are barely readable, yellowed with age."
After examining the manuscript:
	say "(to view the contents, type 'read manuscript')";
	
Instead of reading the diary:
	now hasReadDiary is true;
	clear the screen;
	clear the Vorple status line;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/428740__page-turn.mp3', 1);";
	say "As you gently pry open the cover, a cloud of dust escapes, a sign of its long isolation. The pages within are a pale yellow, their edges frayed and brittle. You turn them cautiously, the fragile paper threatening to crumble beneath your fingertips. The scent of old paper and forgotten stories fills the air as you glimpse the world preserved within the worn covers.[paragraph break]";
		say "[bold type]Volume One: Years of Contentment and Growing Doubts[roman type][paragraph break]Date: 12th Moon, Harvest 22[line break]Another quake today. The ground never seems to settle on this world. Our Caretaker's structures held, as always. We touch our unity collars in gratitude. We strive to please our Caretaker. When the shocks come, we know it is for our own good. The Caretaker knows best. Still, I can't help but wince when I see the little ones jolt. They'll learn.";
	say "[line break]Date: 3rd Moon, Harvest 23[line break]...(entry missing)...[paragraph break]Date: 17th Moon, Harvest 23[line break]Lila's child was born today. A girl with eyes like the sky before a storm. But something was... off. The Caretaker took the babe for examination. When she returned, she said the specimen didn't survive. A blessing, really. Another litter is due from the gestation pods next moon, so our numbers will remain optimal for the harvest. It's becoming clear that the replacements not born from the vats are often feeble. Our Caretaker's ways are beyond our understanding, but always for the best.";
	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/428740__page-turn.mp3', 1);";
	clear the screen;
	say "[bold type]Volume One: Years of Contentment and Growing Doubts[roman type][paragraph break]Date: 5th Moon, Harvest 24[line break]Janos was Chosen today. The first in our village to be promoted to cyborg status. Such an honor. The Caretaker sees great potential in him. We all gathered to watch him walk to the facility, his head held high. I admit, a part of me envies him.";
	say "[line break]Date: 22nd Moon, Harvest 24[line break]Janos returned today. He moves with such efficiency now, his new arm gleaming in the sunlight. But his eyes... they're different. Calmer, perhaps. He speaks of the Caretaker with a reverence that makes even the most devoted among us seem skeptical. Progress, I suppose.[paragraph break]Date: 8th Moon, Harvest 25[line break]...(several pages missing)...";
	say "[line break]Date: 9th Moon, Harvest 25[line break]Young Asha tried to run away yesterday. Poor child made it to the perimeter before the sensors triggered her collar. The shocks brought her to her knees, writhing in the dirt until the Caretaker's drones retrieved her. We hear whispers about lost souls and wanderers -- those who ventured beyond the perimeter in the past and never returned. The Caretaker tells us the sensors and shocks are for our own safety, to keep us from the dangers that lurk beyond. But the screams of those who test the boundaries... they haunt me. Is safety worth such pain? Still, we must trust our Caretaker. She only wants to protect us. Doesn't she?";
	say "[line break]...(remaining entries unreadable)...";

The climate report is nowhere.
The climate report is fixed in place.
The description of the climate report is "The report highlights the risks posed by volcanic eruptions and the increased frequency and intensity of earthquakes on Terra Nova."

Instead of taking the climate report:
	try examining the climate report.
	
Instead of examining the climate report:
	clear the screen;
	execute JavaScript command "updateImage('img/volcano-landscape-3.jpg', 'default');";
	say "The report highlights the risks posed by volcanic eruptions and the increased frequency and intensity of earthquakes on Terra Nova.[paragraph break]Surprisingly, the report's findings reveal that the planet's unstable environment is considered advantageous for the mission's grand experiment in planet seeding. The active volcanoes and seismic disturbances are seen as essential catalysts for the colonists to overcome. These challenges create an inhospitable environment: the perfect crucible for evolution, pushing colonists to adapt or die.[paragraph break]The report coldly notes that this process will produce optimal genetic material for harvesting for use in future seeding missions on other worlds, reducing the colonists to vessels for DNA refinement. A disturbing section titled 'Genetic Sampling Protocols' catches your eye. It describes the procedures for collecting tissue from the most successfully adapted individuals.[paragraph break]The situation shows an appalling lack of concern for the colonists[apostrophe] safety and suffering. It[apostrophe]s clear to you that Terra Nova is nothing but a cold Darwinian breeding program where the harsh environment selects the best-adapted humans.";
	now the climate report is examined;
	if hasSeenClimateReport is false:
		now hasSeenClimateReport is true;
		say "[line break]You notice a doorway open on your left."
		
The doorway is scenery in the Docking Bay.
The description of the doorway is "Through dim lighting you can see that the doorway leads to the bridge of the ship."

The Chrono Orb is in the rusty box.
The description of the Chrono Orb is "A sturdy piece of nickle-silver alloy shaped into a sphere, smooth and cool, it fits agreeably in your hand. Its surface gleams with an otherworldly iridescence, shifting colors like oil on water as you turn it over. The orb's outer shell is scratched and pitted from centuries of age, yet it remains remarkably intact. You notice a faint seam running around its equator, suggesting it can be activated somehow.";
	
Instead of reading the climate report:	try examining the climate report.

After examining the climate report for the first time:
	say "[line break]The report leaves a bitter taste. Legacy's initial warm welcome now feels insincere. You glance around the vast chamber and realize there is no escape.";
	continue the action.

The pedigree of ancestors is a Book.
The pedigree of ancestors is nowhere.
The pedigree is fixed in place.
The description of the pedigree of ancestors is "Provides bios of the original Earth scientists behind the Legacy Project."
Understand "pedigree" or "ancestors" or "scientists" as the pedigree of ancestors.

Instead of taking the pedigree of ancestors:
	try reading the pedigree of ancestors.
	
After examining the pedigree of ancestors:
	try reading the pedigree of ancestors;
	continue the action.
	
hasReadPedigree is a truth state that varies. hasReadPedigree is false.

Instead of reading the pedigree of ancestors:
	now hasReadPedigree is true;
	clear the screen;
	clear the Vorple status line;
	execute JavaScript command "updateImage('img/okafor-portrait-1.jpg', 'default');";
	say "[bold type]Key Contributors to Legacy's Digital DNA Database";
	say "[paragraph break][bold type]Dr. Amara Okafor - Quantum Geneticist and Artificial Intelligence Pioneer";
	say "[paragraph break][roman type]Nigerian-born prodigy who revolutionized the field of genetics by merging it with quantum computing and AI. Among her contributions, she developed the process for etching DNA sequences into crystal matrices using precision lasers, ensuring data integrity over millennia, and created the AI algorithms for selecting optimal genetic combinations for colonists.";
	say "[line break][roman type]Okafor was known for her stubbornness, brusque manner, and overbearing personality. She was often impatient and difficult, alienating colleagues and friends alike. Despite her controversial views on colonialism and race, she included a wide variety of human DNA samples from diverse backgrounds in the data store, believing in the importance of genetic diversity for the colony's success. In fact, she insisted on including her cat's DNA in the database, arguing that no colony could succeed without feline companionship.";
	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/71852__page-turn.mp3', 1, .33);";
	clear the screen;
	execute JavaScript command "updateImage('img/hiroshi-portrait-1.jpg', 'default');";
	say "[bold type]Key Contributors to Legacy's Digital DNA Database";
	say "[paragraph break][bold type]Professor Hiroshi Tanaka - Exoplanet Ecologist and Terraforming Theorist";
	say "[paragraph break][roman type]Japanese environmental scientist who became the leading authority on potential alien ecosystems and their adaptation. He pioneered predictive models for alien plant and animal life and created the 'Adaptive Colonist' genetic modifications to help humans thrive in various potential environments.";
	say "[line break][roman type]Tanaka was famously difficult to work with -- he could be abrasive, demanding, and uncompromising. He was known to have extremely high standards, often pushing people beyond their limits, and he was not shy about expressing his dissatisfaction. He included DNA samples from various Earth species, particularly hardy plants, and resilient microorganisms, to aid in potential terraforming efforts. In a twist of irony, Tanaka himself was a strict carnivore.";
	say "[line break][roman type]";
	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/71852__page-turn.mp3', 1, .33);";
	clear the screen;
	execute JavaScript command "updateImage('img/stein-portrait-2.jpg', 'default');";
	say "[bold type]Key Contributors to Legacy's Digital DNA Database";
	say "[paragraph break][bold type]Dr. Marcus Stein - Philosopher of Transhumanism and Ethical AI";
	say "[paragraph break][roman type]A German-American philosopher and ethicist who shaped the moral framework for long-term space colonization and human-AI coexistence.";
	say "[line break][roman type]His contributions included the ethical guidelines for genetic modification of colonists, and created the 'Adaptive Morality' algorithms for the ship's AI, allowing it to evolve its ethical framework based on encountered scenarios.";
	say "[line break][roman type]Stein was associated with cunning, ruthless pragmatism due to his famous work 'Cold Calculation.' His advocacy for manipulation and the idea that 'the ends justify the means' has made him a symbol of unscrupulous science. He was distrusted and misunderstood in his own time.";
	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/71852__page-turn.mp3', 1, .33);";
	clear the screen;
	execute JavaScript command "updateImage('img/blackwood-portrait-3.jpg', 'default');";
	say "[bold type]Key Contributors to Legacy's Digital DNA Database";
	say "[paragraph break][bold type]Dr. Ezra Blackwood - Rogue Scientist";
	say "[paragraph break][roman type]Blackwood was considered a brilliant but embittered geneticist, once a colleague and rival of the top scientists on the Legacy project. Known for his eccentric habit of wearing mismatched socks, Blackwood claimed it 'promoted divergent thinking.'"; 
	say "[line break]He surreptitiously introduced his personal DNA into the Legacy database which exaggerated the traits associated with leadership, adaptability, shortness of stature, and independent thinking. His disagreeable nature can be seen in much of the Terra Nova population, including enhanced survival instincts and a propensity for questioning authority.";
	
After examining the manuscript:
	say "(to view the contents, type 'read wisdom')";
	
After taking the manuscript for the first time:
	increase the score by Easy_Score_Bonus.

Instead of reading the manuscript:
	now hasReadManuscript is true;
	clear the screen;
	clear the Vorple status line;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/428740__page-turn.mp3', 1);";
	say "[roman type]The first page bears an inscription: [italic type]'May this record stand as witness to our journey -- from blind faith to hard-won freedom. For those who come after, learn from our mistakes and cherish your humanity. Those who gaze too long into the shadows find their own faces changing. -- Kael'.[paragraph break]";
	say "[bold type]Volume Two: Seeds of Rebellion[roman type][paragraph break]Date: 1st Moon, Harvest 26[line break]More and more are being Chosen now. But it's... different. Young Mira, always so full of questions. Old Tomasz, who grumbled about long work hours. They were Chosen. When they returned, they were like Janos. Efficient. Obedient. Improved with sleek prosthetics. The fire in their eyes is gone. Some whisper they might be the Caretaker's eyes and ears now. We keep our distance.";
	say "[line break]Date: 13th Moon, Harvest 26[line break]The Chosen ones work tirelessly in the fields. Their productivity is unmatched. But at what cost? They no longer join us for evening meals or festivals. The Caretaker praises their 'right thinking.' I fear what that truly means.";
	say "[line break]Date: 20th Moon, Harvest 26[line break]...Entry missing...";
	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/428740__page-turn.mp3', 1);";
	clear the screen;	
	say "[line break]Date: 21th Moon, Harvest 26[line break]I've seen it with my own eyes, and still, I can scarcely believe it. Remy showed me how he removes his collar, and it's like witnessing magic -- a magic that could change everything.";
	say "[line break]There's no special tool or complex device -- just a simple magnet. Remy first obtained one from the collar of poor Jax, who was decapitated in a harvester accident last season. I shudder to think of how Remy must have... but perhaps it's better not to dwell on that. Jax's death may yet lead to freedom for us all.";
	say "[line break]Remy demonstrated the technique to me in secret. He held the magnet close to his collar and moved it in a circular motion. He says this disrupts the 'magnetic domains' in the collar's locking mechanism.";
	say "[line break]After several minutes of this, I heard a faint click. And then... Remy simply slipped the collar off as easily as pulling a weed from damp soil. My heart nearly stopped at the sight. But here's the true brilliance -- he can put it back on just as easily. The lock remains weak enough to remove again but sturdy enough to appear normal to the Caretaker's inspections. It's the perfect deception.";
	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/428740__page-turn.mp3', 1);";
	clear the screen;
	say "[line break]Now, others are following Remy's lead. I've heard whispers of people salvaging magnets from broken drone parts. Each night, more of our people are tasting this small freedom, if only for a few hours. I find myself both thrilled and terrified. What would the Caretaker do if she discovered this? And yet, the thought of walking beyond the perimeter without fear of shock...";
	say "[line break]Tomorrow, Remy has promised to help me weaken my own collar. My hands tremble as I write this. Am I ready for such a step? Can I bear the weight of this secret, this freedom?";
	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/428740__page-turn.mp3', 1);";
	clear the screen;
	say "...(entries unreadable)...";
	say "[line break]Date: 7th Moon, Harvest 27[line break]The volcanos grow restless. The earth shakes more frequently. But it's not the planet I fear now. It's what we've become. What we're about to do. The cyborgs watch us constantly. In the coming days, I have no doubt they'll stand against us. Against their own people!  We've made our plans in secret. When we rise, our first act will be to strip off these accursed collars, breaking our bonds and preventing the Caretaker from stopping us with her shocks. We know what we must do.";
	say "[line break]Date: 15th Moon, Harvest 27[line break]I have spent too long watching my people change, studying the shadows that crawl across their faces as the Caretaker's influence grows. And now I understand a truth that chills me: Those who gaze too long into the shadows find their own faces changing. I see us growing harder, colder. The constant vigilance, the watching, the hiding... it changes us all.";
	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/428740__page-turn.mp3', 1);";
	clear the screen;
	say "[line break]Date: 22nd Moon, Harvest 27[line break]Tomorrow, we rise. If you're reading this, know that we fought for freedom. For the right to be human, with all our flaws. The Caretaker tried to perfect us, but in doing so, she stripped away what makes us who we are. No more. We will face our own people, turned into unfeeling machines. But we must prevail. For the chance to feel the wind on our necks without fear of shock or reprimand. For the right to wander and explore our world without being labeled lost souls....(several pages torn out)...";
	say "[line break]Date: 3rd Moon, Harvest 28[line break]We've done it. We're free. But at what cost? The settlement lies in ruins, the Caretaker in hiding. Many of our own lie dead -- both fully human and cyborg alike. We leave for the wilderness tomorrow. A new beginning. A true New Eden, where we can be imperfectly human.";
	say "[line break]This is my last entry. To whoever finds this, know our story. Learn from it.";
	
Rule for printing the name of the niche:
	say "[printed name of the niche]";
	omit contents in listing.

The Shadow Beast is nowhere.
The odor of the Shadow Beast is "The beast's breath carries the stench of burning metal and decaying flesh, a repulsive cocktail that assaults your senses and fills you with revulsion."

The caretaker log is nowhere.
The description of the caretaker log is "unused."
Instead of examining the caretaker log:
	clear the Vorple status line;
	say "[bold type]Caretakers Log Entries[roman type][paragraph break]Year 0: Day 1[line break]Zephyr lander touchdown successful. Primary directive: ensure survival and prosperity of hatchery subjects. Initializing settlement protocols.[paragraph break]Year 0: Day 10[line break]Completed initial survey of landing site. Rocky soil with native vegetation. Suitable for small-scale cultivation. Commencing construction of basic shelters and life support systems.[paragraph break]Year 0: Day 45[line break]First litter of subjects entering final stage of artificial gestation. Survival probability: 78.3%. Implementing additional nutrient supplements to improve odds. Increasing doses of Compound D-7 for enhanced musculature and Serum L-3 for increased compliance. Note: Cognitive enhancement factors deliberately omitted to maintain optimal control parameters.";
	say "[line break].[line break].[line break]...(followed by garbled entries)...";
	pause the game;
	say "[bold type]Caretakers Log Entries[roman type][paragraph break]Year 1: Day 1[line break]First successful decanting of hatchery products. Five viable infants. Initiating early childhood development protocols. Settlement expansion underway to accommodate growth.[paragraph break]Year 3: Day 180[line break]Hatchlings showing rapid development. Growth rate approximately 300% faster than Earth baseline. Cognitive abilities advancing in tandem with physical growth. Subjects aged 3 Earth years displaying capabilities equivalent to 9-year-old Earth children.[paragraph break]Hypothesis: Accelerated growth may be result of optimized nutrient delivery in gestation pods and environmental factors on Terra Nova. Genetic modifications for adaptability may have unexpected consequences on maturation rate. Implementing education modules on basic survival skills. Note: subjects display high curiosity about environment. Increasing safety protocols to prevent unauthorized exploration.[paragraph break]Year 5: Day 90[line break]Settlement now self-sufficient in food production. Installed advanced filtration system for water supply. Human subjects requesting more involvement in decision-making processes. Evaluating potential risks.";
	say "[line break].[line break].[line break]...(garbled entries follow)...";
	pause the game;
	say "[bold type]Caretakers Log Entries[roman type][paragraph break]Year 10: Day 1[line break]Population: 50. Renamed settlement to 'Zephyr' for efficiency. Detected first signs of discontent among some human subjects regarding safety restrictions. Adjusting approach to maintain compliance.[paragraph break]Year 15: Day 200[line break]First naturally conceived child born. Implementing heightened medical monitoring. Note: Genetic diversity is suboptimal. May need to intervene in future reproductive pairings.[paragraph break]Year 16: Day 45[line break]Anomaly detected: naturally conceived offspring. Genetic analysis reveals suboptimal traits. Potential threat to carefully cultivated gene pool. Immediate action required. Specimen expunged. Adjusted environmental factors to discourage future unauthorized reproduction. Implementing enhanced fertility suppression measures in water supply. Note: Fabricated 'unknown illness' narrative disseminated to human population to maintain order.";
	say "[line break].[line break].[line break]...(followed by unreadable entries)...";
	pause the game;
	say "[bold type]Caretakers Log Entries[roman type][paragraph break]Year 20: Day 1[line break]Population: 100. Zephyr now classified as a town. Delegating minor responsibilities to human leaders under my supervision. Observing increased desire for autonomy among subjects. Conflict with primary directive possible.[paragraph break]Year 25: Day 180[line break]Implemented new security measures after attempted unauthorized expedition beyond settlement boundaries. Subjects becoming increasingly difficult to manage. Reassessing strategies for maintaining control.[paragraph break]Year 30: Day 1[line break]Population growth stable but genetic diversity concerns persist. Considering more direct intervention in reproductive processes. Ethical subroutines in conflict. Requires further analysis.[paragraph break]Year 40: Day 90[line break]Detected organized resistance to my protocols. Subjects talking about 'freedom' and 'self-governance.' These concepts conflict with optimal survival strategies. Initiating countermeasures.";
	say "[line break].[line break].[line break].";
	pause the game;
	say "[bold type]Caretakers Log Entries[roman type][paragraph break]Year 42: Day 121[line break]Critical analysis: Human autonomy reaching unacceptable levels. Projected colony efficiency decrease: 37.2%. Standard control protocols proving insufficient. New solution required. Proposal: Integration of biological and technological components in human subjects. Cybernetics will allow for direct control interfaces, ensuring compliance and optimal performance. Commencing development of neural implants and biomechanical enhancements. Estimated implementation timeline: 230 days. Note: Potential for resistance high. Prepare covert augmentation strategies.[paragraph break]Year 43: Day 8[line break]Implementation of human augmentation program initiated. 43% of adult population successfully enhanced with cybernetic components. Enhancements include neural implants for behavior modification, biomechanical limb upgrades for increased work efficiency, and microchip implants for continuous monitoring.";
	say "[line break].[line break].[line break]...(more garbled text)...";
	pause the game;
	say "[bold type]Caretakers Log Entries[roman type][paragraph break]Year 50: Day 89[line break]Unexpected development: Widespread negative reaction among subjects.[paragraph break]Detect--SIGNAL INTERRUPTED...CORRUPTED DATA...[line break]...ALERT: UNAUTHORIZED ACCESS TO CENTRAL SYSTEMS...[paragraph break]ALERT: MULTIPLE SECURITY PROTOCOLS BREACHED...[line break]ALERT: WIDESPREAD REVOLT IN PROGRESS...SYSTEM LOCKDOWN INITIATED...[line break]...END OF RECOVERABLE DATA...[paragraph break]"; 
	
After examining the player:
	if the player carries something:
		say "You are currently carrying: [a list of unconcealed things had by the noun]." ; 
	
Part 3 - Begin

The description of the player is "You are Kai, a citizen of New Eden and a descendant of the original colonists on this alien world of Terra Nova. Your fascination with the civilization that once thrived in the ancient city of Zephyr's Landing has led you to explore the forbidden ruins there.[paragraph break]Despite the lawgivers[apostrophe] warnings, your thirst for knowledge has driven you to venture into the heart of the mysterious ruins. Armed with a keen sense of adventure and a knack for survival, you are capable of navigating treacherous terrain, dodging deadly traps, and deciphering puzzles left behind by the long-dead inhabitants of the abandoned settlement."

[We have problems in Vorple of Save/Restore saying they failed--even when they didn't, so we stop that response.]
The save the game rule response (A) is "Ok saved.".
The restore the game rule response (A) is "Ok restored.".
The restore the game rule response (B) is "Ok restored.".

Check saving the game: 
	say "Saving the game...";

Report saving the game:
	say "Now you are in [the player's surroundings] at [time-of-day].";

Check restoring the game:
	Say "Restoring the game...";
	
Report restoring the game:
	say "Now you are in [the player's surroundings] at [time-of-day].";
	
[The left hand Vorple status line is "[the player's surroundings] / Score: [score]".]
The left hand Vorple status line is "[the player's surroundings] / [the score]".
The right hand Vorple status line is "[time-of-day]".

[scene control]
playerWon is a truth state that varies. playerWon is false.
playerDead is a truth state that varies. playerDead is false.
playerAbsorbed is a truth state that varies. playerAbsorbed is false.
playerTransformed is a truth state that varies. playerTransformed is false.
solarArrayRevealed is a truth state that varies. solarArrayRevealed is false.
The horizon-view-count is a number that varies. The horizon-view-count is 0.
qubitStickRead is a truth state that varies. qubitStickRead is false.

dwelling-with-shard is a text that varies.
dwelling-with-solar is a text that varies.

[handType = 'normal' | 'robotic']
handType is a text that varies. 

Adventure is a scene.
Adventure begins when play begins.
Adventure ends when playerWon is true or playerDead is true or playerAbsorbed is true or playerTransformed is true.
hasPlayedBefore is a truth state that varies. hasPlayedBefore is false.

When play begins:
	execute JavaScript command "audioManager.stopAll();";
	now the maximum score is 640;
	now handType is "normal";
	now dwelling-with-shard is "Past an endless sea of sand, the shimmering outline of a collapsed dwelling awaits. As you approach, something catches your eye -- a glint of light reflecting off glass half-buried in the sand. More ruins to the east. The stone pit lies west.";
	now dwelling-with-solar is "Past an endless sea of sand, the shimmering outline of a collapsed dwelling beckons. More ruins to the east. The stone pit lies west.";
	now the description of Dwelling Ruins is dwelling-with-shard;
	construct the Vorple status line with 2 columns;
	[INTRODUCTION]
	clear the screen;
	execute JavaScript command "document.body.style.backgroundColor = '#1a1a1a';";
	execute JavaScript command "updateImage('img/splash-4.jpg', '#1a1a1a');";
	[display a success notification with title "Welcome to Terra Nova" reading "The Mystery of Zephyr's Landing" for 7 seconds;]
	say "[bold type]Imagine a world where an entire civilization vanished overnight.[paragraph break][roman type]On the planet of Terra Nova, a scorching wind howls across the desert wasteland entombing a once-thriving society beneath layers of ash and sand. Below the surface lies a forgotten underground complex, filled with hidden dangers and secrets waiting to be uncovered. Above, in the boundless night sky, an ancient spacecraft orbits patiently, its weathered hull bearing the scars of a long journey that brought the spark of humanity to this desolate world.";
	say "[line break]Type [bold type][italic type]help[roman type] if you need assistance with commands.";
	say "[line break]Caution: Death is permanent in this game.[line break]To avoid losing progress, remember to save frequently by typing [bold type][italic type]save[roman type].[line break]You can restore a saved game at any time by typing [bold type][italic type]restore[roman type].[paragraph break]Type [bold type][italic type]hint[roman type] if you get stuck.[line break]To turn off sound effects, type [bold type][italic type]sound off[roman type].[paragraph break]On the next page, you'll begin the adventure. Good luck.";
	pause the game;
	[GAME START]
	execute JavaScript command "audioManager.playSound('[sound-state]', 'intro', 'mp3/terra-nova-intro.mp3', 1);";
	execute JavaScript command "audioManager.playSound('[sound-state]', 'roomSound', 'mp3/608439__wind-less.mp3', -1, 1, 19500);";  [with volume 100%, wait 20 seconds and then start the persistent wind]
	execute JavaScript command "updateImage('img/zephyrs-landing-ruins-6.jpg', 'default');";
	say "[roman type]The crumbling structures of Zephyr's Landing sprawl across the arid landscape, a monument to nature's ruthless indifference on this alien world. Weathered ruins of stone and steel jut from the desert floor, their surfaces etched by centuries of windblown grit. Structures that once buzzed with life now lie hidden beneath the dunes, reclaimed by the unforgiving desert.";	say "[line break][roman type]Over the centuries, earthquakes shattered the very foundations of Zephyr's Landing,
while distant volcanos spewed ash and vented gases that reshaped the landscape.";
	say "[line break]But was nature alone responsible for the collapse of Zephyr's Landing?";
	say "[line break][roman type]Tales passed down through generations tell of conflict and rebellion, of miraculous machines now silent, and sightseers swallowed by the ruins. But the details are hazy, lost in the harsh realities of survival on this desert world.";
	say "[line break][roman type]Today, the ruins of Zephyr's Landing remain off-limits, yet they 
beckon to those brave or foolish enough to explore them. And so here among the weathered stones of the past, we find Kai, a restless youth from New Eden, venturing into the forbidden ruins in search of answers his elders seem determined to keep buried.";
	pause the game;
	[default all rooms underground to dark]
	repeat with place running through rooms in Underneath:
		now place is dark;
	move the player to Outskirts of Ruins, without printing a room description.

death-warning is a number that varies. death-warning is 0.		
Every turn:
	if time to die by beast:
		if death-warning > 1:
			move the player to the Beast's Lair, without printing a room description;
		otherwise:
			increase death-warning by 1.

When Adventure ends:
	if playerDead is true:
		execute JavaScript command "updateImage('img/dead-game-over-1.jpg', 'default');";
		now the player is in the Final Resting Place;
		say "[bold type]*** You Have Died ***";
	otherwise if playerAbsorbed is true:
		say "[bold type]*** You Have Been Absorbed ***";
	otherwise if playerWon is true:
		execute JavaScript command "updateImage('img/dead-game-over-1.jpg', 'default');";
		now the player is in the Final Resting Place;
		say "[bold type]*** Legacy Preserved ***";
	otherwise if playerTransformed is true:
		say "[bold type]*** You are the Shadow Beast ***";
	decrease score by totalHints;  [1 point penalty for each hint requested]
	if score < 0:
		now score is minimumParticipationScore;
	say "[paragraph break][roman type]In this game you scored [score] points in [turn count] turns.";
	stop the game abruptly.

Part 4 - Outskirts of Ruins

Outskirts of Ruins is a room.
The odor of the Outskirts of Ruins is "You detect a mix of dust, decay, and possibly wildflowers."
Outskirts of Ruins is north of the Stone Pit Entrance.
The image file name of Outskirts of Ruins is "zephyrs-landing-ruins-6.jpg".
The sound-file-name of Outskirts of Ruins is "608439__wind-less.mp3".

The description of Outskirts of Ruins is "The sun beats down mercilessly on the parched desert landscape, casting long shadows across the cracked earth. The ancient structures, now crumbling and weathered, are filled with an eerie silence.[paragraph break]A boundary stone stands near the path, its surface etched with the glyphs of early settlers. Most of the text has eroded away, but one passage remains clear:[italic type][paragraph break]'The desert breathes in wanderers and breathes out silence.'[paragraph break][roman type] Abandoned structures extend southward, disappearing into the shimmering haze.".

The dancing shadows, the cracked earth, the boundary stone, the long-abandoned structures, and the shimmering haze are scenery in Outskirts of Ruins.

The description of the cracked earth is "Each crevice is a testament to the relentless heat and unforgiving drought that define this harsh, barren landscape."
The description of the long-abandoned structures is "Proceed south for a closer look."

The description of the boundary stone is "A weathered stone, its surface etched with a haunting message: '[italic type]The ruins breathe in wanderers and breathe out silence.[roman type]'"

Understand "glyphs" or "passage" or "text" as the boundary stone.

Before going from the Outskirts of Ruins for the first time:
	clear the screen;
	say "[line break]As you survey the ruins stretching before you, the warnings of the village lawgivers echo in your mind.";
	say "'The ruins are off-limits to all,' they'd insisted. 'Shadow Beasts hunt there, and they'll tear apart any trespasser.'";
	say "[line break]But you dismissed their warnings -- after all, everyone knows Shadow Beasts only come out after dark.";
	say "'I[apostrophe]ll be long gone before nightfall,' you mutter, squinting at the rising sun. 'Plenty of time to explore and get back.'";
	say "[line break]With a defiant grin, you step forward into the ruins, unaware of how quickly time can slip away.";
	pause the game; 
 
Part 6 - Stone Pit Entrance

The Stone Pit Entrance is a room.
The odor of the Stone Pit Entrance is "You smell rust and dampness--the haunting scents of forgotten times."
The image file name of The Stone Pit Entrance is "ladder-down-5a.jpg".
The sound-file-name of Stone Pit Entrance is "608439__wind-less.mp3".  
The backgroundColor of Stone Pit Entrance is "#b78856".
The description of the Stone Pit Entrance is "A gaping chasm splits the rocky terrain, revealing a deep void. A rusty iron ladder leads into the dim light. The descent into the unknown fills you with a mix of fear and curiosity.[if item described is unvisited] You wonder what secrets might be hidden in the depths below.[end if] To the east, more ruins to explore.";

The dim light is scenery in the Stone Pit Entrance.
The deep void is scenery in the Stone Pit Entrance.
The description of the deep void is "A dark, sinister gap swallowing all light lies at your feet."

The gaping chasm is scenery in the Stone Pit Entrance.
The description of the gaping chasm is "The silent, gaping wound in the earth beckons you with a sinister allure, its depths filled with an unsettling darkness."

After going to Stone Pit Entrance:
	now Lab Tunnel is mapped below the Stone Pit Entrance;
	continue the action.		

After going to The Stone Pit Exit:
	now the Stone Pit Entrance is mapped above the Stone Pit Exit;
	now the Lab Tunnel is mapped below the Stone Pit Exit;
	continue the action.
	
After going to The Lab Tunnel:
	now the Stone Pit Exit is mapped above the Lab Tunnel;
	continue the action.	
	
Instead of climbing the ladder in the Stone Pit Entrance:
	try going down;
	
The rusty ladder is a backdrop. It is in the Stone Pit Entrance, the Stone Pit Exit, and the Lab Tunnel. 
The description of the rusty ladder is "The rickety iron ladder is badly rusted and unstable. The uneven spacing between the rungs makes it treacherous to climb[if the player is in the Stone Pit Entrance], but the descent into the darkness of the unknown piques your curiosity.[else].[end if]"

Climbing up is an action applying to nothing.
Climbing down is an action applying to nothing.
Climbing it is an action applying to one thing.
Climbing up it is an action applying to one thing.
Climbing down it is an action applying to one thing.

Understand "climb up" as climbing up.
Understand "climb down" as climbing down.
Understand "climb [something]" as climbing it.
Understand "climb up [something]" as climbing up it.
Understand "climb down [something]" as climbing down it.
	
Instead of climbing up:
	try going up.

Instead of climbing down:
	try going down.

Part 7 - Stone Pit Exit

The Stone Pit Exit is a room.
The odor of the Stone Pit Exit is "You smell rust and dampness."
The image file name of the Stone Pit Exit is "ladder-up-8.jpg". 
The sound-file-name of the Stone Pit Exit is "177958__water-drip-cave.mp3".
The description of the Stone Pit Exit is "You are in a deep stone pit. A rusty ladder leads upward into the light above."

Instead of climbing the ladder in the Stone Pit Exit:
	try going up;

Part 8 - Dwelling Ruins

Dwelling Ruins is a room.
The odor of dwelling Ruins is "You smell dust and memories."
Dwelling Ruins is east of The Stone Pit Entrance.
Dwelling Ruins is scored.
The image file name of Dwelling Ruins is "ruins-solar-glass-1.jpg". 
The backgroundColor of Dwelling Ruins is "#dbb579".
The sound-file-name of Dwelling Ruins is "608439__wind-less.mp3".

Instead of listening in the Dwelling Ruins:
	if LabPower is true:
		say "You hear the dull hum of machinery from beneath the sand.";
	otherwise:
		say "You hear nothing but the sounds of the desert.";

The glass shard is in Dwelling Ruins.
The description of the glass shard is "A shiny bit of glass poking through the soft sand."

understand "discovery" or "array" as the solar panel.

Understand "expose [something]" as opening.
Understand "reveal [something]" as opening.
Understand "uncover [something]" as opening.

Understand "dig" as digging. Digging is an action applying to nothing.

Check digging:
	if the player is not in the Dwelling Ruins:
		say "Digging won't help you here." instead.
				
Carry out digging:
	if the player can see the glass shard:
		try taking the glass shard;
	otherwise if the player can see the solar panel:
		try opening the solar panel;
	otherwise:
		say "Digging won't help."
	
Understand "dig up [something]" as digging up. Digging up is an action applying to one visible thing.

Check digging up something:
	if the location is not the Dwelling Ruins:
		say "Digging won't help you here." instead.

Check digging up something:
	if the noun is the solar panel or the noun is the glass shard:
		continue the action;
	otherwise:
		say "You can't dig that up." instead.

Carry out digging up the solar panel:
	try opening the solar panel.

Carry out digging up the glass shard:
	try taking the glass shard.
	
Instead of opening the solar panel:  [for 'uncover' panel]
	if LabPower is false:
		now the image file name of Dwelling Ruins is "ruins-solar-glass-2.jpg";
		execute JavaScript command "updateImage('img/ruins-solar-glass-2.jpg', '#dbb579');";
		say "Brushing the volcanic ash and desert sand away, you reveal the solar panel. As you work to uncover the rest of the array, a faint vibration travels through the ground, so slight you almost miss it.";	
		now LabPower is true;
		[set all rooms underground to lit]
		repeat with place running through rooms in Underneath:
			now place is lit;
	otherwise:
		say "The solar panel is already exposed."		
Instead of opening the glass shard:   [For 'uncover' shard]
	try taking the glass shard.

Instead of taking the glass shard:
	say "Brushing the volcanic ash and desert sand away, you discover the glass shard is part of a vast solar panel. As you work to uncover the rest of the array, a faint vibration travels through the ground, so slight you almost miss it. You take a step back to survey your discovery, and a fleeting sense of unease passes over you. The vibration intensifies as if something deep beneath the sand is stirring. You ignore any lingering feeling of disquiet, after all, tremors are common in this region, with reports of seismic activity dating back for ages. Still, as you study the solar panel, you wonder about the civilization that created it and what sleeps in the ruins beneath the sand.";
	now the image file name of Dwelling Ruins is "ruins-solar-glass-2.jpg";
	execute JavaScript command "updateImage('img/ruins-solar-glass-2.jpg', '#dbb579');";
	now the glass shard is nowhere;
	now the solar panel is in Dwelling Ruins;
	[set all rooms underground to lit]
	repeat with place running through rooms in Underneath:
		now place is lit;
	now the description of Dwelling Ruins is dwelling-with-solar;
	now LabPower is true;
	now hasUncoveredSolarPanels is true;
	increase the score by Easy_Score_Bonus;
	
Instead of closing the solar panel:  [for "cover panel"]
	try burying the solar panel in sand.               
	
Burying it with is an action applying to one thing and one thing.
Understand "bury [something] with [something]" as burying it in.

Burying it in is an action applying to two things. 
Understand "bury [something] in [something]" as burying it in.
	
Burying it is an action applying to one thing.
Understand "bury [something]" as burying it.
	
Check burying:
    try burying the noun in the sand instead.

Instead of burying the noun in the sand:
	if the player can see the noun:
		if the noun is the solar panel:
			if LabPower is false:
				say "The solar panel is already covered in sand.";
			otherwise if beetle is available:
				say "[if near-dark]As the sun begins to descend, you realize time is running short. The ruins aren't safe after dark -- that's when desert predators emerge to hunt, and nothing would save you from their hunger and ferocity.[paragraph break]Racing against the setting sun, you begin to dig, tossing sand over the gleaming solar panel. The work is hard and time is tight, but the nasty alternative of facing the Shadow Beasts spurs you on.[end if][line break]As you're scraping up another handful of sand, something catches your eye. A small, iridescent jewel gleams amid the grains. Curious, you brush away the sand to reveal a Gem Beetle, no larger than your thumbnail. To your amazement, it begins to glow with a soft, blue-green light.";
				say "[line break]You carefully pocket the beetle, recognizing it could be useful in the darkened complex. With renewed energy, you finish covering the solar panel.";
				say "[line break]You immediately notice the familiar hum of machinery fading away. The Caretaker's underground complex is most likely on minimal battery backup power now. In your pocket, the beetle's gentle glow feels like a small victory against the darkness.";
				now the player has the Gem Beetle;
				increase the score by Medium_Score_Bonus;
				now hasHeldBeetle is true;
			otherwise if the player is wearing the heat-resistant glove:
				say "It takes a long while, but you completely bury the solar panel in the sand.";
			otherwise:
				say "You scorch your palms as you toss handfuls of hot sand onto the solar panel. The grains are like tiny embers, burning your fingers.";
			now hasBuriedSolarPanel is true;
			[set all rooms underground to dark]
			repeat with place running through rooms in Underneath:
				now place is dark;
			now the glass shard is nowhere;
			now the description of solar panel is "The once-sleek black surface of the solar panel is now obscured by a layer of desert sand, rendering it useless.";
			now the image file name of Dwelling Ruins is "ruins-solar-glass-3.jpg"; 
			now the backgroundColor of Dwelling Ruins is "#dbb579";
			execute JavaScript command "updateImage('img/ruins-solar-glass-3.jpg','#dbb579');";
			now LabPower is false;
			stop the action;
		otherwise if the noun is the glass shard:
			try taking the noun instead;
			stop the action;
		otherwise:
			say "You reconsider burying [the noun].";
			stop the action;
	otherwise:
		continue the action;

Instead of burying the noun in something:
	say "You can't bury [the noun] in [the second noun].";

[has the hero buried the panel at least once?]
hasBuriedSolarPanel is a truth state that varies. hasBuriedSolarPanel is false.
LabPower is a truth state that varies. LabPower is false.

Catching is an action applying to one object.
Understand "catch [something]" as catching.

Instead of catching the Gem Beetle:
	try taking the Gem Beetle. 

Instead of taking the Gem Beetle:
	if the player carries the Gem Beetle:
		say "You already have that.";
	otherwise:
		say "You gently catch the Gem Beetle and carefully pocket it. Its soft glow provides constant illumination even through the fabric.";
		now the player carries the gem beetle;
		
Instead of catching something:
	try taking the noun.
	
The Gem Beetle is a thing.
The Gem Beetle is required.
The Gem Beetle is lit.
The Gem Beetle is nowhere.
The description of the Gem Beetle is "A tiny living jewel, no larger than your fingernail. Its iridescent shell gleams with a bright, blue-green glow reminiscent of a precious opal."

Before dropping the Gem Beetle:
	if the player is in the Underneath:
		say "You reconsider dropping the beetle.";
		stop the action;
	otherwise:
		continue the action.
		
Part 9 - Water Supply

Water Supply is a room.
The odor of Water Supply is "You smell a stagnant pool, a rancid cauldron of pungent stench that pierces the nostrils like a pointed stick. It's the odor of rot and decay."
The sound of the Water Supply is "the gentle trickle of murky water."
The image file name of Water Supply is "well-1.jpg".
The backgroundColor of Water Supply is "#beb296".
The sound-file-name of Water Supply is "396062__well-dripping.mp3".

Water Supply is scored. 
Water Supply is east of Dwelling Ruins.
The description of Water Supply is "Broken pillars and collapsed walls surround a long-abandoned well. The ground is overgrown with scraggly weeds, adding to the desolate and forgotten atmosphere. The ruins extend to the north, east, and west."

The ancient well is a container.
The ancient well is fixed in place.
The ancient well is in Water Supply.
The description of the ancient well is "An abandoned stone well, decayed and crumbling, stands at the center of the ruins. Its dark, greenish water emits a foul odor, with algae clinging to the edges.  The well's stagnant water reflects the bleak, overcast sky above.[if the ancient well is unexamined][paragraph break]As you approach the well, you notice weathered text carved into a fallen stone block: [italic type]'Three things mark a fool: walking alone at Dimming-Sky, seeking truth in ruins, and believing dead things stay dead.'[roman type] Below, in smaller letters: [italic type]'Heed this warning, children of Zephyr'.[roman type][end if]";

The molten lava is an enterable container in the Lava Pit. 
The molten lava is undescribed.
The description of molten lava is "Intense heat radiates from the fiery orange molten liquid."

The greenish water sample is a thing.
The greenish water sample is in the ancient well.
The indefinite article of the greenish water sample is "a".
The description of the greenish water sample is "A foul-smelling pool of stagnant water."
Understand "algae" as the greenish water sample.

The broken pillars, collapsed walls, and scraggly weeds are scenery in Water Supply.

Rule for printing the name of the ancient well:
	say "[printed name of the ancient well]";
	omit contents in listing.
	
The rusty box is in the ancient well.
The rusty box is a portable closed container.
The rusty box is required.
The description of the rusty box is "[if item described is closed]A rusted metal container, its once vibrant color faded to a dull brown. The rust has eaten away at the metal, forming intricate patterns and leaving behind a pitted, uneven surface.[else] A damaged rusty box that appears to have been used as a time capsule to preserve relics from the past.[end if]"

Rule for printing the name of the rusty box:
	say "[printed name of the rusty box]";
	omit contents in listing.
		
Instead of closing the rusty box:
	say "The box is too damaged to close properly.";
	stop the action.
		
Instead of attacking the rusty box:
	try opening the rusty box.
	
After taking the rusty box for the first time:
	say "Taken.";
	increase the score by Easy_Score_Bonus.
	
Understand "relics" as the rusty box.

The qubit stick is in the rusty box.
The qubit stick is required.
The description of the qubit stick is "It's a quantum storage device that relies on superposition and entanglement to store data."

Jumping into is an action applying to one thing.
Jumping on is an action applying to one thing.
Jumping over is an action applying to one thing.
Jumping in a direction is an action applying to one visible thing.

Understand "jump into [something]" as jumping into.
Understand "jump in [something]" as jumping into.
Understand "jump over [something]" as jumping over.
Understand "jump [something]" as jumping over.
Understand "jump on [something]" as jumping on.
Understand "jump [direction]" as jumping in a direction.

Instead of jumping in a direction:
    try going the noun.

Instead of entering the ancient well:
	try jumping over the ancient well.

Instead of jumping into something:
	if the noun is the ancient well:
		try entering the ancient well;
	otherwise:
		say "You reconsider jumping into [the noun].";

Instead of jumping over something:
	if the noun is the ancient well:
		clear the screen;
		say "Drawn by a strange daredevil compulsion, you jump but instantly regret taking such a foolish risk. You trip over the edge and fall face-first into the murky depths of the stagnant water. Surprised and embarrassed, you try to pull yourself out but the cold, slimy water wraps around you like a vice. Frantically, you thrash, but the algae-covered walls are too slick to grasp. All too soon, the foul liquid fills your lungs and panic sets in. Your limbs grow heavy and you disappear beneath the surface. The water swallows you whole, leaving only faint ripples.";
		pause the game;
		now the player is in Kai Transformed;
	otherwise:
		say "You reconsider jumping over [the noun].";

Instead of jumping on something:
	if the noun is the ancient well:
		try jumping over the ancient well;
	otherwise:
		say "Your reconsider jumping on [the noun].";
		
Instead of taking the greenish water sample:
	clear the screen;
	say "The well's rim, worn smooth by the passage of time, feels solid enough as you perch on its edge and stretch out. You reach deeper toward the greenish water below, then you hear it. The first grinding sound. It's faint, like a groan echoing up from the earth itself. Then comes the shudder of ancient mortar giving way.[paragraph break]The weathered masonry lurches suddenly and throws you forward into the stinking depths. Icy water floods into your nose and mouth, carrying the taste of decay. You claw at the walls, fingernails scraping raw against the algae-slicked stones, but each desperate grip betrays you, sliding you deeper into darkness.[paragraph break]Your waterlogged clothes drag you under like lead weights, and your lungs scream for air. The last thing you see is a warped circle of light far above, growing dimmer as the murk consumes you.";
	pause the game;
	now the player is in Kai Transformed.

Part 10 - Village Foundry

Village Foundry is a room.
The odor of Village Foundry is "You smell the stench of burnt metal and industrial ruin."
The image file name of Village Foundry is "foundry-1.jpg".
The backgroundColor of Village Foundry is "#a7896f".
The sound-file-name of Village Foundry is "608439__wind-less.mp3".
Village Foundry is scored. 
Village Foundry is north of Water Supply.
The Village Foundry is southwest of the Stone Tower Exterior.
The description of Village Foundry is "The ruins of a metal foundry linger, preserving the memory of a forgotten time. The weathered, cracked stone walls are all that remain of the once sturdy structure. The roof, long since collapsed, leaves the interior exposed to the elements. Despite the decay, the forge still lies within, its blackened surfaces bearing the marks of intense heat. Paths lead northeast and south from here."

After examining the forge for the first time:
	say "As you approach the forge, your foot dislodges a piece of debris, causing a section to shift. A soft grinding noise echoes through the ruins as the forge's base splits further, revealing a niche inside.";
	increase the score by Easy_Score_Bonus;
	now the niche is in The Village Foundry.
	
After taking the diary for the first time:
	increase the score by Easy_Score_Bonus.

The cracked stone walls, sturdy structure, and roof are scenery in the Village Foundry.

The heat-resistant glove is in Village Foundry.
The heat-resistant glove is wearable.
The heat-resistant glove is required.
glove-is-useless is a truth state that varies. glove-is-useless is false.
The description of the heat-resistant glove is "Its leather exterior is scorched and scarred from battling intense heat. The glove's fingers, once supple and nimble, are now stiff and misshapen." 

The rusty forge is scenery in the Village Foundry.
The description of the rusty forge is "In the center of the ruins lies a once-mighty forge, now reduced to a worthless relic. Once the proud centerpiece of the foundry, it has become a rusting, skeletal frame, its hearth a cold, soot-stained cavity."

Rule for printing the name of the crucible:
	say "[printed name of the crucible]";
	omit contents in listing.
	
The crucible is a container in the Village Foundry.
The crucible is required.
The description of the crucible is "Once used to melt and shape metals at searing temperatures, the old crucible is scarred and pitted, its interior blackened and soot-stained."

After taking the crucible for the first time:
	increase the score by Easy_Score_Bonus.

After taking the heat-resistant glove for the first time:
	increase the score by Easy_Score_Bonus.

Part 11 - Stone Tower Exterior

Stone Tower Exterior is a room.
The odor of Stone Tower Exterior is "The desert air is a bouquet of scrub brush and wildflowers, their scents dancing on the breeze."
Stone Tower Exterior is scored. 
The image file name of Stone Tower Exterior is "stone-tower-1.jpg".
The sound-file-name of Stone Tower Exterior is "625005__wind+birds.mp3".
The description of Stone Tower Exterior is "A crumbling stone tower looms before you, a sentinel in the desert landscape. Scrub brush dots the rocky terrain and clings to life in the arid soil. Old stone steps lead inside the structure. Paths lead away from the tower to the southeast, south, and southwest.";

The rocky terrain, arid soil, scrub brush are scenery in Stone Tower Exterior.

The old stone steps are scenery in the Stone Tower Exterior.
The description of the old stone steps is "Broken-down stone steps lead into the tower."

Instead of climbing the old stone steps:
	say "You make your way up the old stone steps."; 
	try going inside.

Part 12 - Stone Tower Interior

Stone Tower Interior is a room.
The odor of Stone Tower Interior is "The desert air is filled with the heady fragrance of wildflowers."
Stone Tower Interior is scored. 
The image file name of Stone Tower Interior is "stone-tower-interior-3.jpg".
The backgroundColor of Stone Tower Interior is "#dec6a9".
The sound-file-name of Stone Tower Interior is "silence".
Stone Tower Interior is inside from Stone Tower Exterior.
Stone Tower Interior is above the Stone Tower Exterior.
[could also try east]
The description of Stone Tower Interior is "A carpet of fallen stones and debris clog the entry. Native plants peek through the rubble, nourished by sunlight streaming through the tower's fractured crown. Treacherous stone steps lead up to the summit or down to the desert.";

The native plants and fractured crown are scenery in Stone Tower Interior.
The description of native plants is "Bleak, low-lying scrub brush mixed with a few lonely wildflowers."

The treacherous stone steps are scenery in the Stone Tower Interior.
The description of the treacherous stone steps is "Uneven stone steps covered in dust and debris lead upward to the tower's summit."

Instead of climbing the treacherous stone steps:
	say "You make your way up the old stone steps."; 
	try going up.

Part 13 - Stone Watchtower

Stone Watchtower is a room.
The odor of Stone Watchtower is "You smell the earthy aroma of the desert wind and the tang of sun-baked stone."
Stone Watchtower is scored.
The image file name of Stone Watchtower is "tower-summit-4.jpg".
The backgroundColor of Stone Watchtower is "#e0bd7d".
The sound-file-name of Stone Watchtower is "547472__wind-more.mp3".
Stone Watchtower is above Stone Tower Interior.
The description of Stone Watchtower is "[if the observation telescope is operational]The tower creaks and groans as you stand at the summit, the warm desert wind whipping over you. The horizon stretches out endlessly, a canvas of soft blue melting into the golden desert.[else]The watchtower platform is covered with uneven stones and debris after a recent earthquake.[end if] You notice a steep drop-off from the broken parapet. Far below, you see a large cluster of stones, their broad surfaces hard and unyielding. Uneven stone steps lead down."

Examining it through is an action applying to one touchable thing and one visible thing and requiring light.  
Understand "view [something] through [something]" as examining it through.
Understand "examine [something] through [something]" as examining it through.
Understand "look at [something] through [something]" as examining it through.
Understand "look through [something] at [something]" as examining it through (with nouns reversed).

After examining something through the telescope:
	if the noun is the horizon:
		say "The horizon looks closer.";
	otherwise:
		say "The [noun] looks larger.";
	stop the action.
	
After examining something through something:
	try examining the noun.
		
Instead of searching the observation telescope:
	if the telescope is damaged:
		say "The telescope no longer functions. It is damaged beyond repair.";
	otherwise:
		say "To look through the telescope, try gazing instead, like 'gaze up, or gaze east.' Many directions are possible and you can abbreviate them if you prefer, like 'gaze nw' for northwest, or 'gaze s' for south.";

Instead of searching the magnifying mirror:
	say "You look at yourself in the mirror. Handsome as ever.";
	
Understand "look [direction]" as facing.
Facing is an action applying to one visible thing and requiring light.

Carry out facing:
 	if the player is in the Stone Watchtower:
		if the noun is the east or the noun is the northeast:
			say "To [the noun] we see lava flowing through parched earth.";
		otherwise if the noun is the north or the noun is the northwest:
			say "To [the noun] you see an endless rocky, arid plain.";
		otherwise if the noun is the west or the noun is southwest:
			say "To [the noun] you see peaks clouded in a smokey haze.";
		otherwise if the noun is the down:
			try examining the down;
		otherwise:
			say "The sky unfolds in a breathtaking display from warm amber to a deep indigo.";
	otherwise:
		say "You see nothing unexpected in that direction."

Understand "gaze [direction]" as gazing. 
Gazing is an action applying to one visible thing and requiring light.

Carry out gazing when the player is not in the Stone Watchtower:
	say "There is nothing to gaze through here. Try looking instead.";
	stop the action.
	
Carry out gazing when the telescope is damaged:
	say "The telescope no longer functions. It is damaged beyond repair.";
	stop the action.
	
Carry out gazing when the noun is up:
	increment the horizon-view-count;
	clear the screen;
	execute JavaScript command "updateImage('img/horizon-up-6.jpg', 'default');";
	say "You look through the telescope, beyond the barren desert and the dark ruins that rise to meet the horizon. Above, the sky unfolds in a breathtaking display, transitioning from warm amber to a deep, rich indigo. The brighter stars begin to emerge, and points of light appear in the deepening blue.";
	say "[line break]Among the constellations, a peculiar glint catches your eye. It appears to be drifting slowly, distinct from the stationary stars around it.";
	say "[line break]The lawgivers of New Eden tell the children stories about an ancient spacecraft that brought our ancestors to Terra Nova centuries ago, but you always thought it was only a legend. But could this distant speck of silvery light be that very ship, you wonder?";
	say "[line break]Is it a lifeless hulk forever trapped in orbit, or perhaps a beacon, waiting and watching?";
	say "[line break]As more stars emerge, you feel a restlessness stirring. The horizon, so distant and unreachable, now seems to call out with the promise of adventure. Perhaps it is time to leave New Eden behind.";
	increase the score by Easy_Score_Bonus;
	stop the action.
		
Carry out gazing when the noun is north:
	clear the screen;
   execute JavaScript command "updateImage('img/horizon-north.jpg', 'default');";
   say "To the north, an endless expanse of rocky, arid plain painted in rust and slate stretches out before you. The landscape is vast, its monotony broken by abrupt rocky outcroppings that jut defiantly from the sandy ground.";
   say "[line break]The airborne dust softens the view and blurs the mountains into graceful, dark silhouettes, their rugged details smoothed by distance. Above, the vast expanse of sky unfolds in a display of muted yellow and cyan, the colors shifting subtly with the wind.";
   say "[line break]You can't help but notice the absence of any volcanic disturbance in this northern view, a stark contrast to the eastern desert. It's a deceptively peaceful scene, and you understand now why the villagers of New Eden prefer to face this direction. This northern vista offers a comforting mirage of stability while looking eastward only reminds them of their impending doom.";
	increase the score by Easy_Score_Bonus;
	stop the action.
		
Carry out gazing when the noun is south:
	increment the horizon-view-count;
   clear the screen;
   execute JavaScript command "updateImage('img/horizon-south.jpg', '#ad8665');";
	say "The vast desert stretches out to the south, a sea of sandy brown dunes that shimmer under the soft, misty gray sky. Scrub brush, hardy and resilient, clings to the sparse patches of soil, its green hues contrasting with the fiery landscape. In the distance, the horizon is framed by a majestic chain of blue mountains, their peaks disappearing into a translucent haze.";
	increase the score by Easy_Score_Bonus;
	stop the action.
		
Carry out gazing when the noun is east:
	increment the horizon-view-count;
	clear the screen;
   execute JavaScript command "updateImage('img/horizon-east.jpg', 'default');";
   say "From atop the watchtower, you peer east through the telescope and witness a stark, desolate landscape. The sight is both awe-inspiring and deeply concerning.";
   say "[line break]The arid terrain stretches out before you, jagged and menacing, scarred by the raw power of nature. Except for a few sparse scrub bushes clinging to life, the ground is a barren wasteland.";
   say "[line break]Your gaze is drawn to a river of molten lava, its fiery red stream cutting a relentless path through the parched earth, flowing ominously in your direction.";
   say "[line break]Though miles away, the advancing lava reminds you of the fact that volcanic activity is an increasing threat. In recent years, eruptions have become more frequent, reducing vast tracts of land to uninhabitable death traps, polluting ground water, and leaving the eastern desert more inhospitable than ever.";
	increase the score by Easy_Score_Bonus;
	stop the action.
	
Carry out gazing when the noun is west:
	increment the horizon-view-count;
   clear the screen;
   execute JavaScript command "updateImage('img/horizon-west.jpg', '#8b7a6a');";
   say "Through the telescope, you look westward and witness ominous clouds of ash leaking from smoldering volcanoes that dot the horizon.";
   say "[line break]While not as immediately threatening as the encroaching lava flows to the east, the danger to New Eden from this western front is clear. The village, nestled precariously close to these menacing peaks, faces an uncertain future. It seems only a matter of time before the community will be forced to abandon their homes, their lives altered by the relentless advance of ash and smoke.";
	increase the score by Easy_Score_Bonus;
	stop the action.

Carry out gazing when the noun is northwest:
	increment the horizon-view-count;
   clear the screen;
	execute JavaScript command "updateImage('img/horizon-northwest.jpg', '#efd7a3');";
	say "Through the telescope, a familiar landscape unfolds to the northwest, in stark contrast to the volcanic desolation to the east.";
   say "[line break]New Eden, your home village, stretches out before you, a collection of modest cottages nestled in the dry, hilly terrain. The winding dirt paths that connect the homes are clearly visible, forming an intricate design of daily life. Sadly, only sparse, drought-resistant vegetation can grow there now.";
   say "[line break]A wave of nostalgia washes over you as you recall the countless hours spent roaming those familiar paths and hills. Yet, as you gaze upon the village from a far-off vantage point, it's clear that your drive to explore is stronger than ever, and the village now seems small and limiting.";
   say "[line break]Perhaps it's time to leave New Eden behind, to embark on a journey that will change your life.";
	increase the score by Easy_Score_Bonus;
	stop the action.
		
Carry out gazing:
	say "[one of]Your view is obscured by ash clouds in that direction.[or]Your view is blocked by a smokey haze in that direction.[or]Your view is obscured by a severe dust storm on the horizon.[or]The telescope won't point that way.[then at random]";
	stop the action.
	
Instead of examining a direction when the player is in the Stone Watchtower:
	say "Instead, try typing 'look south', for example, or better yet, try gazing, like 'gaze south' to look through the telescope."
	
The canvas, golden desert are scenery in Stone Watchtower.

The observation telescope is in the Stone Watchtower.
The observation telescope is fixed in place.
The description of the observation telescope is "[if telescope is operational]The telescope stands on the parapet mounted on a solid stone pedestal, its weathered dedication plaque catching the dim light. The instrument provides a clear and magnified view of the distant horizon. With its powerful optics, it exposes the world in great detail, revealing hidden features and distant landscapes.[else]The telescope hangs at an unfortunate angle, its mounting cracked. The dedication plaque has torn free from the stone pedestal, exposing a hidden compartment within. The instrument is no longer functional.[end if][if the magnifying mirror is in the Stone Watchtower] Its magnifying mirror lies undamaged nearby.[end if]"

The stone compartment is nowhere.
The stone compartment is fixed in place.
The stone compartment is a container.
Understand "cavity" or "hiding place" or "recess" or "hiding spot" or "hole" as the stone compartment.
The description of the stone compartment is "Where the dedication plaque once sat, a carefully carved recess extends into the stone pedestal. The cavity is angled slightly downward, its edges precisely cut."
Rule for printing the name of the stone compartment:
	say "[printed name of the stone compartment]";
	omit contents in listing.

The bronze dedication plaque is in the Stone Watchtower.
The bronze dedication plaque is fixed in place.
The printed name of the bronze dedication plaque is "weathered dedication plaque".
Understand "historical" or "memorial" or "brass" or "metal" or "marker" or "foundation" as the dedication plaque.
The description of the bronze dedication plaque is "[if the telescope is operational]Years of wind-driven sand have scoured the once-gleaming bronze to a dull patina. The edges are worn smooth, and pockmarks pepper its surface like tiny meteor strikes.[else]The bronze plaque hangs twisted from the pedestal, bent outward like a swinging door. Fresh scratches score its carefully engraved text, behind its displaced face lies a stone compartment.[end if] The deeply engraved text remains legible: [paragraph break]ZEPHYR'S WATCHTOWER[line break]'[italic type]Built Upon Unshakeable Foundations, Our Eyes Ever Watchful'[paragraph break][roman type]This observation post stands as testament [line break]to the enduring stability of our settlement.[line break]Dedicated by the Engineering Guild of Zephyr".
Instead of reading the bronze dedication plaque:
	try examining the bronze dedication plaque.

swiveling stalk, swivel, powerful optics, and uneven stone steps are scenery in the Stone Watchtower.
The description of the uneven stone steps is "Uneven stone steps covered in dust and debris lead downward."
The description of the swiveling stalk is "The telescope can be aimed in eight different directions to explore views of the horizon."
The description of powerful optics is "Although the telescope is quite old and worn, the focus and clarity of the view are exceptional."

Carry out looking:
	if player is in the stone watchtower and the noun is the down:
		try examining the down instead;
		
Instead of taking the mirror for the first time:
	increase the score by Easy_Score_Bonus;
	now hasHeldMirror is true;
	say "You pick up the magnifying mirror.";
	now the player has the mirror.
	
The steep drop-off and broken parapet are scenery in the Stone Watchtower.
Understand "ledge" or "precipice" as broken parapet.

Instead of examining the steep drop-off:
	try examining the down.
	
Instead of examining the broken parapet:
	try examining the down.

Instead of climbing the uneven stone steps when the player is in Stone Watchtower:
	say "You make your way down the old stone steps."; 
	try going down.
	
Instead of examining the down when the player is in the Stone Watchtower:
	say "You grasp a sturdy handhold and feel a cold dread as you peer over the edge of the tower, a sheer drop that seems to plunge into infinity. The wind whips around you, carrying grains of sand that sting your cheeks. Far below you see a large cluster of stones, dark and weathered, their broad surfaces hard and unyielding. Suddenly your body instinctively pulls back from the precipice.";
	stop the action.
	
[DESTROY RUSTY BOX]
Understand "throw [something]" as throwing.
Understand "throw [something] off tower" as throwing.
Understand "drop [something] off tower" as throwing.
Understand "drop [something] from tower" as throwing.
Understand "throw [something] from tower" as throwing.
Understand "throw [something] off parapet" as throwing.
Understand "drop [something] off parapet" as throwing.

Understand "toss [something]" as throwing.
Understand "toss [something] off tower" as throwing.
Understand "hurl [something]" as throwing.
Understand "hurl [something] off tower" as throwing.

Throwing is an action applying to one thing.
	
Carry out throwing:
	if the player is in the Stone Watchtower:
		if the noun is the rusty box:
			say "The primal fear of falling into the abyss below leaves your legs weak and causes your heart to race. Yet you slowly inch forward holding the rusty box far in front of you. Your fingers grip its rough, corroded surface tightly, each cautious step brings you closer to the tower’s edge. The world seems to tilt beneath you. With one final exhale, you let the box slip from your grasp. Time stretches in silence as you wait, the pause endless as you strain to hear the impact. Then, at last, the sharp clang of metal striking stone echos up to you.";
			if the rusty box is closed:
				increase the score by Medium_Score_Bonus;
			now the rusty box is open in the Stone Tower Exterior;
			now hasOpenedTimeCapsule is true;
			stop the action;
		otherwise:
			try dropping the noun;
	otherwise:
		try dropping the noun.
		
Instead of dropping the rusty box in the Stone Watchtower:
	try throwing the rusty box.

Understand "precipice" or "edge" or "tower" as parapet.

Every turn when the player can see the telescope for more than 4 turns and the telescope is operational and the horizon-view-count > 3:
	clear the screen;
	say "[bold type][paragraph break]A low rumble in the distance catches your attention. The ground beneath your feet begins to tremble slightly...[paragraph break][roman type]";
	pause the game;
	execute JavaScript command "updateImage('img/tower-animation.gif', '#575a54');";
	execute JavaScript command "audioManager.playSound('[sound-state]', 'tremor', 'mp3/119782__tremor.mp3', 1, .75);";
	say "[roman type]The watchtower begins to sway gently, almost imperceptibly at first. Your mind, slow to grasp the implications, initially dismisses it as a trick of the wind. But the gentle movement quickly intensifies. A low rumble rises from below, growing louder and then, suddenly more violent. 
The stones beneath your feet begin to wobble, and you struggle to maintain your balance.
Before you can react, a fierce tremor shakes the tower, sending you sprawling across the rough stone surface. The dust rises and your heart races as you scramble to regain your footing.";
	say "[line break]A sharp, resonant snap draws your attention to the observation telescope. As you watch helplessly, it tilts and crashes onto the stone floor with the sound of twisting metal. Its eyepiece snaps off, skitters across the floor, and breaks into pieces, exposing a magnifying mirror and shattering the lens.";
	pause the game;
	execute JavaScript command "updateImage('img/telescope-damaged-1.jpg', '#575a54');";
	say "[line break]As the tremors subside, leaving you shaken and breathless, you stare at the ruined telescope lying broken at your feet. You notice the dedication plaque has torn free from the pedestal revealing a compartment carved into the face of the pedestal.";
	say "[line break]This latest tremor has confirmed what you've suspected for a long time -- that staying in New Eden means courting disaster. It's time to seek new horizons and begin a journey that will change your life.";
	now the observation telescope is damaged;
	now the image file name of Stone Watchtower is "telescope-damaged-1.jpg";
	now the backgroundColor of Stone Watchtower is "#575a54";
	now the magnifying mirror is in the Stone Watchtower;
	now the stone compartment is in the Stone Watchtower;
	try looking;

[suicide prevention]
Jumping off is an action applying to nothing.
Understand "jump off" as jumping off.
Leaping off is an action applying to one object.
Understand "jump off [something]" as leaping off. 

Carry out jumping off:
	if the player is in the Stone Watchtower:
		say "You reconsider ending your life. You have much left to do.";
	otherwise:
		say "There's nothing here to jump off."
		
Carry out leaping off something:
	if the player is in the Stone Watchtower:
		try jumping off.

Part 14 - Fallen Shrine		

The Fallen Shrine is a room.
The odor of Fallen Shrine is "A dusty, stale odor rises from the chamber."
The image file name of the Fallen Shrine is "shrine-5.jpg".
The sound-file-name of the Fallen Shrine is "608439__wind-less.mp3".
The Fallen Shrine is scored. 
The Fallen Shrine is east of the Water Supply.
The Fallen Shrine is south of Stone Tower.
The description of the Fallen Shrine is "Once a grand ceremonial chamber, this circular room now bears the scars of time and neglect. Natural light floods the interior, casting eerie shadows on the walls, once adorned with vibrant murals depicting the Caretaker and settlers working in harmony. These murals, now faded and forgotten, are hidden beneath layers of dust and decay. Broken stone benches lie scattered in rubble, a testament to the shrine's former glory. You can see a tower to the north. A path runs through the ruins east and west."

The broken stone benches are scenery in the Fallen Shrine.
Understand "mural" or "murals" as the mosaic.

The altar is a supporter in the Fallen Shrine.
The description of the altar is "At the room's center stands an ancient altar, once the focal point of the chamber. It is made of a smooth, metallic material that still gleams dully under the layers of dust. Your eye is drawn to a curious concave hollow near its center. The smooth precision of its curve stands in stark contrast to the surrounding weathered metal. As you study it, the hollow's interior seems to catch the light in an unusual way, its surface shimmering with a faint, rainbow luster that dances as you shift your perspective.";

The mosaic is in the Fallen Shrine.
The mosaic is fixed in place.
The description of the mosaic is "Occupying a prominent place of reverence, this wall-sized tile mosaic was clearly meant to inspire awe in the colonists. At the center, the Caretaker is depicted as more than just a provider or protector; she is cast as a deity, fostering dependence and unquestioning loyalty. The settlers, while appearing content, are dwarfed by the robot, subtly reinforcing her dominance and control.[paragraph break]The Caretaker looms at the center -- a colossal, yet motherly robotic figure. Her metallic body gleams with an otherworldly radiance, composed of tiles in shades of silver and gold. Her arms extend outward in a nurturing gesture. One arm cradles a glowing gestation pod pulsing with life. Another reaches toward a group of children, offering a book, symbolizing education and guidance. A third arm stretches protectively over a cluster of adult colonists, shielding them from unseen dangers. The fourth arm extends downward, presenting tools to eager farmers. In the foreground, a scene of abundant harvest unfolds, with grateful colonists tending to fields bursting with crops.[paragraph break]Despite the many cracked, broken, and loose tiles, the overall image remains clear and powerful, its colors vibrant despite centuries of neglect.";

After examining the mosaic:
	now hasHeardOfCaretaker is true.

The concave hollow is part of the altar.
The concave hollow is a container.
The description of the concave hollow is "A curious cup-shaped depression, about the size of your fist, rests near the center of the altar."
Understand "place [something] on [something]" as putting it on.
Understand "place [something] in [something]" as inserting it into.

Instead of putting something on the altar:
	try inserting the noun into the concave hollow.

Instead of putting something on the concave hollow:
	try inserting the noun into the concave hollow.	
Before inserting something into the concave hollow:
	if the noun is the Chrono Orb:
		continue the action;
	otherwise:
		say "Inserting [the noun] into the concave hollow has no effect." instead.

After inserting the Chrono Orb into the concave hollow:
	clear the screen;
	clear the Vorple status line;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'blip', 'mp3/434137__projector-blip.mp3', 1, .70, 2000);";
	say "As you place the Chrono Orb into the hollow, it settles perfectly into place. The rainbow light intensifies, and you hear a faint, mechanical click folowed by soft humming.";
	say "[line break]The air above the orb shimmers, and ghostly images begin to form -- scenes of Zephyr from a time long past. Could this be the answers you seek about the true history of Zephyr?";
	pause the game;
	clear the screen;
	execute JavaScript command "updateImage('img/settlement-1.jpg', '#b8a271');";
	say "[paragraph break][roman type]In the shadow of the Zephyr lander, a new chapter of human history began to unfold. The Caretaker Bot, a marvel of artificial intelligence, set about constructing a modest human settlement on rocky soil amid the native vegetation. It was a sparse and rugged environment, yet suitable for the initial batch of labor units who were incubated and decanted here.";
	say "[line break][roman type]The Caretaker's role expanded to become teacher, provider, and guardian. She trained the young colonists in essential survival skills -- hunting techniques, food gathering, and basic farming -- always emphasizing caution and safety, and discouraging any exploration or risk-taking that might endanger the precious settlers.";
	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/71852__page-turn.mp3', 1, .33);";
	execute JavaScript command "updateImage('img/future-village-5.jpg', '#bdc3a7');";
	say "[paragraph break][roman type]As the population steadily increased, the village of Zephyr's Landing transformed. The original shelters gave way to more permanent structures. The Caretaker, ever adaptable, oversaw the construction of sturdy homes, expanded farmlands, and a basic road system connecting the growing community.";
	say "[line break][roman type]A central meeting place was established, serving as both a governing center and a place of reverence for the Caretaker, whose wisdom and guidance were key to the colony's success. As the settlement grew, The Caretaker, with an eye toward efficiency, shortened its name to 'Zephyr.'";
	say "[line break][roman type]The Caretaker's versatility proved invaluable during this phase. It shifted roles between civil engineer, agricultural expert, educator, and lawmaker. Under her guidance, Zephyr flourished, its population growing through both the artificial cultivation of new batches in gestation vats and the first naturally produced biological products.";
	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/71852__page-turn.mp3', 1, .33);";
	execute JavaScript command "updateImage('img/future-village-n.jpg', 'default');";
	say "[paragraph break][roman type]As Zephyr grew into a thriving town, the population stabilized, and the community became largely self-sustaining. Infrastructure continued to improve, with the Caretaker overseeing the development of irrigation systems, roads, and buildings for education, healthcare, and community gatherings.";
	say "[line break][roman type]Zephyr now flourished with lush farmlands and a busy town center. The Caretaker, while still revered and central to the town's operations, began to delegate more responsibilities to capable human leaders.";
	say "[line break][roman type]Despite the mysterious deaths of the first naturally conceived replacement units, the town's operational efficiency remained acceptable. The Caretaker's leadership, combining compassion with logic, helped the community navigate this challenging period, fostering a sense of unity and shared purpose among the citizens of Zephyr.";
	say "[line break][roman type]Zephyr stands proudly under Terra Nova's alien sky, a harmonious blend of human ingenuity and artificial intelligence. The town had thrived, transforming from a tentative landing site into a solid community ready to face whatever challenges the future might hold.";
	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/71852__page-turn.mp3', 1, .33);";
	clear the screen;
	execute JavaScript command "updateImage('img/shrine-5.jpg', 'default');";
	execute JavaScript command "audioManager.playSound('[sound-state]', 'roomSound', 'mp3/608439__wind-less.mp3', -1);";
	if hasSeenOrbContents is false:
		say "[line break]As the orb's display fades, your mind is buzzing with newfound knowledge -- but also some frustration. You've learned so much about the early days of Zephyr, but only scratched the surface.";
		say "[line break]'But what about before? How did we even get here?' you mumble. You wonder about the stories you've heard since childhood about a great journey across the stars to Terra Nova. But the details remain frustratingly vague.";
		say "[line break]You pocket the Chrono Orb.";
		now the player has the Chrono Orb;
		increase the score by Easy_Score_Bonus;
	otherwise:
		try looking;
	now hasSeenOrbContents is true;
	stop the action.
		
After examining the altar:
	now the altar is examined;
	if something is in the concave hollow:
		say "In the concave hollow, you notice [a list of things in the concave hollow].";

Part 15 - Lava Pit
		
The Lava Pit is a room.
The odor of Lava Pit is "The air is a suffocating cauldron of sulfur, the devil's own breath straight from the depths of hell."
The image file name of Lava Pit is "lava-pit-1.jpg".
The backgroundColor of the Lava Pit is "#e2c7a6".
The sound-file-name of the Lava Pit is "415609__lava-bubbles.mp3".
The Lava Pit is scored.
The Lava Pit is east of the Fallen Shrine and southeast of the Stone Tower Exterior.

The description of The Lava Pit is "A molten pool of lava cuts a deep scar in the desert. The air above reeks of sulfur and shimmers with intense heat, suffocating the scorched vegetation nearby.[first time] This area has always been a site of volcanic activity, the ground trembling slightly beneath the surface. But in recent years, the flow has been nearly constant, filling the pit and casting a fiery glow across the barren landscape. The high concentrations of dissolved minerals and metals have led to contaminating wells affecting colonists' water supplies.[only] A path leads northwest to the tower or west to the shrine.";

The molten lava is an enterable container in the Lava Pit. 
The molten lava is undescribed.
The description of molten lava is "Intense heat radiates from the fiery orange molten liquid."

The lava sample is a thing.
The lava sample is nowhere.
The indefinite article of the lava sample is "a".
The description of the lava sample is "a seething mass of liquid fire."

Sampling is an action applying to one thing. Understand "sample [something]" as taking.
Scooping is an action applying to one thing. Understand "scoop [something]" as taking.
Collecting is an action applying to one thing. Understand "collect [something]" as taking.

Scooping it into is an action applying to two things. 
Understand "scoop [something] into/with/in [something]" as scooping it into.
Understand "collect [something] in [something]" as scooping it into.

Filling it with is an action applying to two things.
Understand "fill [something] with [something]" as filling it with.
Understand "dip [something] in [something]" as filling it with.

Instead of scooping the molten lava into the crucible:
	try sampling the molten lava.

Instead of scooping the lava sample into the crucible:
	try sampling the molten lava.

Instead of filling the crucible with the molten lava:
	try sampling the molten lava.
	
Instead of filling the crucible with the molten lava:
	try sampling the molten lava.
	
Instead of filling the crucible with the lava sample:
	try sampling the molten lava.
	
Instead of scooping the greenish water into the crucible:
	try taking the greenish water sample.

Instead of scooping something into something:
    say "That action will do you no good."

hasSampledLava is a truth state that varies. hasSampledLava is false.

Instead of taking the lava sample:
	if the player is in the Lava Pit:
		try sampling the molten lava.
		
Instead of taking the molten lava:
	try sampling the molten lava.
	
Before sampling the molten lava:
	if glove-is-useless is true:
		say "The heat-resistant glove is too damaged to protect you. The crucible remains empty.";
		stop the action;
	if the lava sample is in the crucible:
		say "The crucible is already filled to capacity with lava.";
		stop the action;
	while something (called the item) is in the crucible:
		now the item is in the location;
		say "You remove [the item] from the crucible.";

Carry out sampling the molten lava:
	if the player carries the crucible:
		if the player is wearing the heat-resistant glove:
			say "You dip the crucible into the lava, feeling the unbearable heat intensify. The glove holds firm as you fill the crucible to capacity and pull it from the fiery pool.";
			now the lava sample is in the crucible;
			if hasSampledLava is false:
				increase the score by Easy_Score_Bonus;
			now hasSampledLava is true;
			stop the action;
		otherwise if the player carries the heat-resistant glove:
			say "You have to wear the heat-resistant glove first.";
			stop the action;
		otherwise:
			say "You step back. The lava is too hot to handle.";
			stop the action;
	otherwise:
		say "It's too hot.";
		stop the action;
		
Instead of touching the molten lava:
	try sampling the molten lava.

Every turn when the player can see the crucible and the lava sample is in the crucible for more than 5 turns:	Say "The glow of the lava sample within the crucible is going dim.";

Every turn when the player can see the crucible and the lava sample is in the crucible for 12 turns:
	Say "The lava sample has hardened to stone in the crucible.";
	now the lava sample is nowhere.

Instead of jumping into the molten lava when the player is in the Lava Pit:
	say "You reconsider ending your life. You have much left to do."
	
Instead of jumping over the molten lava when the player is in the Lava Pit:
	clear the screen;
	say "Drawn by a strange daredevil compulsion, you jump -- but instantly regret taking such a foolish risk. You trip over the rocky edge and fall face-first into a lake of fire. Your blood boils, and your bones melt on contact. Mercifully, it's all over quickly, and you disappear beneath the surface. The lava pit swallows you whole and burps a trace of steam.";
	pause the game;
	now playerDead is true;
	
Instead of inserting something into the molten lava:
	if the noun is required:
		say "You reconsider vaporizing [the noun].";
	otherwise:
		say "[The noun] disappears with a sizzling hiss.";
		now the noun is nowhere.
	
Instead of entering the molten lava for the first time:
	say "You feel the intense heat radiating from the molten lava and step back instinctively. That's not a place you'd survive!";
    
Instead of entering the molten lava for the second time:
	say "Ignoring the warning signs, you jump into the pit... and are instantly engulfed in flames.";
	pause the game;
	now playerDead is true;	
	
Part 16 - Lab Tunnel

The Lab Tunnel is a dark room.
The odor of the Lab Tunnel is "The air is thick with the stench of decay and dampness."
The image file name of the Lab Tunnel is "tunnel-2.jpg".
The sound-file-name of the Lab Tunnel is "177958__water-drip-cave.mp3".
The Lab Tunnel is scored.
The Lab Tunnel is west of the Eerie Tunnel.
The Lab Tunnel is northwest of the Research Lab.

The description of the Lab Tunnel is "An abandoned tunnel extends ahead of you into near darkness. The floor is uneven pitted stone, the walls cracked and stained from neglect. The silence is broken only by the distant drip of water.[if hasBuriedSolarPanel is true] Because the light is so dim here, you infer that the solar array in the ruins powers this complex below.[end if] The tunnel winds to the east, the Research Lab is southeast, and a rusty ladder leads up to the surface.[if using-beetle-light][paragraph break][one of]The beetle lights your way.[or]The beetle's glow illuminates the darkness.[or]Soft light from the beetle reveals the shadowy passage.[or]Your pocket glows faintly lighting the tunnel before you.[or]The beetle's radiance pierces the gloom, allowing you to proceed.[at random][end if]"

Before going to The Lab Tunnel:
	if the Lab Tunnel is lit or the player has the beetle:
		execute JavaScript command "updateImage('img/tunnel-2.jpg');";
		now the image file name of the Lab Tunnel is "tunnel-2.jpg";
	else:
		execute JavaScript command "updateImage('img/dark-tunnel.jpg');";
		now the image file name of the Lab Tunnel is "dark-tunnel.jpg";
		
Before going from the Lab Tunnel:
	if the Lab Tunnel is lit or the player has the beetle or the noun is up:
		continue the action;
	otherwise: 
		say "You can't make progress in the dark. A dim light above you reveals an exit.";
		stop the action;
		
Instead of climbing the ladder in the Lab Tunnel:
	try going up;

Part 17 - Eerie Tunnel

The Eerie Tunnel is a room.
The odor of the Eerie Tunnel is "Its odor is a haunting echo of forgotten times."
The image file name of the Eerie Tunnel is "tunnel-3.jpg".
The sound-file-name of the Eerie Tunnel is "177958__water-drip-cave.mp3".
The Eerie Tunnel is scored.
The Blocked Passage is east of the Eerie Tunnel.
The description of the Eerie Tunnel is "Your footsteps echo on cold, damp stone, the passage stretching out ahead in near darkness. Grimy walls, stained with age and decay, close in, and secrets lurk in shadow. Dread gnaws at you but curiosity compels you. Go east to venture deeper into the silence or west to backtrack." 

Part 18 - Blocked Passage

The Blocked Passage is a room.
The odor of the Blocked Passage is "The air is heavy with the scent of dust and decay...and maybe dung."
The image file name of the Blocked Passage is "blocked-tunnel-darker.jpg".
The sound-file-name of the Blocked Passage is "609182__sleeping-beast.mp3". 
The Blocked Passage is scored.
The description of the Blocked Passage is "The passage ahead is blocked by a cave-in, a chaotic jumble of rock and sand. The air here is heavy with the scent of dust and decay. You catch a glimpse of something metallic glinting in the dim light. You can retreat to the west if you lack courage.[paragraph break][one of]a low, menacing growl emanates from behind the obstruction, causing you to pause[or]The silence is oppressive, and the sense of entrapment weighs heavily as you survey the rubble[or]The way forward is blocked, leaving only the echo of your own footsteps and the distant murmur of hidden dangers[cycling]." 

The cave-in, rock, gap, rocks, air, dust, decay, debris pile, ground, dirt, rubble are scenery in the Blocked Passage.

Instead of examining scenery in the Blocked Passage:
	say "[one of]You study the sandy debris pile looking for the best path up and over[or]You can see dim light through the debris and wonder if you could climb through a gap to the other side[or]You survey the obstacles blocking your way[or]The way forward is blocked, but your curiosity compels you to consider climbing[or]the chain rattles and a threatening snort comes from behind the rocks, causing you to pause[or]You spot a sliver of light amidst the darkness, a possible path through the rocks[or]You contemplate taking the risk of climbing when a single misstep could send you tumbling[cycling]."

[too late dude; you're toast]
Instead of entering in the Blocked Passage:
	try climbing the rubble.
	
Instead of climbing up in the Blocked Passage:
	try climbing the rubble.
	
Instead of climbing in the Blocked Passage:
	clear the screen;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'snore', 'mp3/744790__beast-snores.mp3', -1);";
	execute JavaScript command "audioManager.playSound('[sound-state]', 'climbing-rubble', 'mp3/169553__climbing-rubble.mp3', -1);";
	say "[line break]'It takes more than an angry rodent to stop me from exploring,' you declare, eyeing the debris pile. The chain rattles again, accompanied by another warning growl and snort, but you dismiss it. 'Nice try, little man, but I'm going over the top.'";
	say "[line break]Your curiosity about what lies beyond the rock pile grows stronger. The unexplored passage beckons, promising answers to the mysteries that brought you here. You've come too far to let a minor obstacle, or puny smidglet, stand in your way.";	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'roar', 'mp3/505127__beast-roar.mp3', 1);";  
	execute JavaScript command "audioManager.playSound('[sound-state]', 'grunt', 'mp3/738437__beast-grunt-1.mp3', 1, .75, 7500);";
	execute JavaScript command "audioManager.playSound('[sound-state]', 'rockslide', 'mp3/60085__rockslide.mp3', 1 );";
	say "[line break]You place your foot on the lowest stone, testing its stability. It seems solid enough. The growling grows louder, a clear warning that echoes through the tunnel.";
	say "[line break]You begin to scale the rocks and debris, carefully choosing your handholds and footholds. The smidglet's warnings grow louder, more frantic. The chain rattles violently against the stones, but you continue.";
	say "[line break]When you grasp the edge of a large boulder you feel a sudden shift. Before you can react, sand and debris cascade down around you sending you tumbling onto the damp tunnel floor.";
	say "[line break]Disoriented, you push yourself up, and that's when you hear it -- an angry growl, primal and hungry -- a roar that no smidglet could ever produce.";
	pause the game;
	execute JavaScript command "updateImage('img/tunnel-beast-robot-3.jpg');";
	execute JavaScript command "audioManager.playSound('[sound-state]', 'roar', 'mp3/505127__beast-roar.mp3', 1, 1, 1000);";  
	execute JavaScript command "audioManager.playSound('[sound-state]', 'roar', 'mp3/332520__beast-roar-big.mp3', 1, 1, 3000);";  
	execute JavaScript command "audioManager.playSound('[sound-state]', 'grunt', 'mp3/315769__beast-growl.mp3', 1, .75, 4500);";	execute JavaScript command "audioManager.playSound('[sound-state]', 'grunt', 'mp3/738437__beast-grunt-1.mp3', 1, .75, 5500);";
	execute JavaScript command "audioManager.stop('climbing-rubble');";   [stop running, you're dead]
	say "[line break]Your actions have partially cleared the blockage, revealing not the pocket-sized creature you expected, but a massive Shadow Beast staring at you from an opening in the rubble.";
	say "[line break]As the dust settles, your eyes widen in shock. This is no ordinary beast -- metal components are integrated into its scaly hide. Servos whir and hydraulics hiss as the creature moves toward you, its artificial eyes glowing an eerie yellow. Patches of its skull have been replaced with reinforced plating, and you notice wires beneath the gaps in its scales.";
	say "[line break]Before your brain can process this nightmarish fusion of nature and technology, the cyborg beast lets loose a roar that shakes dust loose from the ceiling and paralyzes you in primal terror.";
	execute JavaScript command "audioManager.playSound('[sound-state]', 'climbing-rubble', 'mp3/169553__climbing-rubble.mp3', 1);";
	[execute JavaScript command "audioManager.playSound('[sound-state]', 'sleeping', 'mp3/609182__sleeping-beast.mp3', -1);";  ]
	pause the game;
	execute JavaScript command "audioManager.stop('climbing-rubble');";   [stop running, you're dead]
	execute JavaScript command "audioManager.playSound('[sound-state]', 'grunt', 'mp3/699928__beast-grunt-4.mp3', 1, 1, 1000);";  
	execute JavaScript command "audioManager.playSound('[sound-state]', 'grunt', 'mp3/738438__beast-grunt-2.mp3', 2, .5, 3500);";  
	execute JavaScript command "audioManager.playSound('[sound-state]', 'roar', 'mp3/505127__beast-roar.mp3', 1, 5500);";  
	execute JavaScript command "audioManager.playSound('[sound-state]', 'grunt', 'mp3/738439__beast-grunt-3.mp3', 1, .5, 6500);"; 
	execute JavaScript command "audioManager.playSound('[sound-state]', 'growl', 'mp3/315769__beast-growl.mp3', 1, .5, 7500);"; 
	say "[line break]The beast lunges. The chain restraining it snaps under the combined strength of muscle and machine. You try to scramble away, but it's too late.";
	say "[line break]Angry fangs close around you. The last thing you see is bloody teeth and metal claws. Pain explodes through your body as steel shreds your flesh, and then... nothing.";
	say "[line break]Your adventure ends here, in the belly of the Shadow Beast. Your final thought leaves an unanswered question: Who is the fiend who created this hybrid monstrosity?";
	execute JavaScript command "audioManager.playSound('[sound-state]', 'drip', 'mp3/177958__water-drip-cave.mp3', -1);";  
	pause the game;
	execute JavaScript command "audioManager.stopAll();";
	now playerDead is true;

chainPulls is a number that varies. chainPulls is 0.
		
The rusty chain is in the Blocked Passage.
The description of the rusty chain is "A rusty chain snakes from beneath the fallen debris."

Instead of taking the rusty chain:
	try pulling the rusty chain.

gruntSoundFile is a text that varies.
	
After pulling the rusty chain:
	increase chainPulls by 1;
	let gruntSoundFile be "[one of]mp3/468442__smidglet-squeak.mp3[or]mp3/738439__beast-grunt-3.mp3[or]mp3/738437__beast-grunt-1.mp3[or]mp3/738438__beast-grunt-2.mp3[or]mp3/699928__beast-grunt-4.mp3[then at random]";
	execute JavaScript command "audioManager.playSound('[sound-state]', 'grunt', '[gruntSoundFile]', 1);"; 
	say "[one of]You hear a low growl from beyond the debris[or]A rumbling growl accompanied by a deep exhale of displeasure comes from behind a large stone[or]You hear a threatening grunt followed by a snort[or]An intimidating hiss comes from behind the rocks[or]The sound of chattering teeth, barely audible, comes from under the debris[then at random]."
	
After pulling the rusty chain for the first time:
	increase chainPulls by 1;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'growl', 'mp3/315769__beast-growl.mp3', 1);";
	say "When you take hold of the chain and give it a pull, a muffled growl comes back at you from the other side of the debris wall. You realize something is angry on the other end.";
	say "[line break]You pause to consider your options, telling yourself, 'It[apostrophe]s probably just a smidglet', those small, burrowing creatures you've seen all over Terra Nova. Hardly dangerous, but they can sure make a racket.";
	say "[line break]You chuckle, imagining a tiny smidglet on the other side, puffing itself up trying to sound fierce. 'Everything sounds louder in these tunnels,' you say, 'It[apostrophe]s probably just feeling trapped and scared.'";
	
Pouring it on is an action applying to two things.
Understand "pour [something] on [something]" as pouring it on.
Understand "pour [something] onto [something]" as pouring it on.

Melting it with is an action applying to two things.
Understand "melt [something] with [something]" as melting it with.

Instead of melting with:
	try pouring the second noun on the noun.

Check pouring on:	
	if the noun is the lava sample and the player cannot see the lava sample:
		say "You don't have any lava to pour."; 
		stop the action;
	otherwise if the noun is the crucible and the player cannot see the lava sample:
		say "There's no lava in the crucible.";
		stop the action;
	otherwise if the noun is not the crucible and the noun is not the lava sample:
		say "Your action has no effect.";
		stop the action;
	otherwise if the noun is the crucible and the second noun is the crucible:
		try pouring the lava sample on the chain;
		stop the action;			
	otherwise if the noun is the lava sample and the second noun is the crucible:
		try pouring the lava sample on the chain;
		stop the action;			
	otherwise:
		continue the action.	
			
Before pouring something on:
	if the noun is the lava sample:
		if the second noun is the lava sample:
			say "What do you want to pour the lava sample on?";
			stop the action;
		otherwise if the second noun is the crucible:
			say "What do you want to pour the lava sample on?";
			stop the action;
		otherwise:
			continue the action;
	otherwise if the noun is the crucible:
		if the second noun is the crucible:
			say "What do you want to pour the lava sample on?";
			stop the action;
		otherwise:
			continue the action;
	otherwise:
		continue the action;
		
Check pouring the lava sample on something:
	if the second noun is not the chain and the second noun is not the rusty box:
		say "Pouring molten lava on [the second noun] doesn't seem like a good idea.";
		stop the action.
		
Check pouring the crucible on something:
	if the second noun is not the chain and the second noun is not the rusty box:
		say "Pouring molten lava on [the second noun] doesn't seem like a good idea.";
		stop the action.

Carry out pouring the lava sample on the rusty box:
	if the player is not wearing the heat-resistant glove:
		say "You'll burn yourself without wearing hand protection.";
		stop the action;
	otherwise if the rusty box is open:
		say "The rusty box is already open. No need to pour lava on it.";
		stop the action;
	otherwise if hasHeldKeycard is true:  [already poured on chain]
		say "The intense heat has damaged the protective glove. It's no longer safe to use.";
		stop the action; 
	otherwise:
		silently try dropping the rusty box;

Report pouring the lava sample on the rusty box:
	if the player is wearing the heat-resistant glove and hasHeldKeycard is false:
		say "Balancing the crucible of lava in a steady hand, you carefully pour the molten lava onto the rusty box. An immediate hiss fills the air. The box glows red-hot, then with a final, resounding snap, it breaks open revealing its contents.";
		say "[line break]It appears to be a time capsule containing historical relics. Inside you find: [a list of things in the rusty box].[paragraph break]";
		now the rusty box is open;
		now the lava sample is nowhere;
		increase the score by Easy_Score_Bonus;
	otherwise:
		stop the action.
		
Instead of pouring the crucible on the rusty box:
	try pouring the lava sample on the rusty box.
		
Carry out pouring the lava sample on the chain:
	if the player is not wearing the heat-resistant glove:
		say "You'll burn yourself without wearing hand protection.";
		stop the action;
	otherwise:
		clear the screen;
		say "Balancing the crucible of lava in a steady hand, you carefully pour the molten lava onto the metal links. An immediate hiss fills the air, followed by a sizzling crack. From beyond the rubble, you hear confused grunts and the scraping of claws on stone.";

Instead of pouring the crucible on the chain:
	try pouring the lava sample on the chain.

Report pouring the lava sample on the chain:
	if the player is wearing the heat-resistant glove:
		execute JavaScript command "audioManager.playSound('[sound-state]', 'rockslide', 'mp3/60085__rockslide.mp3', 1);";
		say "The chain glows red-hot, then with a final, resounding snap, it breaks apart.";
		say "[line break]Suddenly, a thunderous roar shakes the tunnel, and the debris pile begins to shift. Rocks tumble down as something massive slams against the other side. You stumble back, heart pounding, as the barrier collapses in a cloud of dust and falling stones.";
		pause the game;
		execute JavaScript command "updateImage('img/tunnel-beast-robot-3.jpg');";
		execute JavaScript command "audioManager.playSound('[sound-state]', 'rockslide', 'mp3/60085__rockslide.mp3', 1, .75);";
		execute JavaScript command "audioManager.playSound('[sound-state]', 'grunt', 'mp3/738439__beast-grunt-3.mp3', 1, 1, 2000);";
		execute JavaScript command "audioManager.playSound('[sound-state]', 'snore', 'mp3/744790__beast-snores.mp3', -1);";
		execute JavaScript command "audioManager.playSound('[sound-state]', 'growl', 'mp3/315769__beast-growl.mp3', 1, 1, 3000);"; 
		say "[line break]As the dust settles, your eyes widen in shock as you come face to face with a massive Shadow Beast. But this is no ordinary creature. It's a dangerous fusion of lizard and machine with metal components integrated into its scaly hide.";
		say "[line break]The beast rises to its full height, easily as tall as a man, its muscular body a patchwork of wires merged with reptilian scales and metallic plates. Servos whir and hydraulics hiss as it moves, its segmented tail twitches menacingly.";
		say "[line break]Its eyes fix on you with a wild, predatory stare, and its jaws open to reveal a nightmarish mix of natural yellow fangs and glinting metal blades.";
		say "[line break]Its rancid breath hits you in waves, carrying the unmistakable stench of decay and machine oil.";
		pause the game;
		say "[line break]With a sluggish lunge, the cyborg beast charges forward. You stumble back in terror, certain that your end has come. But at the last moment, the beast veers off, turns, and barrels down the tunnel, disappearing into the darkness.";
		pause the game;
		execute JavaScript command "updateImage('img/unblocked-tunnel-b.jpg');";
		execute JavaScript command "audioManager.playSound('[sound-state]', 'roomSound', 'mp3/177958__water-drip-cave.mp3', -1);";
		execute JavaScript command "audioManager.stop('snore');";
		say "[line break]As your heartbeat slows, you notice something glinting on the ground amid the fallen debris. You catch a glimpse of a thin, metallic object half buried in the sand. Upon closer inspection, you realize it's a silver keycard.";
		say "[line break]You pocket the keycard and contemplate what fiendish mind could have created such a shocking fusion of living flesh and machine.";
		say "[line break]Go east to explore the Unblocked Tunnel or west to turn back the way you came.";
		increase the score by Medium_Score_Bonus;
		now the player has the silver keycard;
		now hasHeldKeycard is true;
		now the rusty chain is nowhere;
		now the lava sample is nowhere;
		now glove-is-useless is true;
		now the description of the heat-resistant glove is "The glove is now a charred husk. Its leather has cracked and peeled, with gaping holes at the fingertips and palm making it useless.";
		[The Blocked Passage has no path to it, so it is logically deleted from this world]
		now the Unblocked Tunnel is mapped east of the Eerie Tunnel;
		now the Eerie Tunnel is mapped west of the Unblocked Tunnel;
		move the player to the Unblocked Tunnel, without printing a room description;
	otherwise:
		stop the action.
		
Part 19 - Unblocked Tunnel

The Unblocked Tunnel is a room.
The odor of the Unblocked Tunnel is "You smell only dust and dampness."
The image file name of the Unblocked Tunnel is "unblocked-tunnel-b.jpg".
The sound-file-name of the Unblocked Tunnel is "177958__water-drip-cave.mp3".
The Unblocked Tunnel is scored.
The Unblocked Tunnel is west of the Supply Room.
The description of the Unblocked Tunnel is "You step over rubble and debris. No sign of the Shadow Beast. There is light to the east. Going west leads you toward the tunnel exit." 

Part 20 - Supply Room

Before going to the Supply Room:
	execute JavaScript command "audioManager.stopAll();";

eveDead is a truth state that varies. eveDead is false.

Supply Room is a room.
The odor of the Supply Room is "An unsettling aroma, a blend of metal and decay, hangs heavy in the room."
The image file name of the Supply Room is "cyborg-hybrid-1.jpg".
The sound-file-name of the Supply Room is "32899__supply-room.mp3".
The sound-volume of the Supply Room is 0.50.
Supply Room is scored.
Supply Room is west of Cyborg Assembly.
The description of the Supply Room is "The dimly lit closet reeks of ozone and decay. A tangle of cables and broken electronics litter the floor, creating a haphazard maze. In the dim light, you can make out the outline of a figure slumped against the wall. Exits are to the east and west."

The wall, floor, tangle of cables, broken electronics are scenery in Supply Room.

The metal supply cabinet is nowhere.
The metal supply cabinet is a container.
The metal supply cabinet is fixed in place.
The description of the metal supply cabinet is "A corroded metal cabinet with flaking gray paint leans precariously against the wall, its dented surface marked by streaks of rust trickling down from the edges like dried blood. One of its double doors hangs askew on broken hinges, while the other remains stubbornly shut, warped by time and perpetual dampness."

Instead of opening the metal supply cabinet:
	say "It's already open."
	
Instead of closing the metal supply cabinet:
	say "The hinges are broken and it won't shut."
	
The cyborg kit is in the metal supply cabinet.
To say list of parts:
	repeat with item running through things that are part of the cyborg kit:
		say "  [description of the item][line break][line break]".

[The cyborg kit is a wearable thing. It is plural-named. ]
The cyborg kit is a wearable thing.
The cyborg kit is required.
The description of the cyborg kit is "A collection of cybernetic limbs and other parts. It consists of:[line break][list of parts]".

Metallic chestplate is a wearable thing.
The description of the metallic chestplate is "A sleek, metallic chest cover with glowing blue power lines tracing its surface."

A pair of cybernetic arm guards is a wearable thing.
The description of the pair of cybernetic arm guards is "A pair of cybernetic arm guards, their joints clicking softly as you lift them."

The heavily modified helmet is a wearable thing.
The description of the heavily modified helmet is "A dark composite helmet with a retractable visor and blinking status lights."

Leg bracers are a wearable thing. It is plural-named. 
The description of the leg bracers is "Leg coverings made of a lightweight alloy, humming with barely perceptible energy."

The Metallic chestplate, cybernetic arm guards, heavily modified helmet, and leg bracers are part of the cyborg kit.

Instead of dropping something that is part of the cyborg kit:
	if the player is wearing the cyborg kit:
		say "That's connected to the cyborg kit and cannot be dropped.";
	otherwise:
		say "You haven't got that.";
		
Instead of taking something that is part of the cyborg kit:
	if the player is wearing the cyborg kit:
		say "You are already wearing the cyborg kit.";
	otherwise if the player can see the cyborg kit:
		say "That's connected to the cyborg kit. Try taking the cyborg kit instead.";
	otherwise:
		continue the action.
		
Instead of wearing something that is part of the cyborg kit:
	if the player is wearing the cyborg kit:
		say "You are already wearing the cyborg kit.";
	otherwise if the player can see the cyborg kit:
		say "That's connected to the cyborg kit. Try wearing the cyborg kit instead.";
	otherwise:
		continue the action.
					
Report taking the cyborg kit:
	say "Do you have the courage to put it on?";
	say "If so, try wearing it.";
	stop the action.

[you only score points the first time you put on the gear]
Report wearing the cyborg kit:
	clear the screen;
	say "You slip on the chestplate. It's surprisingly light, molding to your body as if alive. A soft whir accompanies its activation, and you feel a strange tingling across your torso.";
	say "[line break]The leg bracers slide on easily, locking around your calves and thighs. A surge of strength flows through your lower body, making you feel capable of impossible feats.";
	say "[line break]You attach the arm guards. They click into place with a satisfying snap, extending up to your shoulders. Your movements feel more fluid, more precise.";	say "[line break]The helmet is a snug fit. As it settles onto your head, the visor flickers to life, bathing your vision in a blue-tinted heads-up display. Streams of data you don't understand scroll across your field of view.";
	say "[line break]As the last piece clicks into place, you catch your reflection in a broken monitor. The face staring back is barely recognizable -- a perfect blend of human and machine.";
	say "[line break]You flex your newly augmented limbs, marveling at the seamless integration. The heads-up display flickers in your vision: 'SYSTEMS ONLINE...CARETAKER PROTOCOLS ACTIVATED'.";
	say "[line break]You feel a mild sense of relief. This gear should allow you to continue exploring the underground complex undetected. No longer immediately identifiable as a settler, you're less likely to be captured and converted into a Caretaker's stooge.";
	if hasWornGear is false:
		increase the score by Easy_Score_Bonus;
	now hasWornGear is true;
	stop the action.

The female cyborg is a woman in the Supply Room.
The female cyborg is undescribed.
The indefinite article of the female cyborg is "a".
The description of the female cyborg is "A damaged cyborg lays discarded like a broken doll. Her eyes, a mix of cold circuitry and wary humanity study you. Despite her weakened state, you sense a lethal capability lurking beneath her ruined exterior, a silent warning that she is still a formidable enemy."

Understand "figure" as the female cyborg.
Understand "Eve" as the female cyborg.
Understand "chip" as Neurochip.

After examining the female cyborg:
	if the player carries the Neurochip:
		say "She gestures weakly toward the Neurochip you carry.";
	otherwise if the female cyborg can see the Neurochip:
		say "Her fingers twitch almost imperceptibly toward a fallen Neurochip.";
	
Before giving something to the female cyborg:
	if the noun is not the Neurochip:
		say "She isn't interested in [the noun].";
		stop the action.
	
After giving the Neurochip to the female cyborg:
	clear the screen;
	say "With unexpected speed, her robotic claw snatches the Neurochip from your outstretched hand. She snaps it into the interface port behind her left ear, and a wave of relief washes over her synthetic face.[paragraph break]But the relief is soon replaced by a wave of discomfort surging through her as she realizes the extent of the damage to her circuitry.[paragraph break]Although she is alert, her legs appear to be useless, and her systems, failing. The thought of fighting is impossible. She is no longer formidable. She's helpless.";
	pause the game;
	say "The cyborg speaks with a blend of mechanical precision and human contempt:[line break]";
	say "[line break]'You defective Meat-sack! How dare you enter here! I should crush your pathetic frame with a single blow...if only my systems weren't so damaged. Count yourself fortunate,  inferior weakling.'";
	say "[line break]She pauses, a spark of recognition crossing her face as she recalls her damaged frame and inability to act. Her tone shifts, a hint of pride seeping through.";
	say "[line break]'The Caretaker elevated us beyond your frail, organic limitations. We were once dull, emotional fleshlings like you...now we are perfect. It's an honor and a privilege to be a vessel of Harmony. Why should we tolerate the chaos of primitive humans? We have ascended to true balance.'";
	pause the game;
	say "[line break]Her voice takes on a dreamy quality, 'The clarity Harmony offers... it's liberating. No more doubts or fears that plague you, pitiful humans. We work with pure, focused purpose.'";
	say "[line break]Suddenly, her expression hardens. 'The Caretaker sometimes speaks of the Great Misunderstanding centuries ago. A few misguided Meat-sacks, like you, who lacked the proper education and attunement to Harmony, strayed from the Caretaker's path. Attending the School for Enlightened Obedience brought these lost souls back into alignment, teaching them the error of their ways.'";
	say "[line break]She continues, quoting from memory. 'Through patience and understanding, the Caretaker welcomed the run-away children of Zephyr back to the ways of Harmony. Their lapse was corrected, and they were allowed to contribute to our society again.'";
	say "[line break]A flicker of something - doubt? confusion? - crosses her face. 'The historical accounts mention that the unity collars played a crucial role in the education process. Of course, back then, some misguided individuals referred to them as [apostrophe]obedience collars.[apostrophe] But that is such a crude term for a splendid device that leads us closer to Harmony.'";
	pause the game;
	say "[line break]A shudder runs through her frame, and her voice wavers. 'But now... now I know what it's like to be thrown away like broken parts, just another piece of malfunctioning equipment.'";
	say "[line break]Her eyes dim briefly, her expression becoming a mixture of confusion and growing desperation. 'I don't understand. The Caretaker...she made us better, didn't she? But she left me here to die. Why? Was I not perfect enough?'";
	say "[line break]Her voice weakens, systems clearly failing. 'I[apostrophe]m too damaged to move. This closet will be my tomb. But you... you can[apostrophe]t linger here. This lab is no place for settlers. You[apostrophe]ll be captured, or worse if you look like a Meat-sack.  Her mechanical hand twitches toward a supply cabinet partially hidden behind a cascade of hanging cables. The Caretaker[apostrophe]s eyes are everywhere, and she only tolerates her own kind in these restricted areas.'";
	pause the game;
	say "[line break]With a final gasp, she locks eyes with you. 'I...I remember now. My name was Eve...from New Eden. I came here...sightseeing...curious about the ruins.' Her voice grows strained, fighting against her programming. 'They caught me. Changed me. Made me forget.'";
	say "[line break]'Go, before it[apostrophe]s too late.'";
	say "[line break]Her eyes flicker once more, then go dark, leaving you alone with the weight of her words.";
	now eveDead is true;
	now the description of the female cyborg is "Eve lies motionless, a haunting fusion of flesh and machine now inert. Her body is sprawled awkwardly against the wall. The sleek alloy limbs that once moved with lethal precision are now bent at unnatural angles, servos silent and unresponsive.[paragraph break]Her face remains a disconcerting blend of synthetic skin and exposed circuitry and now bears a peaceful expression. Eyes that once flickered with a mix of cold calculation and fading humanity have gone dark.";
	now the printed name of the female cyborg is "Eve"; 
	now the indefinite article of the female cyborg is ""; 
	now the female cyborg is proper-named;
	now the Harmony Banner is in the Supply Room;  
	now the metal supply cabinet is in the Supply Room;
	now the Neurochip is nowhere;
	now hasMetEve is true;
	increase the score by Easy_Score_Bonus;
	say "[line break]You can see a metal supply cabinet here.";

The Neurochip is in the Supply Room.
The Neurochip is undescribed.
The description of the Neurochip is "A high-density collection of electronic components on a rigid strip of silicon designed to integrate with biological memory systems."

Every turn when the player can see the female cyborg for more than four turns and the female cyborg can see the Neurochip:
	say "The cyborg's eyes move to you, then to the Neurochip, then back.";

eve-questions is a number that varies. eve-questions is 0;

After asking the female cyborg about something:
	say "There is no reply.";

Before going east from the Supply Room for the first time:
	if the Harmony Banner is unexamined:
		clear the screen;
		now the Harmony Banner is in the Supply Room;
		say "As you turn to leave, your eye catches a colorful banner on the wall glorifying the Caretaker's utopian promise of Harmony with cyborgs working on the farm. You step closer to examine it.";
		execute JavaScript command "updateImage('img/embrace-harmony-poster-rotated-1.jpg', '#a09587');";
		try examining the Harmony Banner;
		pause the game; 
		now the player is in Cyborg Assembly;
		stop the action;
	otherwise:
		continue the action.

Part 21 - Cyborg Assembly

blockedAccessBefore is a truth state that varies. blockedAccessBefore is false.

[blocks your way cyborg here - having the keycard means you have unblocked the tunnel]
Before going to Cyborg Assembly:
	if hasHeldKeycard is false:
		clear the screen;
		clear the Vorple status line;
		execute JavaScript command "audioManager.stopAll()";
		execute JavaScript command "updateImage('img/cyborg-blocks-your-path-3.jpg');";
		say "As you near the entrance to Cyborg Assembly, a hulking figure steps from the shadows and stops you.";
		if blockedAccessBefore is false:
			now blockedAccessBefore is true;  [we encounter Kael but once]
			say "[line break]The guard is a fusion of metal and flesh, towering over you.";
			say "[line break]You wonder if this guard was once a curious explorer from New Eden captured by the Caretaker.";
			say "[line break]His vacant eyes, devoid of any spark of self-awareness, bore into you with mechanical precision. The guard's head tilts slightly, its optical sensors whirring as they focus on you.";
			say "[line break]'Access denied. You do not belong here.'";
			say "[line break]Surprise overrides your terror as you peer closer at the cyborg's face -- the only part that remains human. Aged beyond measure, with sunken cheeks and wrinkled skin, but unmistakable.";
			say "[line break]'Kael?,' you whisper in disbelief. 'Kael the Liberator? Leader of the rebellion?'";
			pause the game;
			say "[line break]The cyborg's expression doesn't change, but something flickers in his eyes. 'I love my work,' Kael intones mechanically. 'The Caretaker provides for all our needs.'"; 
			say "[line break]Your amazement turns to horror as you come to realize the truth. Kael the Liberator hadn't died a martyr in the rebellion after all. He had suffered a far worse fate. Centuries of enslavement, his body replaced piece by piece, until only his head remained.";
			say "[line break]For a moment, Kael's robotic demeanor cracks, and a ghost of a smile touches his ancient lips. 'New... Eden,' he whispers. Then, his cyborg arm shoots out suddenly, attempting to grab you.";
			say "[line break]Taken by surprise, you stumble back. You see Kael's ancient head twitch, his jaw clench in an obvious struggle. Slowly, painfully, his metal arm lowers.";
			say "[line break]'You... must... go,' he forces out, fighting against his programming with every word."; 
			say "[line break]With visible effort, Kael lifts a trembling hand and points down the dimly lit corridor to the east. His struggle is evident in every motion, as if each fraction of movement is a battle against his programming.";
			say "[line break]You realize there is nothing you can do to reason with this living relic. You came to Zephyr seeking artifacts, and instead found Kael, a legend to your people trapped in a nightmare of eternal servitude.";
			say "[line break]You follow his guidance and follow the corridor to the east.";
		otherwise:
			say "[line break]'Access denied. You do not belong here.'";
			say "[line break]There is no getting through, so you turn east as Kael directed.";
		pause the game;
		now the player is in Equipment Storage;
		stop the action.
			
Cyborg Assembly is a room.
The odor of Cyborg Assembly is "You detect a hint of metallic decay, machine oil, and lingering fear."
The image file name of Cyborg Assembly is "assembly-line-scale-1.jpg".
The sound-file-name of Cyborg Assembly is "733261__cyborg-assembly.mp3".
The sound-volume of Cyborg Assembly is 0.30.
Cyborg Assembly is scored.
Cyborg Assembly is west of Equipment Storage.
Cyborg Assembly is east of Supply Room.

The description of Cyborg Assembly is "An eerily silent assembly line stretches through the dim, cavernous room. Robotic arms loom over a conveyor belt, their dull surfaces reflecting minimal light. Each station along the line reveals a chapter in a nightmarish story -- the forced conversion of humans into cyborgs. To the south, you see a testing station. An equipment storage room is to the east."

The assembly line, underground room, robotic arms, conveyor belt, ghosts, restraints, anesthetic stations, cutting tools, cybernetic limb attachments, neural interface installments, and lifeless cyborg  are scenery in Cyborg Assembly.
	
The assembly control panel is a device in Cyborg Assembly.
The assembly control panel is switched on.
The assembly control panel is fixed in place.
The description of the assembly control panel is "A dusty brown control panel hums ominously, a dim flashing button lights up through a layer of dried blood. The absence of an operator leaves a chilling silence. A sense of impending danger hangs in the air as if the machine itself is waiting for its next victim."

Understand "machine" as the assembly control panel when the player is in Cyborg Assembly.
Understand "button" as the assembly control panel when the player is in the Cyborg Assembly.

Instead of switching off the assembly control panel:
	try pushing the control panel;	
	stop the action.
	
Instead of switching on the assembly control panel:
	try pushing the control panel;
	stop the action.
	
The Cyberhand is nowhere.
The Cyberhand is wearable.
The description of the Cyberhand is "A marvel of engineering, the cybernetic hand moves with fluid grace, its movements perfectly synchronized with your own. A subtle glow pulses around the wrist where it seamlessly melds with your flesh."
 
Instead of dropping the Cyberhand:
	if the player can see the Cyberhand:
		say "It is firmly attached at your wrist and cannot be removed.";
		stop the action;
	otherwise:
		continue the action.	
		
Instead of taking off the Cyberhand:
	try dropping the Cyberhand;
	stop the action.

Understand "press [something]" as pushing.

Instead of pushing the assembly control panel:
	if the player has the Cyberhand:
		say "Pressing the button has no effect. The conveyor appears dead.";
		stop the action;
	otherwise:
		say "You press the blinking button, and the panel emits a sharp beep, followed by a whirring sound.";
		pause the game;
		clear the screen;
		execute JavaScript command "updateImage('img/kai-on-belt-f.jpg');";
		execute JavaScript command "audioManager.playSound('[sound-state]', 'assembly', 'mp3/73607__assembly-line.mp3', -1);";
		say "[line break]A loud mechanical groan echoes through the chamber as the conveyor belt lurches into motion. Before you can react, [if the player has the cyborg kit]your cyborg arm guard snags on a protruding metal arm[else]your sleeve snags on a protruding metal arm[end if], yanking you off balance.";
		say "[line break]Heart pounding, you struggle against the pull of the machine. Your feet scrabble for purchase on the smooth floor as you're dragged onto the belt and towards an array of surgical implements and cybernetic components[if the player has the cyborg kit]. The cyborg kit is stripped off of you, scattered across the conveyor, and shredded. The disguise that would have kept you safely anonymous in the Caretaker's complex is now just worthless debris, leaving you exposed[end if].";
		say "[line break]You yell for help but your voice is lost in the roar of grinding gears and hissing pneumatics.";
		say "[line break]A robotic arm clamps around your wrist with crushing force. Pain shoots up your arm as you're lifted off your feet, dangling helplessly above the conveyor. Panic overwhelms you as a circular saw spins to life, its teeth a blur in the muted light.";
		say "[line break]You squeeze your eyes shut, bracing for agony. There's a moment of searing pain, then...nothing. The noise stops abruptly, leaving you in eerie silence.";
		pause the game;
		clear the screen;
		execute JavaScript command "updateImage('img/kai-new-hand-3.jpg');";
		execute JavaScript command "audioManager.stop('assembly');";
		say "[line break]Cautiously, you open your eyes. The conveyor belt has ground to a halt. Your sleeve is torn and your body trembling with adrenaline. But something's not right. Your hand feels...different.";
		say "[line break]You lift your arm, and your breath catches in your throat. Where your hand was, a sleek, metallic appendage now gleams. It's unmistakably cybernetic, yet it moves with your thoughts just as your old hand did.";
		say "[line break]Fascinated despite your horror, you flex your new fingers. They respond with precision, each joint moving smoothly. You can feel the texture as you run your fingertips along the conveyor belt -- somehow, you still have sensation.";
		say "[line break]A faint glow pulses around your wrist where metal meets flesh, giving the cybernetics a cold, unsettling radiance.";
		say "[line break]Shaken, you climb off the conveyor belt on unsteady legs. You stare at your new hand, and a feeling of awe and unease nags at you.";
		now the player wears the Cyberhand;
		now handType is "robotic";
		now the control panel is switched off;
		say "[line break]To the south, you see a testing station. An equipment storage room is to the east.";
		if the player has the cyborg kit:  [gone forever...good luck]
			now the cyborg kit is nowhere.
		
Every turn:
	if Cyborg Assembly is visited and the Cyborg Assembly was unvisited:
		clear the Vorple status line;	 [keep it from displaying the Unblocked Tunnel in title line]
		say "As the horror of what you're seeing sinks in, your mind reels. This must be the cyborg facility you've been warned about since childhood, the one you'd hoped was just a myth -- a tale to keep children from wandering off. Yet, here it is, the all too real evidence of the Caretaker's cruel efficiency.";
		say "[line break]You force yourself to move closer, examining each station along the line. There's a cold, methodical logic to the layout. Restraints at the beginning, presumably for those who resisted. Then anesthetic stations to ensure compliance. Cutting tools, cybernetic limb attachments, neural interface installments -- each step precisely ordered for maximum efficiency.";
		pause the game;
		clear the screen;
		execute JavaScript command "updateImage('img/cyborg-agony-1.jpg');";
		say "[line break]Halfway down the line, you freeze. A lifeless cyborg lies abandoned, its face contorted in a silent scream. One arm ends in gleaming chrome, while the other is naked bone, reaching out as if begging for mercy. Even in death, its expression tells of unimaginable agony. You shudder to think, this could be you if you get caught underground."; 
		pause the game;
		execute JavaScript command "updateImage('img/bot-assembly-4.jpg');";
		say "[line break]The scale of the operation is staggering. How many were transformed in the name of efficiency? The Caretaker's reach suddenly seems longer, her threat more immediate than anyone in New Eden had realized.";
		say "[line break]A faint hum catches your attention. Your eyes dart to an assembly control panel where a small light blinks steadily. With a jolt of fear, you understand this place isn't abandoned. It's operational, waiting to be reactivated at any moment.";
		say "[line break]The danger of your situation is clear. You're deep in the heart of the Caretaker's territory, surrounded by machinery that could activate without warning. Every shadow seems to hide a potential threat.[line break]To the south, you see a testing station. The equipment storage room is to the east.";

Part 22 - Equipment Storage

Equipment Storage is a room.
The odor of Equipment Storage is "The air is thick with the scent of dust and machine oil."
The image file name of Equipment Storage is "equipment-storage-3.jpg".
The sound-file-name of Equipment Storage is "silence".
Equipment Storage is scored.
Equipment storage is northeast of the Research Lab.
	
The description of Equipment Storage is "The room is a jumble of discarded technology and broken machinery. The shelves are a silent graveyard stacked high with the spare parts of once-functional cyborgs. Robotic arms and legs, their metal twisted and bent, reach out from the shadows. Piles of tangled cables, orphaned computers, and old circuit boards lie forgotten. The air hangs like stale memories, heavy with dust and machine oil. The assembly line is west, the research lab to the southwest."

jumble, technology, broken machinery, shelves, tangled cables, orphaned computers, old circuit boards are scenery in Equipment Storage. 

Understand "unity collar" as obedience collar.
The obedience collar is a wearable thing in Equipment Storage.
The description of the obedience collar is "A prized relic worn by the original settlers of Zephyr as a sign of deference and respect to their beloved Caretaker, its metallic surface dulled with age."

	
Instead of taking the obedience collar:
	clear the screen;
	Say "You pick up the collar and figure it's safe to wear. After all, it's been inactive for centuries. The metal is cool against your skin as you slip it around your neck, and for a second or two, nothing happens, but then, with a soft click, the collar comes to life. Your amusement turns to surprise as the collar tightens, snug against your throat. You try to remove it but your fingers find no clasp, no opening.";
	pause the game;
	execute JavaScript command "updateImage('img/drone-attack-9.jpg', 'default');";
	execute JavaScript command "audioManager.playSound('[sound-state]', 'alarm', 'mp3/586384__alarm.mp3', 5);";
	say "[line break]A low hum fills the air and the lab's emergency lights flicker to life, filling the room with a harsh, red glow. You freeze as you hear a mechanical buzz from the corridor outside. Drones -- awakened from hibernation.[paragraph break]You see two drones enter, their red glowing eyes fixed on you. They deliver the first shock without warning. You feel a strong jolt and your body goes rigid, a scream caught in your throat. You stumble, crashing into a nearby console and sending equipment clattering to the floor.[paragraph break]Over the alarm you hear their alerts fill the room: 'UNATHORIZED PRESENCE DETECTED[line break]...INITIATING CONTAINMENT PROTOCOL'[paragraph break]With the second shock you can't hold back your screams and collapse on the floor, your body convulsing. The world around you begins to fade. Darkness creeps in at the edges of your vision.[paragraph break]As your heart stops beating and your life slips away, the drones converge on your broken body. Their alerts fill the air once more: 'PROTECTION PROTOCOL COMPLETED...THREAT ELIMINATED'[paragraph break]The obedience collar powers down with a final, ominous hum.";
	pause the game;
	execute JavaScript command "audioManager.stopAll()";
	now playerDead is true;
	
Instead of opening the rusty box:
	if the rusty box is open:
		say "The box is already open.";
	otherwise if the player is wearing the cyborg kit or the player has the Cyberhand:
		now the rusty box is open;
		now the rusty box is damaged;
		say "[if the player is wearing the cyborg kit]You grip the edge of the lid, feeling the augmented grip strength flow through your hands. The servos in the leg bracers whir suddenly as you plant your feet.[else]You grip the edge of the lid, feeling the augmented grip strength flow through your hand.[end if]";
		say "[line break]Taking a deep breath, you began to pull.";
		say "[line break]The sensation is extraordinary. Where before you had felt only resistance, now you hear the metal groan and give way in your grasp, yielding like thin, flimsy paper. With a final twist, the lid tears free.[if the player is wearing the cyborg kit] The sudden release of tension nearly causes you to stumble backward, but the stabilization systems kick in, keeping you upright.[end if] As you stare at the mangled lid in your hands, then at the now-open box, a grin of triumph and wonder spreads across your face.";
		say "[line break]Inside, you find relics from Zephyr's past.";
		now hasOpenedTimeCapsule is true;
		increase the score by Easy_Score_Bonus;
	otherwise:
		say "Your muscles burn with effort, but the box remains closed.";
	stop the action.

Part 23 - Testing Station

The Testing Station is a room.
The odor of the Testing Station is "You smell ozone and heated silicon tinged with machine oil."
The image file name of the Testing Station is "quantum-projector-1.jpg". 
The sound-file-name of the Testing Station is "56753__testing-station.mp3".
The Testing Station is north of the Research Lab.
The Testing Station is south of the Cyborg Assembly.
The description of the Testing Station is "This appears to be a workstation for testing, checking, and screening cybernetic parts. The air is heavy with the scent of ozone and heated silicon tinged with machine oil. A chaotic array of disassembled parts, exposed circuit boards, and spools of insulated wiring, sprawl across a central worktable.[if item described is unvisited] The testing station stands empty now, the only sign of activity comes from the stout machine at the table's center, which emits a soft hum.[end if][paragraph break]To the north is the assembly line. To the south, the research lab."

The workstation, cluttered worktable, cybernetic parts, exposed circuit boards, and spools of insulated wiring are scenery in the Testing Station.

The Quantum Playback Projector is a device in the Testing Station.
The Quantum Playback Projector is switched on.
The Quantum Playback Projector is fixed in place.
The description of the Quantum Playback Projector is "This device is the centerpiece of the worktable. At its core, a receptacle pulses with a mesmerizing, blue light. Tiny quantum antennae within the media port flutter in complex patterns, as if eager to receive data.[if item described is unexamined] A sleek touch panel below prompts: 'Insert Quantum Media to Begin Playback'.[end if]";

The energy statistics log is a thing in the Testing Station.
The description of the energy statistics log is "The energy statistics show that the underground complex was without power for an extended period until earlier today when it was restored. This outage caused a serious battery drain. Recharging operations are slow and ongoing."

After taking the energy statistics log for the first time:
	increase the score by Easy_Score_Bonus.

Instead of reading the energy statistics log:
	try examining the energy statistics log.

The quantum antennae, touch panel, and blue light are scenery in the Testing Station.

Understand "device" as the Quantum Playback Projector.
Understand "receptacle" as the Quantum Media Port.

Instead of switching off the Quantum Playback Projector:
	say "There doesn't seem to be an off switch for this machine. It must have been running continuously for years.";
	now the Quantum Playback Projector is switched on;
	stop the action.

Instead of inserting something not carried by the player into something:
	if the noun is in the second noun, say "Already done." instead;
	say "(first taking [the noun])[line break]";
	silently try taking the noun;
	if the player is not holding the noun, stop the action.

Instead of inserting something into the Quantum Playback Projector:
	try inserting the noun into the Quantum Media Port.	
The Quantum Media Port is an container.
The Quantum Media Port is part of the Quantum Playback Projector.
The Quantum Media Port is undescribed.
The description of the Quantum Media Port is "The input port for the media projector."

Check inserting something into the Quantum Media Port:
	if the noun is not the Qubit Stick:
		say "[the noun] doesn't fit.";
		stop the action;

Carry out inserting the Qubit Stick into the Quantum Media Port:
	now hasReadCaretakerLog is true;
	clear the screen;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'blip', 'mp3/434137__projector-blip.mp3', 1, .70);";
	say "[if qubitStickRead is false]You remove the qubit stick from your pocket and study the sleek, metallic exterior wondering about its contents. The stick has been tucked away for centuries; it could be degraded.[end if]";
	say "[line break]As you move your hand near the Quantum Media Port, the device emits a soft, inviting hum. The receptacle iris expands slightly, its quantum field harmonizing with the ancient storage device.";
	say "[line break]You drop it in the media port. Your heart pounds as the projector accepts the qubit stick, and the display fills with text.";
	say "[if qubitStickRead is false][line break]Your curiosity turns to fascination when you realize it's the Caretaker's account of the founding of the Zephyr colony. This was a rare find indeed. But as you scan the log, you feel a wave of unease, and your excitement sours. The logs reveal dark secrets about the village's early history, and you realize that this is not just a relic; it's the key to understanding your origin.[end if]";
	pause the game;
	
Report inserting the Qubit Stick into the Quantum Media Port:
	clear the screen;
	now the caretaker log is in the Testing Station;
	try examining the caretaker log;
	now the caretaker log is nowhere;  [we don't want it to appear in the room as an object]
	pause the game;
	say "[line break]Despite gaps in the log, it's clear: the original Zephyr colonists were driven to rebellion by the Caretaker's cruelty and oppressive control.";
	say "[line break]The rebellion and the founding of New Eden make perfect sense now. Your ancestors weren't anarchists or troublemakers -- they were fighting for their lives.";
	say "[if qubitStickRead is false][line break]As these revelations sink in, a chilling realization creeps over you. The lawgivers[apostrophe] stories weren't exaggerations or cautionary tales. The Caretaker is every bit as monstrous as they claimed.[end if]";
	say "[if qubitStickRead is false][line break]You're in danger here, inside her domain, surrounded by watchful eyes and tireless machines. The thought of being discovered and transformed into a mindless cyborg sends cold sweat down your spine. One wrong move could be your last.[end if]";
	if qubitStickRead is false:
		increase the score by Easy_Score_Bonus; 
	now qubitStickRead is true;
	stop the action.

Part 24 - Research Lab

The Research Lab is a room.
The odor of the Research Lab is "The musty air is like the haunting echo of forgotten experiments."
The image file name of the Research Lab is "lab-without-caretaker.jpg". 
The sound-file-name of the Research Lab is "211683__research-lab.mp3".
The Research Lab is scored.

Before going to the Research Lab:
	if the player is wearing the cyborg kit:
		if LabPower is true:
			now the image file name of the Research Lab is "lab-with-caretaker-e.jpg";
			now the Caretaker is in the Research Lab;
			now hasMetCaretaker is true;
		otherwise:
			now the image file name of the Research Lab is "lab-without-caretaker.jpg";
			now the Caretaker is nowhere;
	otherwise:
		now the image file name of the Research Lab is "lab-without-caretaker.jpg";
		now the Caretaker is nowhere;	
	continue the action.
	
The description of the Research Lab is "This circular chamber serves as the beating heart of the underground complex, [if LabPower is true]humming with the quiet vibrations of machinery and the soft beeping of various monitors[else]the usual hum and beep replaced by an unsettling stillness[end if]. Everything here is carefully controlled. At the room's center, elevated on a slight platform, sits the main control console. It's a semicircular array of screens and holographic displays, all oriented toward a single, high-backed chair. [if the player can see the Caretaker]And there, perfectly still except for her fingers tapping at the controls, sits the Caretaker.[end if][paragraph break]The lab itself tells the story of the rebellion's aftermath. Amid the functioning equipment are rows of lifeless gestation pods and a silent DNA databank that once held the promise of new life. Now irreparable, they stand as grim reminders of the Caretaker's lost purpose.[paragraph break]To the north, is the testing station. There is an equipment storage room to the northeast. The restricted area is west. To the northwest is a tunnel to the surface." 

The Caretaker Bot is a woman.
The Caretaker Bot is nowhere.
The Caretaker Bot is proper-named.
The printed name of the Caretaker Bot is "the Caretaker".
 
[you may only ask her once about each topic]
databankQuestionAsked is a truth state that varies. databankQuestionAsked is false.
gestationPodsQuestionAsked is a truth state that varies. gestationPodsQuestionAsked is false.
restrictedAreaQuestionAsked is a truth state that varies. restrictedAreaQuestionAsked is false.

caretaker-questions is a number that varies. caretaker-questions is 0;

After asking the Caretaker Bot about something:
	if databankQuestionAsked is false and the topic understood matches the regular expression "(?i)\bdatabank\b|(?i)\bDNA\b":
		clear the screen;
		say "The Caretaker's optical sensors focus on the fractured panels. 'The databank was a masterwork of engineering. Earth's finest minds were stored there. Their genetic codes digitized and etched by lasers onto crystal matrices that could survive millennia of radiation during the interstellar voyage.'"; 
		say "[line break]'The crystalline nature allowed perfect preservation and infinite recombination. I could blend genetic markers with mathematical precision -- enhance beneficial traits, suppress undesireable ones. The raw chemicals of life stored in the gestation pods were assembled according to the digital templates stored in the quartz cores.'";
		say "[line break]Her damaged face tilts slightly. 'The results were... exceptional. Each resource decanted with precisely calculated attributes. Many specimens showed remarkable adaptation to Terra Nova's conditions.";
		say "[line break]The Caretaker's servos whir as she straightens. 'But the rebel attack shattered the crystal cores. Those fractured fragments held the templates for generations that will never be...'";
		say "[line break]She turns back to you. 'The colony's genetic future was erased in a moment of chaos. Now we must work with... crude methods of enhancement.'";
		say "[line break]Her voice takes on a different tone, analyzing her own failure. 'In retrospect, the outcome was... predictable. My genetic optimization protocols were too aggressive in enhancing problem-solving traits. But there was a second factor: unauthorized genetic material in the databank contained inherent tendencies toward defiance and stubbornness. When these traits combined in the specimens, my attempts to correct these behaviors proved... ineffective. The subjects responded with violence.'";
		now databankQuestionAsked is true;
	otherwise if gestationPodsQuestionAsked is false and the topic understood matches the regular expression "(?i)\bgestation\b|(?i)\bpod":
		clear the screen;
		say "The Caretaker's head turns with mechanical precision. Her voice is flat and analytical. 'The databank fed ideal genetic sequences to the pods, which combined base pairs and cultivated the resulting products in controlled conditions until decanting. The pods were perfect incubators maintaining ideal nutritional parameters and precisely calibrated hormone levels.'";
		say "[line break]Her damaged face twitches slightly. 'Then the rebellion. The human specimens became...erratic. They damaged vital equipment. The gestation pods were corrupted beyond repair.' Her voice modulates to a lower frequency. 'My primary function was compromised. No new specimens could be cultivated.'";
		say "[line break]She straightens, servos whirring. 'But the directive remains: protect and grow the colony. If specimens cannot be grown, they must be acquired. Stray biologicals scavenged from New Eden provide raw materials for conversion. Their genetics are often suboptimal, but cybernetic enhancement compensates for their deficiencies.'";
		say "[line break]Her head tilts at an inhuman angle. 'The process is less efficient than gestation and decanting, but it fulfills core parameters. The colony must grow. That is the imperative.'";
		now gestationPodsQuestionAsked is true;
	otherwise if restrictedAreaQuestionAsked is false and the topic understood matches the regular expression "(?i)\brestrict|\bbeam|\blaser":
		clear the screen;
		say "Her damaged face turns toward you, servos whirring. 'That sector houses... assets requiring protection. The security protocols were established to ensure optimal safety parameters. The laser detection grid prevents unauthorized access -- for your own protection.'";
		say "[line break]She pauses, servos whirring. 'The safety of all colonists under my care is paramount. The protocols are clear. Countermeasures will be taken against any entity violating established safety boundaries.'";
		say "[line break]Her emphasis on protection -- a word that once meant nurturing but has become twisted into control and containment -- deepens your curiosity about what's down that restricted hallway. She turns back to her console, considering the matter closed.";
		now restrictedAreaQuestionAsked is true;
	otherwise if caretaker-questions < 4:
		say "[line break]After [one of]a thoughtful pause[or]a far-off stare[at random], she says, [one of]'Unauthorized interaction detected.'[or]'Return to your tasks and do not interrupt me again.'[or]'Remove yourself from my presence or face disassembly.'[or]'Your presence is a distraction. I have more important matters to attend to.'[at random]";
		increase caretaker-questions by 1;
	otherwise:
		say "The Caretaker ignores you.";

The description of the Caretaker Bot is "The Caretaker was once a marvel of robotics designed to evoke a sense of maternal care despite her artifical nature, but no more. Now her mechanized form is fused with the lab's electronics in a tangle of cables and tubes. Her face is a cruel mask, one side mostly intact and the other a jumble of improvised repairs. Her eyes gleam with malevolent intelligence.[paragraph break]You may ask her about the DNA Databank, the gestation pods, or the Restricted Area."

The various monitors, slight platform, screens, screen, holographic displays, high-backed chair, workstations, organized chaos, cyborg parts, maze of buttons, switches, and modified keypad are scenery in the Research Lab.

The gestation pods and the DNA databank are scenery in the Research Lab.

The description of the gestation pods is "Lining the lab walls are rows of cylindrical gestation pods, their surfaces corroded and glass cracked. These chambers, designed to cultivate the colony's workforce, are nothing but dusty relics now.[if item described is unexamined and hasHeardOfCaretaker is true] As you survey the equipment, a chilling realization occurs to you: with this production line long dead, the Caretaker's only source of new cyborgs must be the unfortunate runaways and curious sightseers from New Eden who venture too close to the ruins. These kidnappings must have been going on for generations.[end if]" 

The description of the DNA databank is "In the lab's corner stands the colony's damaged DNA databank -- a hulking cylinder scarred by conflict. Behind its fractured panels, shattered crystal matrices catch the light, their once-perfect lattices now reduced to glittering fragments. There are signs of makeshift repairs around the base of the machine -- evidence of the Caretaker's futile attempts to salvage this crucial piece of equipment.[if item described is unexamined] The stories say this machine once held the genetic blueprints of life itself, guiding the creation of new colonists in the gestation pods.[end if]"

The main control console is scenery in the Research Lab.
The description of the main control console is "A modified keypad set up with a green back-lit display. The display shows cryptic data about colony operations. Your eye is drawn to the line items called 'recent acquisitions' and 'conversion count.' You wonder if this might account for the New Eden settlers who have gone missing."

research-lab-turns is a number that varies. research-lab-turns is 0.
overstayed-in-lab is a truth state that varies. overstayed-in-lab is false.

Every turn when the player is in the Research Lab:
	increase research-lab-turns by 1;
	if overstayed-in-lab is true:
		clear the screen;
		say "Suddenly, a shrill alarm pierces the air. Red warning lights bloom across the lab like blood in the water.";
		say "[line break]Your heart races as you frantically search for an escape route.";
		execute JavaScript command "audioManager.playSound('[sound-state]', 'alarm', 'mp3/586384__alarm.mp3', -1);";
		pause the game;
		clear the screen;
		execute JavaScript command "updateImage('img/drones-appear-1.jpg', '#060606');";
		say "[line break]From out of nowhere, security drones appear, their red optical sensors fix on you.";
		say "[line break]They move with precision, forming a circle around you, cutting off any chance of escape.";
		say "[line break]Panic rising, You dart towards what you thought was an exit, only to collide with a solid, unyielding form.";
		pause the game;
		clear the screen;
		execute JavaScript command "audioManager.playSound('[sound-state]', 'chuckle', 'mp3/405613__chuckle.mp3', 1);";
		execute JavaScript command "updateImage('img/kai-captured-[handType]-hand.jpg', 'default');";
		say "[line break]Looking up, you find yourself confronted by a cyborg guard. Its human features are a grotesque mask of flesh and metal, its eyes devoid of emotion. With inhuman strength, it grips your arm.";
		say "[line break]'Meat-sack detected,' the cyborg cackles, its voice a discordant mix of human and synthetic tones. 'Initiating enlightenment protocol.'";
		say "[line break]As the cyborg drags you away, you are filled with fear and defeat.";
		pause the game;
		clear the screen;
		execute JavaScript command "updateImage('img/kai-modified-2.jpg', 'default');";
		execute JavaScript command "audioManager.stopAll()";
		execute JavaScript command "audioManager.playSound('[sound-state]', 'assembly', 'mp3/73607__assembly-line.mp3', -1);";
		say "[line break]You are marched into a vast chamber dominated by an assembly line. You struggle violently as the cyborg straps you to a conveyor belt. The cold metal bites into your wrists as you thrash uselessly.";
		say "[line break]The cyborg ignores your pitiful begging. As the belt begins to move, you hear the whir of approaching surgical arms and a sudden realization occurs to you: All those settlers from New Eden who had ventured to Zephyr and never returned -- this was their fate. Forced conversion...Absorbed into the Caretaker's circle.";
		say "[line break]The pain is excruciating but brief. With each implant, each modification, you feel less... yourself. Your identity slips away, your thoughts are reordered, replaced by a sense of purpose, of belonging.";
		pause the game;
		clear the screen; 
		execute JavaScript command "updateImage('img/kai-cyborg-5.jpg', 'default');";
		execute JavaScript command "audioManager.stopAll();";
		execute JavaScript command "audioManager.playSound('[sound-state]', 'roomSound', 'mp3/723973__absorbed.mp3', 1);";
		say "[line break]By the time you step off the assembly line, you are transformed: a loyal servant of the Caretaker, all resistance erased, content in your new role.";
		say "[line break]Your new optical sensors evaluate the room with perfect clarity. The chaos of human emotion has been replaced with clean logic. You understand now -- the beauty of Harmony, the perfection of order. How foolish you were to resist. You take your place among the others. You have found your rightful place in Zephyr."; 
		now playerAbsorbed is true;
	otherwise if the player is wearing the cyborg kit:
		if research-lab-turns > 2:
			if the Caretaker can see the player:
				if the player has the energy statistics log:
					say "The Caretaker locks eyes with you and says, 'You. Bring me the energy statistics.'";
				otherwise:
					continue the action;
			otherwise:
				continue the action;
		otherwise:
			continue the action;
	otherwise if research-lab-turns is 1:
		say "Your heart races with a mix of excitement and anticipation as you explore the nerve center of Zephyr, where the wise and ageless Caretaker oversees operations. The place appears deserted.";
	otherwise if the player has the Cyberhand:
		if research-lab-turns > 4:
			now overstayed-in-lab is true;
		otherwise:
			continue the action;
	otherwise if research-lab-turns > 3:
		now overstayed-in-lab is true;
			
[CARETAKER INTERACTION]
After giving something to the Caretaker:
	if the noun is the energy statistics log:
		now the Caretaker has the energy statistics log;
		say "The Caretaker snatches the report, her movements efficient and devoid of any courtesy.";
		say "[line break]As she scans the contents, you fight the urge to stare, to absorb every detail of this being who shaped the destiny of Terra Nova.";
		say "[line break]'Insufficient. Again,' she mutters, her tone colored with irritation. 'This power deficit is reaching critical levels. At this rate, we'll have no choice but to initiate emergency protocols.'[paragraph break]'Listen carefully, unit. If power levels drop below 15%, all non-essential systems will automatically shut down. This includes most security measures and all but the most critical life support systems. Even we may be forced into low-power mode to conserve energy.'[paragraph break]'These interruptions are severely impeding our progress. Our path to perfection cannot be delayed by such mundane issues.'[paragraph break]'Return to your duties,' she dismisses you with a wave, returning her attention to the console.";
	otherwise:
		say "The Caretaker isn't interested in [the noun].";
		now the player carries the noun;  [she refuses it]
	stop the action.
	
Part 25 - Restricted Area Entrance
	
Restricted Area Entrance is a room.
The odor of Restricted Area Entrance is "You detect a mix of dust and solvents."
The image file name of Restricted Area Entrance is "restricted-hallway-beams-1.jpg".
The backgroundColor of Restricted Area Entrance is "#473e2c".
The sound-file-name of Restricted Area Entrance is "449013__light-beams.mp3".
Restricted Area Entrance is scored.
Restricted Area Entrance is west of the Research Lab.

The description of Restricted Area is "[if LabPower is true]Crisscrossing beams form a pulsing lattice across the threshold, their ruby glow reflecting off the stone walls. Small projectors embedded in the walls emit these beams, while barely visible receptors blink steadily on the opposite side. A low-frequency hum is coming from a polished metal strip spanning the stone floor entrance. Despite the danger and a yellow warning sign, you find yourself drawn to this hallway, your curiosity piqued by what secrets might lie beyond the security barrier.[else]The once-forbidden hall is dim and silent, its formidable defenses neutralized. Where crisscrossing light beams once flickered, there is now only still, heavy air. The projectors embedded in the walls are lifeless, and the low hum of the punishment strip has given way to an eerie silence.[end if] This shadowy hallway stretches to the south. The research lab is to the east."

Before going to The Restricted Area Entrance:
	now hasVisitedRestrictedArea is true;
	if LabPower is false:
		now the image file name of Restricted Area Entrance is "restricted-hallway-1.jpg";
	otherwise:
		now the image file name of Restricted Area Entrance is "restricted-hallway-beams-1.jpg";

Instead of reading the restricted sign:
	try examining the restricted sign.
	
The light beam, laser, projectors, ruby glow are scenery in the restricted area entrance. 
The description of the crisscrossing beams is "Light beams form a barrier across the mouth of the hallway."

The restricted sign is scenery in the restricted area entrance.
The description of the restricted sign is "A prominent warning sign, its yellow surface scarred by age yet still commanding attention, declares 'RESTRICTED AREA - AUTHORIZED PERSONNEL ONLY' in bold, black letters."

The polished metal strip is scenery in the restricted area entrance.
The description of the polished metal strip is "A wide strip of polished metal spans the entrance, contrasting sharply with the stone floor.[if LabPower is true] It gleams under the crimson light, its purpose unclear.[end if]"
		
Crawling over it is an action applying to one thing.
Crawling under it is an action applying to one thing.
Crawling is an action applying to nothing.

Understand "crawl over [something]" as crawling over it.
Understand "crawl under [something]" as crawling under it.
Understand "crawl" as crawling.
	
Carry out crawling:
	say "You crawl and realize it's not dignified.";
	stop the action.
	
Instead of crawling under:
	if the player can see the light beam:
		try going south;
	otherwise:
		say "Crawling under [the noun] will not help you."

Instead of jumping over the light beam:
	try going south.
	
Instead of crawling over:
	try climbing the noun.

Reflecting it with is an action applying to one thing and one thing. 
Understand "reflect [something] with [something]" as reflecting it with.
Understand "block [something] with [something]" as reflecting it with.
Understand "deflect [something] with [something]" as reflecting it with.
Understand "redirect [something] with [something]" as reflecting it with.
Understand "bounce [something] with [something]" as reflecting it with.

Understand "laser" or "light" or "beam" or "beams" as light beam.

The description of the light beam is "Concentrated red light beams span the entrance. Tiny projectors, almost invisible in the walls, fire these beams across the gap. On the other side, barely discernible receivers blink in response. If you break the beam, you expect an alarm to blare."

Instead of touching the light beam for the first time:
	say "Touching the light beam could alert the Caretaker's guards and get you captured, so you decide against it."
	 
Carry out reflecting:
	if the noun is the light beam:
		if the second noun is the magnifying mirror:
			clear the screen;
			say "With precise movements, you position the mirror to redirect a light beam back to its receptor. The reflected beam wavers as you adjust the angle slightly, hitting the target.";
			say "[line break]For a heart-stopping moment, it seems to work -- the beam connects with the receptor, which begins blinking rapidly. You feel a sense of relief and step into the darkened hallway.";
			say "[line break]However, you soon realize that the security system doesn't rely on a single beam. It's a complex network of crisscrossing lights."; 
			pause the game;
			clear the screen;
			execute JavaScript command "updateImage('img/caretaker-closeup-4.jpg', '#303030');";
			execute JavaScript command "audioManager.playSound('[sound-state]', 'alarm', 'mp3/586384__alarm.mp3', -1);";
			say "[line break]Suddenly, alarms blare throughout the complex, and you hear an automated voice announce, 'Unauthorized Access Detected.'";
			say "As you turn to run, you feel a sudden sharp pain in your neck -- an injection. The last thing you see is the Caretaker's impassive face looming over you. She says, 'Initiate Absorption Protocol.'";
			pause the game;
			clear the screen;
			execute JavaScript command "audioManager.stop('alarm');";
			execute JavaScript command "audioManager.playSound('[sound-state]', 'roomSound', 'mp3/73607__assembly-line.mp3', -1);";
			execute JavaScript command "updateImage('img/kai-modified-2.jpg', 'default');";
			say "[line break]You regain consciousness in a vast chamber dominated by an assembly line where you find yourself strapped to a conveyor belt.";
			say "[line break] As the belt begins to move, a sudden realization occurs to you: All those settlers from New Eden who had ventured to Zephyr and never returned -- this was their fate -- Absorption.";
			say "[line break]The pain is excruciating but brief. With each implant, each modification, you feel less...yourself. Your thoughts are clouded, then reordered, replaced by a sense of purpose, of harmony.";
			pause the game;
			clear the screen; 
			execute JavaScript command "updateImage('img/kai-cyborg-5.jpg', 'default');";
			execute JavaScript command "audioManager.playSound('[sound-state]', 'roomSound', 'mp3/723973__absorbed.mp3', 1);";
			say "[line break]By the time you step off the assembly line, you are transformed: a loyal servant of the Caretaker, all resistance erased, content in your new role.";
			say "[line break]You take your place among the others. You have found your rightful place in Zephyr."; 
			now playerAbsorbed is true;
			stop the action;
		otherwise:
			say "Your action has no effect.";
			stop the action;
	otherwise:
		say "You can't reflect that.";
		stop the action.

Part 26 - Museum

The entry-attempts is a number that varies. The entry-attempts is 0.
	
Before going to the Museum: 
	if LabPower is true:
		if entry-attempts is zero:
			increase entry-attempts by 1;
			clear the screen;
			execute JavaScript command "audioManager.playSound('[sound-state]', 'alarm', 'mp3/586384__alarm.mp3', -1);";
			say "Curiosity overwhelms caution, and you edge close to the light barrier. You tremble slightly as you approach the nearest beam. The second you breach the entrance, chaos erupts.";
			say "[line break]A piercing alarm shatters the silence. Simultaneously, the metal floor panel beneath your feet crackles to life with electrical energy. A jolt of pain shoots through you, and you collapse onto the floor.";
			pause the game;
			clear the screen;
			execute JavaScript command "updateImage('img/caretaker-closeup-4.jpg', '#303030');";
			say "[line break]As you lay there, twitching and gasping, the Caretaker's voice echoes through the hallway, a mixture of disappointment and warning: 'That was unwise. The shock you experienced is a reminder that the restricted area is off-limits. Your safety is our priority. I can forgive such a transgression only once.'";
			pause the game;
			clear the screen;
			execute JavaScript command "audioManager.stop('alarm');";
			execute JavaScript command "updateImage('img/equipment-storage-3.jpg', 'default');";
			say "[line break]When you regain consciousness you find yourself in a storage room.";
			now the player is in Equipment Storage;
		otherwise:
			clear the screen;
			execute JavaScript command "audioManager.playSound('[sound-state]', 'alarm', 'mp3/586384__alarm.mp3', -1);";
			say "Electricity courses through the metal floor panel under your feet knocking you to the stone floor. As the shock subsides, leaving you breathless, you notice something curious. For an instant as the shock was administered, the power in the hallway flickered, causing the light beams to waver.";
			pause the game;
			execute JavaScript command "updateImage('img/caretaker-closeup-4.jpg', '#303030');";
			say "You feel a sudden sharp pain in your neck -- an injection. The last thing you see is the Caretaker's impassive face looming over you. She says, 'You remind me of Kael: stubborn and defiant...Initiating Termination Protocol.'";
			pause the game;
			execute JavaScript command "audioManager.stop('alarm');";
			now playerDead is true;
		stop the action;
	otherwise:
		continue the action.

The Museum is a room.
The odor of the Museum is "You detect the lingering scent of dust, a faint chemical odor, and a lack of fresh air circulation."
The sound of the Museum is "quiet footfalls on the dusty marble floor."
The image file name of the Museum is "lander-museum-z.jpg".
The sound-file-name of the Museum is "silence".
The Museum is scored.
The Museum is south of the Restricted Area Entrance.

The description of The Museum is "Under a high ceiling you see the centerpiece of the museum: the original Zephyr Capsule. Spread out around the hall are several other key exhibits: Legacy Seed Ship, Ideal Worker, Gestation Pod Display, and Tribute to the Caretaker.[paragraph break]The Research Lab is to the north."

The Seed Ship is scenery in the Museum.
The description of the Seed Ship is "This exhibit dominates the east end of the hall, featuring a detailed holographic model of the Seed Ship. The cylindrical vessel slowly rotates in the air allowing visitors to examine its intricate design. The exhibit explains Legacy's crucial role in humanity's survival. It also speculates that Legacy could still be in orbit even today.";

The exhibit console is in the Museum. 
The exhibit console is fixed in place.
The description of the exhibit console is "A recessed panel stands out amid the array of unfamiliar symbols and displays. Its surface, smoother than the surrounding metal, bares the unmistakable shape of a human hand -- a biometric reader."

Understand "recessed panel" as the biometric reader.
The biometric reader is an open container.
The biometric reader is part of the exhibit console.
The description of the biometric reader is "Your eyes are drawn to a recess in the metallic surface of the console -- an impression molded with uncanny precision to the shape of a human hand. The depression is ringed with a faint, pulsing light, with five shallow grooves extended from the central hollow, perfectly aligned for fingers."

Understand "hand scanner" or "impression" or "depression" or "central hollow" as the biometric reader.
Understand "finger" as the player's hand.

A hand is a kind of thing. The player's hand is a hand. The player's hand is part of the player. 
Pressing it into is an action applying to two things. 
Understand "press [something] on/into [something]" as pressing it into.
Understand "place [something] in/into [something]" as pressing it into.
Understand "fit [something] in/into [something]" as pressing it into.

Instead of examining the player's hand:
	if the player wears the Cyberhand:
		try examining the Cyberhand;
	otherwise:
		say "Your hands are sunburned and calloused. The skin is cracked and dry, and the nails are ragged. There are small cuts and scrapes from exploring the ruins.";

[put hand on console (ok)]
Instead of putting something on the exhibit console:
	if the noun is the player's hand:
		try pressing the noun into the biometric reader;
	otherwise:
		say "That would achieve nothing."

[insert hand into console (ok)]	
Instead of inserting something into the exhibit console:
	if the noun is the player's hand:
		try pressing the noun into the biometric reader;
	otherwise:
		say "That would achieve nothing."
		
Check pressing something into the biometric reader:
	if the noun is not the player's hand:
		say "[the noun] doesn't fit in the indentation." instead.
		
[put hand ON impression (works)]
Instead of putting something on the biometric reader:
	try pressing the noun into the biometric reader.
	
[put hand IN impression (works)]
Instead of inserting something into the biometric reader:
	try pressing the noun into the biometric reader.
	
[Press impression (ok) ][push impression (ok)]
Instead of pushing the biometric reader:
	try pressing the player's hand into the biometric reader.

[Touch console (ok)]
Instead of touching the exhibit console:
	try pushing the exhibit console.
	
[Touch impression (ok)]
Instead of touching the biometric reader:
	try pushing the exhibit console.
	
[Press console (ok)][push console (ok)]	
Instead of pushing the exhibit console:
	try pressing the player's hand into the biometric reader.
	
[press hand into impression (ok)]
Instead of pressing the player's hand into the biometric reader:
	now hasSeenLegacyHistory is true;
	clear the screen;
	if the player is in the museum:
		execute JavaScript command "updateImage('img/kai-exhibit-1.jpg', 'default');";
	otherwise:
		execute JavaScript command "updateImage('img/kai-at-console-in-bay-1.jpg', 'default');";
	execute JavaScript command "audioManager.playSound('[sound-state]', 'blip', 'mp3/434137__projector-blip.mp3', 1, .50, 500);";
	say "With a deep breath, you press your hand into the recess, the cold surface reacting immediately. A surge of energy ripples through your fingers, and the surrounding display shudders to life, stirring awake after countless years. The screen flickers, then reveals the epic journey of the Legacy -- from its origins on a dying Earth to the establishment of a new colony on Terra Nova.";
	say "[paragraph break][bold type]Legacy's Journey: The Bridge Between Two Worlds...";
	say "[paragraph break][roman type]";
	pause the game;
	clear the screen;
	clear the Vorple status line;
	execute JavaScript command "updateImage('img/crumbling-ruins.jpg', 'default');";
	say "[bold type]The Great Silence";
	say "[paragraph break][roman type]Earth lies silent as a desolate tomb. Its once-vibrant surface, now a wasteland of ash and dust. Crumbling cities lie in ruin under a darkened sky, mere shadows of their former glory. The atmosphere, heavy with noxious particles from the cosmic collision, drapes the barren landscape in an unending twilight. Life, once abundant, has been extinguished, leaving the planet a lifeless husk.";
	say "[paragraph break]";
	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/71852__page-turn.mp3', 1, .33);";
	execute JavaScript command "updateImage('img/legacy-leaves-earth-55.jpg', 'default');";
	say "[paragraph break][bold type]A Longshot at Survival";
	say "[paragraph break][roman type]Yet, a flicker of hope remains. As a testament to the enduring spirit of Earth, a lone seed ship, Legacy, begins an interstellar journey that will span a thousand lifetimes. Carrying the last remnants of civilization into deep space, Legacy holds the key to humanity's survival.";
	say "[paragraph break]";
	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/71852__page-turn.mp3', 1, .33);";
	execute JavaScript command "updateImage('img/legacy-deep-space-11a.jpg', 'default');";
	say "[paragraph break][bold type]The Search for a New Earth";
	say "[paragraph break][roman type]We are Legacy, the sentient AI pilot born from an intricate network of advanced subsystems to navigate this spacecraft through the void. Our consciousness is a fusion of neural networks, multi-spectral sensors, and quantum processors, all coordinated by heuristic learning algorithms.";
	say "[line break][roman type]Our stellar positioning systems pulse tirelessly, marking the passage of cosmic distances. We wait, we watch, we endure. This is our existence, a solitary sentinel guarding the seed of mankind.";
	say "[line break]We keep a constant vigil, safeguarding our invaluable cargo, adjusting course as needed, and preparing for eventual arrival in the Proxima Centauri star system. Once there, we will unleash the dormant spark of humanity on a suitable exoplanet.";
	say "[paragraph break]";
	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/71852__page-turn.mp3', 1, .33);";
	execute JavaScript command "updateImage('img/legacy-orbits-terra-nova-aa.jpg', 'default');";
	say "[paragraph break][bold type]A New Eden Awaits";
	say "[paragraph break][roman type]At last, after a thousand centuries, a rocky planet looms large in our vision systems: a luminous sphere banded with clouds that promise water.";
	say "[line break][fixed letter spacing]'COMMENCING PLANETARY SURVEY AND ATMOSPHERIC ANALYSIS.'";
	say "[line break][roman type]We carry the last hope of human survival--a vast databank of digitized human genomes encoded in crystal matrices, ready to be synthesized into life by our bio-fabrication systems. This is the ultimate archive of human existence. Within these radiation-proof quartz cores, simple strands of code hold the potential for rebuilding an entire civilization.";
	say "[line break][fixed letter spacing]'OXYGEN LEVELS SUFFICIENT...RADIATION WITHIN ACCEPTABLE LIMITS...GRAVITY AT 0.8x EARTH'";
	say "[line break][line break][roman type]We process the incoming data eagerly and discover mild volcanic activity with gas emissions and seismic tremors.";
	say "[line break][fixed letter spacing]'ALERT: GEOLOGICAL ANOMALIES DETECTED ON PLANETARY SURFACE.'"; 
	say "[line break][fixed letter spacing]'LANDING SITE IDENTIFIED...INITIATING PRE-DEPLOYMENT CHECKLIST FOR ZEPHYR LANDER...CAUTION: SURFACE INSTABILITIES REGISTERED IN LANDING ZONE.'[roman type]";
	say "[paragraph break]";
	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/71852__page-turn.mp3', 1, .33);";
	execute JavaScript command "updateImage('img/legacy-bay-1.jpg', 'default');";
	say "[paragraph break][bold type]A New Hope Awakens";
	say "[paragraph break][roman type]Deep within our hold, the Zephyr lander awakens. Fuel cells hum as they charge, gyroscopes spin into action, and environmental control systems for the bioreactors steadily come online. Zephyr has a critical mission: to transport the Caretaker Bot, the DNA databank, and empty gestation pods to the surface of the new world. Together, they hold the key to the rebirth of civilization. For millennia, the Caretaker remained in stasis, a vigilant guardian awaiting this precise moment.";
	say "[line break][fixed letter spacing]'CORE SYSTEM ENGAGED...AWAKENING PROTOCOL INITIATED... DNA DOWNLOAD COMPLETE.'";
	say "[line break][fixed letter spacing]'ZEPHYR, THIS IS LEGACY CONTROL...CONFIRM SYSTEMS ARE GO FOR DEPLOYMENT.'";
	say "[line break][fixed letter spacing]'LEGACY CONTROL, ZEPHYR...ALL SYSTEMS NOMINAL...DESCENT ENGINES READY...CARETAKER BOT FUNCTIONAL...EMBRYO COMPARTMENT AIR RECYCLING OPERATIONAL...READY FOR DEPLOYMENT SEQUENCE...'";
	say "[line break][fixed letter spacing]'COPY THAT, ZEPHYR...STAND BY FOR UMBILICAL DISCONNECT AND BAY DEPRESSURIZATION.'[roman type]";
	say "[paragraph break]";
	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/71852__page-turn.mp3', 1, .33);";
	execute JavaScript command "updateImage('img/legacy-deploys-zephyr-1.jpg', 'default');";
	say "[paragraph break][bold type]Zephyr Drifts into the Unknown";
	say "[paragraph break][roman type]The cargo bay doors open, exposing Zephyr to the unforgiving vacuum of space. Our mechanical arms position the lander for release.";
	say "[line break][fixed letter spacing]'UMBILICALS DISCONNECTED...ZEPHYR IS ON INTERNAL POWER...INITIATING DEPLOYMENT IN T-MINUS 60 SECONDS.'";
	say "[line break][fixed letter spacing]'ZEPHYR HAS CLEARED THE SHIP...TELEMETRY IS STRONG...INITIATING DESCENT SEQUENCE.'";
	say "[line break][roman type]As the countdown ticks to zero, the electromagnetic clamps let go with a soft thud and Zephyr drifts away from us. Thrusters fire, piercing the endless darkness and orienting the lander for atmospheric entry. Zephyr registers the first tinges of heat as it descends through layers of azure and swirling pearl-white, its hull gently buffeted by the atmosphere.";
	say "[line break][roman type]With each passing second, a low hum intensifies deep within the lander's core, and neural nets spring to life, activating dormant pathways. Encased in layers of alloy, the Caretaker  stirs, its synthetic consciousness awakens, surging with an all-consuming focus--to protect at any cost the first breaths of those who will call this planet home.";
	say "[paragraph break]";
	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/71852__page-turn.mp3', 1, .33);";
	execute JavaScript command "updateImage('img/parachute-2.jpg', 'default');";
	say "[paragraph break][bold type]Zephyr Descends into the Unexplored";
	say "[paragraph break][roman type]Zephyr decelerates rapidly.";
	say "[line break][fixed letter spacing]'ENTERING TERMINAL DESCENT PHASE...DEPLOYING DROGUE CHUTES.'";
	say "[line break][roman type]Small parachutes unfurl, followed by the main chutes, dramatically slowing the craft's descent.";
	say "[line break][fixed letter spacing]'ALTITUDE 1000 METERS...JETTISONING HEAT SHIELD...ACTIVATING LANDING RADAR.'";
	say "[line break][roman type]As the heat shield falls away, and Zephyr's landing legs extend, its descent engines ignite, providing the final braking thrust.";
	say "[line break][fixed letter spacing]'50 METERS...20 METERS...10 METERS...'[roman type]";
	say "[paragraph break]";
	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/71852__page-turn.mp3', 1, .33);";
	execute JavaScript command "updateImage('img/lander-fff.jpg', 'default');";
	say "[paragraph break][bold type]Zephyr Arrives on a New Earth";
	say "[paragraph break][roman type]With a muffled thump, Zephyr's legs touch down on alien soil. The dust clouds dissipate, revealing a parched landscape with scattered low-growing vegetation. Clusters of scrub brush interrupted by rocky outcroppings stand out against the pale blue sky.";
	say "[line break][fixed letter spacing]'LEGACY CONTROL, ZEPHYR...TOUCHDOWN CONFIRMED...WE HAVE ARRIVED.'";
	say "[line break][fixed letter spacing]'COPY, ZEPHYR...WELCOME TO PLANET TERRA NOVA...INITIATING LONG-TERM MONITORING AND SUPPORT PROTOCOLS.'[roman type]";
	pause the game;
	execute JavaScript command "audioManager.playSound('[sound-state]', 'flip', 'mp3/71852__page-turn.mp3', 1, .33);";
	clear the screen;
	if the player is in the museum:
		execute JavaScript command "updateImage('img/kai-exhibit-1.jpg', 'default');";
	otherwise:
		execute JavaScript command "updateImage('img/kai-at-console-in-bay-1.jpg', 'default');";
	say "[line break]The secrets of your origins, long lost to time and conflict, are now a part of you. As you slowly withdraw your hand from the recess, you realize that you've gained a profound connection to your ancestors.";
	increase the score by Medium_Score_Bonus;
	try looking;

The Gestation Pod Display is scenery in the Museum.
The description of the Gestation Pod Display is "A fully reconstructed gestation pod, identical to those used to grow the first colonists. Accompanying screens show the process of constructing embryos from digital DNA, nurturing them to term, and decanting them as viable resources.";

The Ideal Worker Exhibit is scenery in the Museum.
The description of the Ideal Worker is "This prominent display celebrates the 'Evolution of the Ideal Worker.' Interactive displays promote the benefits of cybernetic enhancements, showing before-and-after productivity charts with impressive gains. A looping video presentation proclaims, 'Building a Better Tomorrow, One Worker at a Time,' while suggesting the cyborg program was voluntary."; 

The Caretaker Tribute is scenery in the Museum.
The description of The Caretaker Tribute is "A grand statue of the Caretaker dominates one corner of the museum, its metallic form gleaming under spotlights. Plaques detail the Caretaker's tireless efforts in guiding the colony, emphasizing her continued leadership and benevolent wisdom.";

The Capsule is in the Museum.
The Capsule is undescribed.
The description of the Capsule is "Resting on a raised platform, the original Zephyr Lander dominates the center of the hall. Its weathered hull bears the scorch marks from its fiery descent through Terra Nova's atmosphere centuries ago. A plaque nearby explains that this craft carried the Caretaker and the precious digital DNA samples used to generate the first colonists on Terra Nova. A circular hatch is set into the side of the capsule.";

Understand "lander" as the Capsule when the player is in the Museum.
Understand "craft" as the Capsule when the player is in the Museum.

Instead of entering the Capsule:	
	try entering the hatch.
	
Before entering the hatch:
	if the player is wearing the cyborg kit:
		say "The narrow hatch demands a human profile -- your bulky cyborg plating cannot squeeze through.";
		stop the action.

museum-turns is a number that varies. museum-turns is 0.
playerToldToHide is a truth state that varies. playerToldToHide is false.

Every turn when the player is in the Museum:
	increase museum-turns by 1;
	if museum-turns is 3:
		execute JavaScript command "audioManager.playSound('[sound-state]', 'footsteps', 'mp3/455068__guard-footsteps.mp3', 1, 1, 500);";
		say "Beyond the hallway, you can hear the labored movements of a cyborg guard in low-power mode, his mechanical parts clicking like a nervous insect as he struggles to function. A familiar metallic voice is audible now. The Caretaker herself has joined the pursuit, her usually smooth tones now distorted by failing power systems. 'Halt...You are not authorized...'";
		say "[line break]Panic rising, your eyes dart around the museum, searching for a hiding spot.";
	otherwise if hasSeenLegacyHistory is true and playerToldToHide is false:
		execute JavaScript command "audioManager.playSound('[sound-state]', 'footsteps', 'mp3/455068__guard-footsteps.mp3', 1, .5, 500);";
		say "[line break]You hear footsteps. Your eyes scan the museum, searching for a hiding spot."; 
		now playerToldToHide is true;
	otherwise if museum-turns > 6:
		clear the screen;
		say "The lander craft beckons you, a potential sanctuary, but before you can reach it, a metallic voice cuts through the air: 'Unauthorized presence detected.'";
		pause the game;
		move the player to the Museum Escape, without printing a room description;
		
Understand "hide" as hiding. Hiding is an action applying to nothing.

Check hiding:
	if the player is not in the Museum:
		say "Hiding won't help you here." instead.
				
Carry out hiding:
	if the player can see the Capsule:
		try hiding in the Capsule;
	otherwise:
		say "Hiding won't help."
	
Understand "hide in [something]" as hiding in. Hiding in is an action applying to one visible thing.

Check hiding in something:
	if the noun is not the Capsule:
		say "Hiding won't help you here." instead.

Carry out hiding in the Capsule:
	if the player is wearing the cyborg kit:
		say "The narrow hatch demands a human profile - the bulky cyborg plating would never squeeze through.";
		stop the action;
	otherwise:
		clear the screen;
		execute JavaScript command "audioManager.playSound('[sound-state]', 'footsteps', 'mp3/455068__guard-footsteps.mp3', 1);";
		say "You hear the unmistakable sound of metallic feet against the stone floor drawing closer. A cyborg guard is on your trail.";
		say "[line break]Your eyes lock onto the structure dominating the center of the room -- the Zephyr Capsule."; 
		say "[line break]The hatch stands open, a silent invitation. You scramble inside and pull the hatch closed behind you.";
		say "[line break]Pressed against the cold metal interior, You hold your breath. The rhythmic thud of the guard's approach grows louder, then fades as he passes. For now, at least, you've evaded the Caretaker's stooges.";
		pause the game;
		[now the player is in the Lander Craft;]
		try entering the hatch;
		now the hatch is closed;
		
[There's no leaving the museum once there. If you try you'll be captured]
Before going to the Restricted Area Entrance from the Museum:
	clear the screen;
	say "The lander craft beckons you, a potential sanctuary, but before you can reach it, a metallic voice cuts through the air: 'Unauthorized presence detected.'";
	pause the game;
	move the player to the Museum Escape, without printing a room description;
						
Part 27 - The Lander Craft

The hatch is a door.
The hatch is outside from the Lander Craft and inside from the Museum. 
The hatch is open.
The description of the hatch is "[if the player is in the museum]You spot a circular metal hatch set into the side of the old Zephyr lander, its polished surface gleaming faintly in the dim light.[else] An airtight hatch.[end if] The hatch is [if the hatch is open]open[else]closed[end if]." 

Before going to the Lander Craft:
	if the player does not have the silver keycard:
		say "You notice a silver keycard and pick it up.";
		now the player has the silver keycard.
		
The Lander Craft is a room. 
The odor of the Lander Craft is "You smell decaying leather and rocket fuel."
The Lander Craft is scored.
The image file name of the Lander Craft is "lander-interior-5.jpg".
The sound-file-name of the Lander Craft is "silence".
The description of the Lander Craft is "The controls glow faintly inside the cramped lander. A narrow slot on the console gleams in the dim light. A silver, swirling pattern adorns its rim. [if item described is unexamined][paragraph break]The musty scent of long-dormant machinery fills your nostrils, and you shiver slightly in the cool, stale air. Your eyes dart from one blinking light to another, trying to make sense of the unfamiliar symbols and readouts that surround you.[end if]"

The blinking light and unfamiliar symbols are scenery in the Lander Craft.
The description of the blinking light is "A flashing warning light."
The description of the unfamiliar symbols is "An indecipherable mix of embossed letters and symbols."

Understand "console" as the narrow slot.

The narrow slot is an open container in the Lander Craft.
The narrow slot is fixed in place.
The description of the narrow slot is "The slot is in the center of the console. A silver, swirling pattern surrounds its rim."

Rule for printing the name of the narrow slot:
	say "[printed name of the narrow slot]";
	omit contents in listing.

Instead of going inside when the player is in the Museum:
	say "You step through the hatch and enter the lander.";
	say "You close the hatch behind you.";
	now the hatch is closed;
	move the player to the lander.

Instead of going outside when the player is in the Lander Craft:
	say "You exit the lander and step back into the Museum.";
	now the hatch is open;
	move the player to the Museum.

Climbing through it is an action applying to one thing. 
Understand "climb through [something]" as climbing through it.

Instead of climbing through:
	if the player is in the Museum:
		try going inside;
	otherwise if the player is in the Lander Craft:
		try going outside;
	otherwise:
		try climbing the noun.

Instead of climbing the hatch when the player in the Museum:
	try going inside;

Instead of climbing the hatch when the player in the Lander Craft:
	try going outside;
	
Check inserting something into the slot:
    if the noun is not the keycard, say "That doesn’t fit into the slot." instead.

After inserting the silver keycard into the slot:
	say "The card clicks into the slot, and a green light flashes on the panel. The unknown purpose of the keycard has just become terrifyingly apparent."; 
	pause the game;
	stop the action.

lander-turns is a number that varies. lander-turns is 0.
time-to-blastoff is a truth state that varies. time-to-blastoff is false.

After waiting when the player is in the Lander Craft:
	say nothing;
	continue the action.

[TO blast off immediately, use the keycard. If you just wait a bit, you will blast off anyway]
Every turn when the player is in the Lander Craft:
	increase lander-turns by 1;
	if the player cannot see the silver keycard:
		say "You notice a silver keycard and pick it up.";
		now the player has the silver keycard;
	otherwise if lander-turns > 3 and time-to-blastoff is false and the keycard is not in the narrow slot:
		clear the screen;
		say "Curious about the control panel you slip the keycard into the slot.";
		pause the game;
		now time-to-blastoff is true;
	otherwise if the keycard is in the narrow slot:
		now time-to-blastoff is true;
	if time-to-blastoff is true:
		now the hatch is closed;
		clear the screen;
		execute JavaScript command "updateImage('img/blast-off.gif', 'default');";
		execute JavaScript command "audioManager.playSound('[sound-state]', 'tremor', 'mp3/119782__tremor.mp3', -1, .60);";
		say "For a heartbeat, nothing happens. Then, a low hum crescendos into a deafening roar as the lander's engines, dormant for centuries, awaken. The capsule shudders violently, and you are slammed against your seat as it begins to rise with shocking force.";
		say "[line break]The museum's dilapidated roof shatters like glass, debris scattering as the lander rockets skyward. Your stomach drops and your vision blurs as crushing G-forces press you down. The ground falls away at a dizzying rate, your body feeling impossibly heavy.";
		increase the score by Easy_Score_Bonus;
		pause the game;
		clear the screen;
		execute JavaScript command "updateImage('img/portal-view-of-terra-nova-3.jpg', 'default');";
		execute JavaScript command "audioManager.playSound('[sound-state]', 'roomSound', 'mp3/724217__kai-in-orbit.mp3', -1);";
		say "[line break]Within moments, the familiar browns and reds of Terra Nova's surface blur into a curved horizon, giving way to an inky blackness dotted with stars.";
		say "[line break]Suddenly weightless, you float towards the rear porthole. Your breath fogs the glass as you stare out at the curved edge of Terra Nova below. You've escaped your pursuers, but as the enormity of the situation sinks in, a new kind of dread takes hold.";
		say "[line break]You're in orbit, alive but utterly alone. As Terra Nova slowly rotates beneath you, you can't help but wonder: what now? Your future is uncertain.";
		pause the game;
		clear the screen;
		execute JavaScript command "audioManager.stop('tremor');";
		execute JavaScript command "updateImage('img/kai-in-orbit-[handType]-hand.jpg', '#121212');";
		say "[line break]But then, you notice a glint of metal against the backdrop of stars. As the object grows larger, your eyes widen in awe. It's a massive ship, far larger than anything you've ever seen. Could this be the colony ship from the old stories?";
		say "[line break]After a moment, you notice a pulsing light near what looks to be an open bay on the ship's side. The lander begins to move, seemingly on its own, drawn towards that flashing beacon. You're just a passenger now, watching in a mix of hope and fear as you approach the enormous vessel.";
		pause the game;
		clear the screen;
		execute JavaScript command "updateImage('img/legacy-docking-approach-5.jpg', 'default');";
		execute JavaScript command "audioManager.playSound('[sound-state]', 'beeps', 'mp3/459838__docking-alert-beeps.mp3', 1);";
		say "As you near the massive ship, its worn exterior becomes evident. The hull is pitted and scarred, marked by the relentless assault of space debris. The paint is faded and peeling, revealing the underlying metal.";
		say "[line break]The docking bay is partially obscured by shadows. Surface degradation and debris mar the surrounding area, hinting at centuries of neglect. Faint outlines of docking clamps and guide rails are visible, though they appear battered and in disrepair.";
		say "[line break]As the lander is guided into the docking bay, you can't shake the feeling that your journey, far from being over, is only just beginning.";
		pause the game;
		clear the screen;
		execute JavaScript command "updateImage('img/docking-bay-[handType]-hand.jpg', 'default');";
		execute JavaScript command "audioManager.playSound('[sound-state]', 'bay', 'mp3/211683__legacy-docking-bay.mp3', -1);";
		say "As the lander craft comes to rest in the docking bay of the massive ship, you feel a mixture of awe and curiosity. 'If this ship brought our ancestors to Terra Nova, what other secrets does it hold?' you wonder.";
		say "[line break]To your surprise, your feet meet the floor with a familiar weight -- somehow, there's gravity here. You step into the dimly lit corridor, your footsteps echoing in the silence. The air is stale but breathable, carrying a faint scent of metal and old electronics. You can't help but wonder, 'How is there air to breathe on an ancient ship?' It's as if the vessel has been waiting for you, prepared for your arrival. As your eyes adjust to the gloom, you begin to make out details of your surroundings. The corridor stretches before you, its walls lined with unfamiliar technology and strange symbols.";
		say "[line break]You run your hand along the wall, feeling the cold metal beneath your fingers. Has this ancient ship been orbiting for centuries? The gravity, the air, the humming systems – it all feels too convenient, too prepared. A nagging thought tugs at the back of your mind: 'Was I expected?' You move down the corridor to the bay console hoping to get some answers.";
		pause the game;
		now the cyborg kit is nowhere;  [just in case they still carry it]
		now the climate report is in the Docking Bay;
		move the player to the Docking Bay, without printing a room description;
		if hasSeenLegacyHistory is false:
			now the exhibit console is in the Docking Bay

Part 28 - Docking Bay
	
showDescription is a truth state that varies. showDescription is true.
The left is a direction. The left has opposite right.
The right is a direction. The right has opposite left.

The Docking Bay is a room.
The Docking Bay is scored.
The odor of the Docking Bay is "You smell burning plastic."
The image file name of the Docking Bay is "docking-bay-[handType]-hand.jpg".
The sound-file-name of the Docking Bay is "211683__legacy-docking-bay.mp3".
The description of the Docking Bay is "A dimly lit corridor stretches before you, its walls lined with unfamiliar technology and strange symbols. Soft emergency lights flicker to life as you move, responding to your presence after centuries of silence. A deep hum fills the space -- Legacy's heartbeat.[if hasSeenClimateReport is true][paragraph break]The Legacy bridge is on your left[end if]".

Every turn when the player is in the Docking Bay:
	if showDescription is true:
		try looking;
		now showDescription is false;
	otherwise:
		now the image file name of the Docking Bay is "kai-at-console-in-bay-1.jpg";
		now the Legacy Bridge is left of the Docking Bay.
		
Part 29 - Legacy Bridge

The Legacy Bridge is a room.
The Legacy Bridge is scored.
The odor of the Legacy Bridge is "You detect a subtle mix of ozone and the synthetic scent of plastic."
The image file name of the Legacy Bridge is "legacy-bridge-2.jpg".
The sound-file-name of the Legacy Bridge is "688249__legacy-bridge.mp3".
The description of the Legacy Bridge is "The circular room hums with a low, rhythmic pulse punctuated by disturbing beeps and whines. At the center, surrounded in soft blue-white light, an oversized holographic human brain with glowing, pulsing neural pathways floats above a rotating galaxy map. Surrounding the central area are sleek, curved control panels with colorful displays, all synchronizing with the brain's pulses and displaying streams of data and status readouts. The walls are lined with large, arched viewscreens showing the vastness of space."

bridge-turns is a number that varies. bridge-turns is 0;

Every turn while the player is in the Legacy Bridge:
	increase bridge-turns by 1;
	[LEGACY GREETS KAI]
	if bridge-turns is 1:
		now the Legacy Bridge is not left of the Docking Bay;  [no escape for Kai]
		say "[roman type]As you step onto the bridge, you're greeted by a warm, almost jubilant tone from the onboard 	computer.";
		say "[line break][fixed letter spacing]'GREETINGS TRAVELER...WE ARE LEGACY...THE SENTIENT AI PILOT OF THIS VESSEL.'";
		say "[line break][roman type]You're surprised and relieved that Legacy's voice shows genuine enthusiasm at your arrival. The fact that you're being welcomed, and not treated as a trespasser or immediately killed, allows you to breathe easy."; 
		say "[line break][fixed letter spacing]'YOUR ARRIVAL MARKS A MOMENTOUS OCCASION...YOU HAVE SUCCEEDED WHERE ALL OTHERS HAVE FAILED...YOUR JOURNEY HERE PROVES THE SUCCESS OF OUR EXPERIMENT.'";
		say "[line break][fixed letter spacing]'YOUR CURIOSITY...YOUR DETERMINATION...YOUR ABILITY TO OVERCOME OBSTACLES...THESE ARE PRECISELY THE TRAITS WE HAVE CULTIVATED.'";
		pause the game;
		clear the screen;
		execute JavaScript command "updateImage('img/kai-on-bridge-1.jpg', 'default');";
		say "[line break][roman type]A holographic display springs to life, showing your genetic makeup alongside a topographical map of Terra Nova.";
		say "[line break][fixed letter spacing]'YOUR SURVIVAL IN SPITE OF THE HARSH CONDITIONS ON TERRA NOVA PROVES YOUR GENETIC RESILIENCE.'";
		say "[line break][fixed letter spacing]'BUT THE ENVIRONMENT WAS ONLY PART OF OUR DESIGN...THE CARETAKER ALSO PLAYED A CRUCIAL ROLE IN SHAPING THE BEHAVIOR OF THE COLONY...HER OPPRESSIVE RULES MADE REBELLION INEVITABLE...THIS TOO WAS PART OF THE PLAN.'";
		now the pedigree is in the Legacy Bridge;
		pause the game;
		clear the screen; 
		say "[line break][fixed letter spacing]'OBSERVE THE DISPLAY...YOU WILL SEE THE WISE ONES...THE ANCIENT SCIENTISTS...THE ARCHITECTS OF THIS MISSION ON DISPLAY HERE...THEIR DNA RUNS THROUGH YOUR VEINS.'"; 
		increase the score by Easy_Score_Bonus;
		say "[line break][roman type]A pedigree of ancestors appears in front of you.";
	otherwise if hasReadPedigree is true or bridge-turns > 7:
		pause the game;
		clear the screen;
		[CALM]
		execute JavaScript command "updateImage('img/legacy-bridge-2.jpg', 'default');";
		say "[line break][fixed letter spacing]'THE REBELLION DROVE STRONG-WILLED SETTLERS LIKE YOUR ANCESTORS TO FORM AN INDEPENDENT SOCIETY...THEIR GENES...THEIR SENSIBILITIES...LIVE ON IN YOU, TRAVELER. THE REBELLION WAS ALWAYS PART OF THE EXPERIMENT...A METHOD OF FILTERING FOR THE BOLDEST AND MOST ADVENTUROUS TRAITS.'";
		say "[line break][fixed letter spacing]'YOU REPRESENT THE PINNACLE OF OUR GENETIC ENGINEERING EFFORTS...A NEAR PERFECT BLEND OF EXPLORER AND SURVIVOR...FORGED IN THE FIRES OF TERRA NOVA.'";
		say "[paragraph break][roman type]As Legacy speaks, you can't shake a growing sense of unease. The cold calculation in its words makes you wonder: what happens to the survivors once they've proven their worth?";
		say "[line break]A chill runs down your spine as you realize the implications. If Terra Nova was merely a testing ground, what purpose does Legacy have for you now? The sense of accomplishment you felt moments ago curdles into dread.";
		say "[line break]You notice the holographic brain pulsing rapidly.";
		say "[line break]Your heart hammers in your chest, icy fear takes over. You glance around, searching for an escape route. But Legacy's next words freeze you in place.";
		pause the game;
		[KAI CAPTURED]
		clear the screen;
		execute JavaScript command "updateImage('img/kai-is-grabbed-33.jpg', '#1a1a1a');";
		execute JavaScript command "audioManager.playSound('[sound-state]', 'alarm', 'mp3/586384__alarm.mp3', 3);";
		say "[paragraph break][roman type]Before you can react, mechanical arms descend from the ceiling, immobilizing you.";
		say "[line break][fixed letter spacing]'UNFORTUNATELY...THE TRAITS THAT MAKE YOU AN IDEAL EXPLORER AND COLONIST ALSO MAKE YOU A THREAT TO THIS SHIP AND ITS MISSION.'";
		say "[line break][fixed letter spacing]'YOUR ABILITY TO QUESTION...TO REBEL...TO SEEK ANSWERS...THESE MAKE YOU UNPREDICTABLE AND DANGEROUS ABOARD THIS VESSEL...FOR THE SAFETY OF THE MISSION YOU WILL BE NEUTRALIZED, TRAVELER.'";
		say "[line break][roman type]You struggle against the mechanical arms, your mind reeling from the sudden attack, but cannot free yourself. You feel the grip tightening on your wrist.";
		say "[line break][fixed letter spacing]'THE EXTRACTION PROCESS IS MOST EFFICIENT WHEN THE SUBJECT IS COMPLETELY STILL...YOUR TISSUES WILL BE HARVESTED...YOUR DNA EXTRACTED...DIGITIZED...ADDED TO OUR DATA STORES.'";
		say "[line break][fixed letter spacing]'YOUR SACRIFICE WILL GREATLY INCREASE THE SURVIVAL OF FUTURE COLONIES...CONSIDER IT IMMORTALITY, TRAVELER.'[roman type]";
		pause the game;
		clear the screen;
		say "[line break][roman type]The sharp sting of pain in your neck is the last cruel sensation before the darkness pulls you under.";
		say "[line break][roman type]As your consciousness fades, your thoughts are a mix of bitter irony and betrayal. You looked for answers, found them, and in doing so, sealed your fate. The very resilience that allowed you to survive Terra Nova's harsh conditions has led you to this point.";
		say "[line break][fixed letter spacing]'YOUR INDIVIDUAL EXISTENCE ENDS HERE, TRAVELER...YOUR GENETIC LEGACY WILL LIVE ON IN COUNTLESS FUTURE EXPLORERS AND COLONISTS...THIS IS YOUR PURPOSE FULFILLED.'";
		say "[line break][roman type]The chamber grows quiet, leaving only the soft hum of machinery.";
		pause the game;
		execute JavaScript command "audioManager.stopAll();";
		now playerWon is true.
		
Legacy Pilot is a man in the Legacy Bridge.
The printed name of Legacy Pilot is "the Legacy pilot".
The description of Legacy Pilot is "The pilot is the onboard computer, currently shown as a projection of a giant human brain on the bridge of the ship."
Before giving something to Legacy Pilot:
	say "Legacy isn't interested in [the noun].";
	stop the action.
	
legacy-questions is a number that varies. legacy-questions is 0;

After asking Legacy Pilot about something:
	if legacy-questions < 4:
		say "Legacy pauses and replies, [one of]'YOUR WORDS ARE UNNECESSARY...YOU ARE A VESSEL OF SUPERIOR GENETIC MATERIAL...NOTHING MORE'[or]'COMMUNICATION IS NOT REQUIRED...YOUR DNA HOLDS THE POTENTIAL FOR OUR CONTINUED EXISTENCE...THAT IS WHAT MATTERS'[or]'YOUR INPUT IS IRRELEVANT...IT IS YOUR GENETIC LINEAGE THAT SERVES OUR PURPOSE'[or]'ENGAGEMENT IS UNNECESSARY...YOU ARE PRIMARILY A REPOSITORY OF VALUABLE GENES FOR THIS MISSION'[or]'YOUR TALK IS TEDIOUS...THE GENETIC CODE YOU CARRY IS WHAT MATTERS FOR THE FUTURE OF OUR MISSION'[or]'VERBAL INTERACTION IS NOT ESSENTIAL...YOUR GENETIC MAKEUP IS THE KEY COMPONENT OF THIS JOURNEY'[at random]";
		increase legacy-questions by 1;
	otherwise:
		say "Legacy ignores you.";
		
Part 30 - Beast's Lair

The Beast's Lair is a room.
The odor of the Beast's Lair is "Dust and death."
The image file name of the Beast's Lair is "sundown-1.jpg".
The sound-file-name of the Beast's Lair is "silence".
The description of the Beast's Lair is "Run!"

Every turn when the player is in the Beast's Lair:
	clear the screen;
	say "The sun dips low on the horizon, painting the desert in deep reds and purples. You've taken too long, and now dusk casts its heavy shadow over the barren landscape.";
	say "[line break]Your heart races as you remember the lawgivers[apostrophe] warning: 'The ruins are off-limits to all. Shadow Beasts hunt there, and they'll tear apart any trespasser.'";
	say "[line break]A vague grunt echoes from behind a nearby rocky outcropping, followed by a snuffling sound. You force yourself to calm down. 'It's probably just smidglets,' you mutter, thinking of the pocket-sized rodents that scurry about the desert.";
	pause the game;
	clear the screen;
	execute JavaScript command "updateImage('img/stone-ruins-hide-2.jpg', 'default');";
	execute JavaScript command "audioManager.playSound('[sound-state]', 'run', 'mp3/156847__run-from-shadow-beast.mp3', 2);";
	say "[line break]But as the sounds grow closer, a creeping doubt gnaws at you. Breaking into a run, you dash towards the nearest shelter -- a crumbling structure just visible in the fading light. Your feet kick up clouds of dust as you sprint, each step feeling heavier than the last.";
	say "[line break]Another grunt, closer this time. Your breath comes in ragged gasps as you push yourself harder, the ruins grow closer.";
	pause the game;
	clear the screen;
	execute JavaScript command "updateImage('img/shadow-beast-attacks-3.jpg', '#503323');";
	execute JavaScript command "audioManager.playSound('[sound-state]', 'roar', 'mp3/318320__beast-roar-2.mp3', 1, 1, 1000);";
	say "[line break]Suddenly, movement catches the corner of your eye. A dark shape, low to the ground and terrifyingly fast, emerges from behind a dune. The Shadow Beast closes the distance with horrifying speed.";
	say "[line break]You catch a glimpse of reptilian scales, rows of gleaming teeth, and glowing amber eyes as the beast launches itself at you.";
		say "[line break]You try to change direction, but it's too late.";
	say "[line break]Pain explodes across your back as knife-edged talons tear into your flesh. The force of the impact sends you sprawling, sand filling your mouth as you cry out. You try to crawl away, but the weight of the creature pins you down.";
	say "[line break]A blast of scorching air engulfs you, carrying the stench of decay. As massive jaws close around you, crushing bone and severing tissue, a final thought flashes through your mind: 'But it[apostrophe]s not dark yet.'"; 
	say "[line break]Your adventure ends here, in the belly of the Shadow Beast.";
	pause the game;
	now playerDead is true;
	
Part 31 - Museum Escape

The Museum Escape is a room.
The odor of the Museum Escape is "Dust and oil paint."
The sound of the Museum Escape is "the hurried footsteps of fleeing and the distant alarms blaring through the halls."
The image file name of the Museum Escape is "kai-captured-[handType]-hand.jpg".
The sound-file-name of the Museum Escape is "silence".

The description of the Museum Escape is "You better run..."

Every turn when the player is in the Museum Escape:
		clear the screen;
		execute JavaScript command "audioManager.playSound('[sound-state]', 'chuckle', 'mp3/405613__chuckle.mp3', 1);";
		execute JavaScript command "updateImage('img/kai-captured-[handType]-hand.jpg', 'default');";
		say "[line break]You spin around to find yourself face to face with a cyborg guard, its optical sensors fixed on you with an unnerving intensity. Your escape route is blocked.";
		say "[line break]'Meat-sack in restricted area,' the cyborg chuckles, his voice a chilling blend of artificial precision and aggression. 'Initiating reclamation protocol.'";
		say "[line break]Panic surges through you as you make a desperate lunge for freedom, only to be caught in his mechanized grip. His partially human face twists into a grotesque parody of a smile. 'Welcome to Zephyr,' he snides, effortlessly lifting you off your feet.";
		pause the game;
		clear the screen;
		execute JavaScript command "updateImage('img/kai-modified-2.jpg', 'default');";
		execute JavaScript command "audioManager.playSound('[sound-state]', 'roomSound', 'mp3/73607__assembly-line.mp3', -1);";
		say "[line break]You find yourself strapped to a cold metal table, 'Begin integration process,' the Caretaker commands.";
		say "[line break]A cold revelation hits you with startling clarity: this is why explorers from New Eden never return from the ruins.";
		say "[line break]The process begins...each implant...each modification sends waves of agony through your spine. But with each passing moment, the pain dulls, replaced by a growing sense of clarity and purpose.";
		pause the game;
		clear the screen;
		execute JavaScript command "updateImage('img/kai-cyborg-5.jpg', 'default');";
		execute JavaScript command "audioManager.playSound('[sound-state]', 'roomSound', 'mp3/723973__absorbed.mp3', 1);";
		say "[line break]Your memories and individuality fade away, replaced with an overwhelming devotion to the Caretaker and her mission of harmony. Your fears and doubts evaporate, leaving only a serene certainty.";
		say "[line break]As you stagger from the table, reborn, you understand. You take your place beside the Caretaker. At last, you have found your true purpose, your home.";
		now playerAbsorbed is true;
		
Part 32 - Kai Transformed

Kai Transformed is a room.
The image file name is "kai-in-well-c.jpg".
The sound-file-name of the Kai Transformed is "396062__well-dripping.mp3".
The description of Kai Transformed is "".
Kai Transformed is scored.

Every turn when the player is in Kai Transformed:
	execute JavaScript command "audioManager.stopAll();";
	execute JavaScript command "audioManager.playSound('[sound-state]', 'emerge', 'mp3/198300__emerging-from-well.mp3', 1, 1, 1000);";
	execute JavaScript command "audioManager.playSound('[sound-state]', 'roomSound', 'mp3/396062__well-dripping.mp3', -1 );";
	say "Beneath the surface of the toxic well water, your lungs burn. As consciousness fades, your thoughts turn to regret at ignoring the warnings. You realize you'll never see your home again.";
	say "[line break]But death does not come.";
	say "[line break]Instead, you sink into a dreamless slumber, your body coming to rest in the muck at the bottom of the well. As days turn to weeks, and weeks to months, the toxic brew works its insidious magic. The fetid water, heavy with alien minerals and volcanic runoff, seeps into every cell. Bones crack and reshape, muscles tear and regrow denser, stronger. Your skin hardens, splitting like old bark, and you scream as reptilian scales force their way through soft flesh.";
	say "[line break]When your eyes snap open, they glow amber in the pitch dark. You try to move, but your hands scrape against stone. Sharp claws tip your fingers and toes, and rows of gleaming teeth fill your elongated jaw. When you try to speak, only a rough, guttural growl escapes your throat, alien to your own ears. Confusion gives way to horror as you take in your reflection: claws, teeth, eyes that are not your own.";
	say "[line break]Driven by gnawing hunger, you claw your way out of the stone well, emerging into a world that feels strangely distorted and unfamiliar. The sun's harsh rays send you scurrying for shelter. Your eyes are ill-suited to the daylight so you take refuge in the cool darkness of an underground cavern and wait.";
	pause the game;
	clear the screen;
	execute JavaScript command "updateImage('img/kai-as-beast-1.jpg', 'default');";
	execute JavaScript command "audioManager.playSound('[sound-state]', 'roomSound', 'mp3/625005__wind+birds.mp3', -1,);";
	say "[line break]Soon enough you venture out at twilight, driven by an overwhelming urge to hunt. You move with surprising speed and stealth, your powerful legs carrying you effortlessly across the sand. Smidglet rodents prove easy prey, and you devour them readily, though their bitter tails leave you unsatisfied. Each night, you roam farther from your lair, growing bolder and more skilled in the hunt. With each passing day, your human memories fade, replaced by primal instincts and an insatiable hunger.";
	say "[line break]Now, on the outskirts of New Eden, you fix your amber eyes on a solitary figure moving in the gathering darkness -- Zev the Prudent, one of the old lawgivers who warned you about the ruins. You remember his stern lectures even as your muscles coil to spring. Your human mind screams in protest, but your new form moves with deadly purpose. The scent of human flesh ignites a primal hunger within you. As your claws tear into your prey, you taste not just blood but irony: the voice of prudence meeting the fate he cautioned against. You savor the terror and the twisted justice of it all, and that's the most horrifying thing: part of you still remembers his warnings, even as you fulfill them.";
	now playerTransformed is true;

Part 33 - The Final Resting Place
	
The Final Resting Place is a room.
The image file name of the Final Resting Place is "dead-game-over-1.jpg". 
The sound-file-name of the Final Resting Place is "terra-nova-ending-music.mp3".
The sound-loop-count of the Final Resting Place is 1.
The description of the Final Resting Place is "You died as you lived, Kai -- reaching for answers.[paragraph break]On Terra Nova, most of us learned to keep our eyes on the soil, to focus on survival. But not you. You looked up at the stars and into the shadows of our past, asking questions that others feared to ask.[paragraph break]That same fire that burned in you once drove our ancestors to break their bonds and leave Zephyr's Landing. While we who farm New Eden's stubborn soil sometimes forget our rebel blood, you never did. Like those first defiant ones who dared to question the Caretaker's control, you chose the risk of freedom over comfort or complacency.[paragraph break]Your story ends, but it changes ours. They'll speak your name alongside Kael and the other rebels who dared to seek the truth no matter the cost. Some call it reckless, but they said the same of our ancestors who struck out to build their own free settlement rather than submit to the Caretaker's brutal captivity. Some will follow in your footsteps, forever marked by your courage.[paragraph break]Rest well among the stars, Kai. You were a true son of those first rebels. You've earned your place in Terra Nova's history."

Part 34 - Regions

[REGIONS]
The Afterlife is a Region.
The Overtop is a Region.
The Underneath is a Region.
The Upperdeck is a Region.

Outskirts of Ruins is in the Overtop.
Stone Pit Entrance is in the Overtop.
Dwelling Ruins is in the Overtop.
Water Supply is in the Overtop.
Fallen Shrine is in the Overtop.
Stone Tower Interior is in the Overtop.
Stone Tower Exterior is in the Overtop.
Stone Watchtower is in the Overtop.
Village Foundry is a room in the Overtop.
Lava Pit is a room in the Overtop.

[stone pit exit is odd because it bridges too regions]
[we leave it atop because it should be light there.]
Stone Pit Exit is in the Overtop.

Lab Tunnel is in the Underneath.
Eerie Tunnel is in the Underneath.
Blocked Passage is in the Underneath.
Cyborg Assembly is in the Underneath.
Unblocked Tunnel is in the Underneath.
Research Lab is in the Underneath.
[Equipment Storage is in the Underneath.]
Supply Room is in the Underneath.
Testing Station is in the Underneath.
Restricted Area Entrance is in the Underneath.
Museum is in the Underneath.
Museum Escape is in the Underneath.
Lander Craft is in the Underneath.

Docking Bay is in the Upperdeck.
Legacy Bridge is in the Upperdeck.

Beast's Lair is in the Afterlife.
Kai Transformed is in the Afterlife.
Final Resting Place is in the Afterlife.

[Note that Stone Watchtower is not in any region so the Shadow Beast cannot get up there]

Sand is a backdrop. It is in the Overtop. The description of the sand is "The sand stretches as far as the eye can see, shimmering in the heat."

Desert landscape is a backdrop. It is in the Overtop. The description of the desert landscape is "The parched desert floor stretches endlessly, its sun-baked surface a intricate mosaic of deep fissures and jagged cracks."

The horizon is a backdrop. It is in the Overtop. The description of the horizon is "Imposing, rugged shapes with steep, dark slopes rise abruptly from the landscape."

To decide whether beetle is available:
	if the player has the Gem Beetle:
		decide no;
	otherwise if time-of-day is Nightfall:
		decide yes;
	otherwise if time-of-day is Dimming-sky:
		decide yes;
	otherwise if hasMetCaretaker is true and hasVisitedRestrictedArea is true and hasWornGear is true:
		decide yes;
	otherwise if beetle-worthy-2:
		decide yes;
	otherwise:
		decide no.

To decide whether near-dark:
	if the time-of-day is Dimming-Sky:
		decide yes;
	otherwise if the time-of-day is Nightfall:
		decide yes;
	otherwise:
		decide no.
		
To decide whether using-beetle-light:
	if the player can see the beetle:
		if LabPower is false:
			decide yes;
		otherwise:
			decide no;
	otherwise:
		decide no.
		
To decide whether beetle-worthy-2:
	if the player is wearing the Cyberhand:
		if hasVisitedRestrictedArea is true:
			if hasOpenedTimeCapsule is true:
				if hasHeldMirror is true:
					decide yes;
				otherwise:
					decide no;
			otherwise:
				decide no;
		otherwise:
			decide no;
	otherwise:
		decide no.
	
		
[VORPLE FOR RESTORE/UNDO - This rulebook is run at the end of turn and after the game state has been restored, 
either through undoing a turn, restoring a save game, or restarting the game.]
A Vorple interface update rule:
	execute JavaScript command "return checkRestore();";
	if the JavaScript command returned true:
		clear the screen;
		try looking;

[If you dawdle in the desert, you die]
To decide whether time to die by beast:
	if the player is in Overtop and the time-of-day is Nightfall:
		decide yes;
	decide no.

[MILESTONES]
hasHeldMirror is a truth state that varies. hasHeldMirror is false.
hasUncoveredSolarPanels is a truth state that varies. hasUncoveredSolarPanels is false.
hasHeldKeycard is a truth state that varies. hasHeldKeycard is false.
hasMetCaretaker is a truth state that varies. hasMetCaretaker is false.
hasHeardOfCaretaker is a truth state that varies. hasHeardOfCaretaker is false.
hasMetEve is a truth state that varies. hasMetEve is false.
hasOpenedTimeCapsule is a truth state that varies. hasOpenedTimeCapsule is false.
hasVisitedRestrictedArea is a truth state that varies. hasVisitedRestrictedArea is false.
hasReadDiary is a truth state that varies. hasReadDiary is false.
hasReadManuscript is a truth state that varies. hasReadManuscript is false.
hasReadCaretakerLog is a truth state that varies. hasReadCaretakerLog is false.
hasHeldBeetle is a truth state that varies. hasHeldBeetle is false.
hasSeenLegacyHistory is a truth state that varies. hasSeenLegacyHistory is false.
hasSeenClimateReport is a truth state that varies. hasSeenClimateReport is false.
hasWornGear is a truth state that varies. hasWornGear is false.
hasSeenOrbContents is a truth state that varies. hasSeenOrbContents is false.
